
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	// Родитель.
	Родитель = __ОбщегоНазначенияКлиентСервер.ПолучитьНеПустоеЗначение(
					Родитель,
					ПолучитьСвойство(ДанныеЗаполнения, "Родитель"));
	
	// ТипСтола.
	ТипКомнаты = __ОбщегоНазначенияКлиентСервер.ПолучитьНеПустоеЗначение(
					ТипКомнаты,
					ПолучитьСвойство(ДанныеЗаполнения, "ТипКомнаты", Родитель.ТипКомнаты));
	ТипКомнаты = __ОбщегоНазначенияКлиентСервер.ПолучитьНеПустоеЗначение(
					ТипКомнаты,
					Справочники.ТипыКомнат.ОсновнойТипКомнаты());
					
	// Номер и Наименование.
	Если Не ЭтоГруппа Тогда
	
		Номер = __ОбщегоНазначенияКлиентСервер.ПолучитьНеПустоеЗначение(
					Номер,
					ПолучитьСвойство(ДанныеЗаполнения, "Номер", Справочники.Комнаты.ПолучитьОчереднойКод(ЭтотОбъект)));
		Наименование = Справочники.Комнаты.ПолучитьНаименование(ЭтотОбъект);
		
	КонецЕсли; 					
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	// ТипСтола.
	Если ЭтоГруппа Тогда
		__ОбщегоНазначенияСервер.ИзменитьПроверяемыеРеквизиты(ПроверяемыеРеквизиты, , "ТипКомнаты");
	Иначе
		
		Если ДополнительныеСвойства.Свойство("ПредотвратитьПроверкуУникальности") Тогда
			ДополнительныеСвойства.Удалить("ПредотвратитьПроверкуУникальности")
		Иначе
			
			Если Не __УправлениеДаннымиСервер.ПроверитьУникальностьРеквизитаСправочника(ЭтотОбъект, "Номер") Тогда
				__ОбщегоНазначенияСервер.СообщитьОбНеуникальностиРеквизитаСправочника(ЭтотОбъект, "Номер", , Отказ);
			КонецЕсли;
			
		КонецЕсли; 
		
	КонецЕсли; 
	
КонецПроцедуры // ОбработкаПроверкиЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	
	Актуальность = Истина;
	Если Не ЭтоГруппа Тогда
		
		Номер = Справочники.Комнаты.ПолучитьОчереднойКод(ЭтотОбъект);
		Наименование = Справочники.Комнаты.ПолучитьНаименование(ЭтотОбъект);
		
	КонецЕсли; 					
	
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ)

	// При обмене данными ничего не проверяем.
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Нормализация.
	Если Не ЭтоГруппа Тогда
		
		Если ПустаяСтрока(Наименование) Тогда
			Наименование = Справочники.Комнаты.ПолучитьНаименование(ЭтотОбъект);
		КонецЕсли; 
		
	КонецЕсли;

КонецПроцедуры // ПередЗаписью()
