
&НаКлиенте
Процедура Сформировать(Команда)
	
	Перем Отказ;

	ОчиститьСообщения();
	
	// Выполнение.
	СформироватьСервер(Отказ);
	Если Не Отказ Тогда
		Закрыть(Истина);
	КонецЕсли; 
	
КонецПроцедуры // Сформировать()

&НаСервере
Процедура СформироватьСервер(Отказ)

	Отказ = Ложь;
	
	// Проверка.
	Проверка = РеквизитФормыВЗначение("Объект");
	Проверка.ДополнительныеСвойства.Вставить("ПредотвратитьПроверкуУникальности");
	Если Не Проверка.ПроверитьЗаполнение() Тогда
		Отказ = Истина; Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Попытка
		НачатьТранзакцию();
		
		// Поиск существующих.
		Запрос = Новый Запрос("ВЫБРАТЬ
		                      |	Комнаты.Ссылка,
		                      |	Комнаты.Номер
		                      |ИЗ
		                      |	Справочник.Комнаты КАК Комнаты
		                      |ГДЕ
		                      |	Комнаты.Номер МЕЖДУ &НомерНачальный И &НомерКонечный
		                      |	И НЕ Комнаты.ЭтоГруппа
		                      |
		                      |ДЛЯ ИЗМЕНЕНИЯ");
		Запрос.УстановитьПараметр("НомерНачальный", НомерНачальный);
		Запрос.УстановитьПараметр("НомерКонечный", НомерКонечный);
		ТаблицаСуществующих = Запрос.Выполнить().Выгрузить();
		
		// Создание новыйх элементов и изменение существующих.
		Для Номер = НомерНачальный По НомерКонечный Цикл
			
			Существующий = ТаблицаСуществующих.Найти(Номер, "Номер");
			Если (Существующий = Неопределено) Тогда
				ЭлементОбъект = Справочники.Комнаты.СоздатьЭлемент();
				ЭлементОбъект.УстановитьНовыйКод();	// Принудительно в связи с использованием ОбменДанными.Загрузка.
			Иначе
				ЭлементОбъект = Существующий.Ссылка.ПолучитьОбъект();	
			КонецЕсли; 
			
			// Заполнение.
			Если (Существующий = Неопределено) Тогда
				ЭлементОбъект.Номер = Номер;
			КонецЕсли; 
			Если (Существующий = Неопределено) Или СуществующиеПеремещать Тогда
				ЭлементОбъект.Родитель = Объект.Родитель;
			КонецЕсли; 
			Если (Существующий = Неопределено) Или СуществующиеИзменятьТип Тогда
				ЭлементОбъект.ТипКомнаты = Объект.ТипКомнаты;
			КонецЕсли; 
			Если (Существующий = Неопределено) Или СуществующиеПереименовывать Тогда
				ЭлементОбъект.Наименование = Шаблон(Объект.Наименование, 
					Новый Структура("Тип,Номер", ЭлементОбъект.ТипКомнаты, ЭлементОбъект.Номер));
			КонецЕсли; 
			ЭлементОбъект.Актуальность = (Не ЗначениеЗаполнено(ЭлементОбъект.Родитель)) Или ЭлементОбъект.Родитель.Актуальность;
			
			ЭлементОбъект.ОбменДанными.Загрузка = Истина;
			Если ЭлементОбъект.Модифицированность() Тогда
				ЭлементОбъект.Записать();
			КонецЕсли; 
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
	Исключение
		// TODO: Регистрация ошибки.
		__ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке(НСтр("ru = 'Не удалось выполнить формирование элементов'; uk = 'Не вдалося виконати формування елементів'"), ,,, Отказ);
		
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
	  	КонецЕсли;
	КонецПопытки;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры // СформироватьСервер()


&НаКлиенте
Процедура СформироватьПример()
	
	Пример = Шаблон(Объект.Наименование, 
		Новый Структура("Тип,Номер", Объект.ТипКомнаты, НомерНачальный));
	
КонецПроцедуры // СформироватьПример()

&НаКлиенте
Процедура НомерНачальныйПриИзменении(Элемент)
	
	НомерКонечный = Макс(НомерНачальный, НомерКонечный);
	
	// Пример.
	СформироватьПример();

КонецПроцедуры // НомерНачальныйПриИзменении()

&НаКлиенте
Процедура НомерКонечныйПриИзменении(Элемент)
	
	НомерНачальный = Мин(НомерНачальный, НомерКонечный);
	
	// Пример.
	СформироватьПример();

КонецПроцедуры // НомерКонечныйПриИзменении()

&НаКлиенте
Процедура РодительПриИзменении(Элемент)
	
	// Наследуемые реквизиты.
	РодительПриИзмененииСервер();
	
	// Пример.
	СформироватьПример();

КонецПроцедуры // РодительПриИзменении()

&НаСервере
Процедура РодительПриИзмененииСервер()
	
	Объект.ТипКомнаты = __ОбщегоНазначенияКлиентСервер.ПолучитьНеПустоеЗначение(
							Объект.Родитель.ТипКомнаты,
							Объект.ТипКомнаты);

КонецПроцедуры // РодительПриИзмененииСервер()

&НаКлиенте
Процедура ТипКомнатыПриИзменении(Элемент)
	
	// Пример.
	СформироватьПример();

КонецПроцедуры // ТипКомнатыПриИзменении()

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	// Пример.
	СформироватьПример();

КонецПроцедуры // НаименованиеПриИзменении()


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// Параметры.
	НомерНачальный = Справочники.Комнаты.ПолучитьОчереднойКод(Неопределено); НомерКонечный = НомерНачальный + 10;
	
	Объект.Номер = 1; // Должен быть не нулевым.
	Объект.Наименование = Элементы.Наименование.СписокВыбора[0].Значение;
	Объект.Родитель = Параметры.ТекущийРодитель;
	РодительПриИзмененииСервер();
	
	СуществующиеПеремещать = Истина;
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// Пример.
	СформироватьПример();

КонецПроцедуры // ПриОткрытии()
