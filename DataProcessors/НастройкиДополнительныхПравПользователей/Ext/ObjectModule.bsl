
# Если Клиент Тогда

// Процедура считывает настройки прав данного объекта.
//
Процедура ДеревоПравПрочитать(Ссылка, Элемент, Принудительно = Ложь) Экспорт
	
	// Проверка заполнялись ли уже данные.
	Если Не(ЗначениеЗаполнено(Принудительно) И Принудительно) И (Не ДеревоПрав.Строки.Количество() = 0) И (ВладелецПравПользователей = Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	ВладелецПравПользователей = Ссылка;
	
	// Очистка дерева.
	ДеревоПрав.Строки.Очистить();
	
	// Добавление колонок.
	Если (ДеревоПрав.Колонки.Найти("Наименование") = Неопределено) Тогда
		ДеревоПрав.Колонки.Добавить("Наименование", Новый ОписаниеТипов("Строка"), "Наименование");
	КонецЕсли;
	Если (ДеревоПрав.Колонки.Найти("Значение") = Неопределено) Тогда
		ДеревоПрав.Колонки.Добавить("Значение", Метаданные.ПланыВидовХарактеристик.ПраваПользователей.Тип, "Значение");
	КонецЕсли;
	
	// Перенос и настройка колонок.
	Элемент.СоздатьКолонки();
	Элемент.Колонки.Наименование.ТолькоПросмотр = Истина;
	Элемент.Колонки.Наименование.Ширина = 25;	// Как упровлять ширеной, так и не понял... подобрано империческим путем.
	Элемент.Колонки.Значение.Ширина = 10;
	
	
	// Добавление служебных колонок.
	Если (ДеревоПрав.Колонки.Найти("Право") = Неопределено) Тогда
		ДеревоПрав.Колонки.Добавить("Право");
	КонецЕсли;
	Если (ДеревоПрав.Колонки.Найти("ТипЗначения") = Неопределено) Тогда
		ДеревоПрав.Колонки.Добавить("ТипЗначения");
	КонецЕсли;
	Если (ДеревоПрав.Колонки.Найти("ПоУмолчанию") = Неопределено) Тогда
		ДеревоПрав.Колонки.Добавить("ПоУмолчанию");
	КонецЕсли;
	Если (ДеревоПрав.Колонки.Найти("ЭтоГруппа") = Неопределено) Тогда
		ДеревоПрав.Колонки.Добавить("ЭтоГруппа");
	КонецЕсли;
	
	
	// Выборка данных из регистра.
	Если (Не ВладелецПравПользователей = Неопределено) И ВладелецПравПользователей.Пустая() Тогда
		ТаблицаРезультатаЗапроса = Новый ТаблицаЗначений();
		ТаблицаРезультатаЗапроса.Колонки.Добавить("Право");
	Иначе
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	Право,
			|	Значение
			|ИЗ
			|	РегистрСведений.ЗначенияДополнительныхПравПользователя КАК ЗначенияДополнительныхПравПользователя
			|ГДЕ
			|	ЗначенияДополнительныхПравПользователя.Пользователь = &Пользователь
			|");
		Запрос.УстановитьПараметр("Пользователь", ВладелецПравПользователей);
		ТаблицаРезультатаЗапроса = Запрос.Выполнить().Выгрузить();
	КонецЕсли;
	
	
	// Заполнение данных.
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Ссылка,
		|	Родитель,
		|	Наименование,
		|	ТипЗначения,
		|	ПоУмолчанию
		|ИЗ
		|	ПланВидовХарактеристик.ПраваПользователей КАК ПраваПользователей
		|ГДЕ
		|	НЕ ПраваПользователей.ЭтоГруппа
		|";
		
	// TODO: Разграничение по объектам применения.
		
	//Если (Не ВладелецПравПользователей = Неопределено) Тогда
	//	ТекстЗапроса = ТекстЗапроса + "И ПраваПользователей.Применение ПОДОБНО ""%" + ВладелецПравПользователей.Метаданные().Имя + "%""";
	//КонецЕсли;
	Запрос = Новый Запрос(ТекстЗапроса);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
		
	// -- Обход запроса.
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		// -- Поиск в регистре.
		СтрокаТаблицаРезультатаЗапроса = ТаблицаРезультатаЗапроса.Найти(Выборка.Ссылка, "Право");
		
		// -- Добавление в дерево родителей.
		СтрокаГруппы = ДеревоПрав;
		
		СписокРодителей = Новый СписокЗначений;
		Родитель = Выборка.Родитель;
		Пока Не Родитель.Пустая() Цикл
			СписокРодителей.Вставить(0, Родитель);
			Родитель = Родитель.Родитель;
		КонецЦикла;
		
		Для Каждого Родитель Из СписокРодителей Цикл
			Родитель = Родитель.Значение;
			
			СтрокаЭлемент = ДеревоПрав.Строки.Найти(Родитель, "Право", Истина);
			Если (СтрокаЭлемент = Неопределено) Тогда
				СтрокаЭлемент = СтрокаГруппы.Строки.Добавить();
				СтрокаЭлемент.Наименование = Родитель.Наименование;
				СтрокаЭлемент.Право = Родитель;
				СтрокаЭлемент.ЭтоГруппа = Истина;
			КонецЕсли;
			
			СтрокаГруппы = СтрокаЭлемент;
		КонецЦикла;
		
		// ---- Добавление в дерево элемента.
		СтрокаЭлемент = СтрокаГруппы.Строки.Добавить();
		СтрокаЭлемент.Наименование = Выборка.Наименование;
		СтрокаЭлемент.Право = Выборка.Ссылка;
		СтрокаЭлемент.ТипЗначения = Выборка.ТипЗначения;
		СтрокаЭлемент.ПоУмолчанию = Выборка.ПоУмолчанию;
		СтрокаЭлемент.ЭтоГруппа = Ложь;
		Если (Не СтрокаТаблицаРезультатаЗапроса = Неопределено) Тогда
			СтрокаЭлемент.Значение = Выборка.ТипЗначения.ПривестиЗначение(СтрокаТаблицаРезультатаЗапроса.Значение);
		Иначе
			СтрокаЭлемент.Значение = Выборка.ТипЗначения.ПривестиЗначение(Выборка.ПоУмолчанию);
		КонецЕсли;
		
	КонецЦикла;
	
	// Сортировка.
	ДеревоПрав.Строки.Сортировать("ЭтоГруппа Убыв, Наименование", Истина);

КонецПроцедуры // ДеревоПравПрочитать()

// Процедура при необходимости изменяет настройки прав данного объекта.
//
Процедура ДеревоПравЗаписать(Ссылка, Отказ) Экспорт
	
	// Запись уже была прервана, продолжение не имеет смысла.
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ВладелецПравПользователей = Ссылка;
	
	// Запись набора записей.
	НаборЗаписей = РегистрыСведений.ЗначенияДополнительныхПравПользователя.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Пользователь.Использование = Истина;
	НаборЗаписей.Отбор.Пользователь.Значение = ВладелецПравПользователей;
	
	// Нисходящий обход дерева.
	СписокПодчиненных = Новый СписокЗначений();
	СписокПодчиненных.Добавить(ДеревоПрав);

	Для Каждого СтрокаВладелец Из СписокПодчиненных Цикл
		СтрокаВладелец = СтрокаВладелец.Значение;	// Наепка - друг чекиста ;)
		
		Для Каждого СтрокаВладельца Из СтрокаВладелец.Строки Цикл
			// -- Добавление данных в набор записей.
			Если (Не СтрокаВладельца.ЭтоГруппа) И (Не СтрокаВладельца.ТипЗначения.ПривестиЗначение(СтрокаВладельца.Значение) = СтрокаВладельца.ТипЗначения.ПривестиЗначение(СтрокаВладельца.ПоУмолчанию)) Тогда
				Запись = НаборЗаписей.Добавить();
				Запись.Пользователь = ВладелецПравПользователей;
				Запись.Право = СтрокаВладельца.Право;
				Запись.Значение = СтрокаВладельца.ТипЗначения.ПривестиЗначение(СтрокаВладельца.Значение);
			КонецЕсли;
					
			Если (Не СтрокаВладельца.Строки.Количество() = 0) Тогда
				СписокПодчиненных.Добавить(СтрокаВладельца);
			КонецЕсли;
		КонецЦикла;
	
	КонецЦикла;

	// Запись набора записей.
	Попытка
		НаборЗаписей.Записать();
	Исключение
		ОбщегоНазначения.СообщитьОбОшибкеЗапеисиРегистра(ОписаниеОшибки(), Отказ, ,, НаборЗаписей, ВладелецПравПользователей);
	КонецПопытки;
	
КонецПроцедуры // ДеревоПравЗаписать()

// Обработчик события ДеревоПрав.ПриВыводеСтроки элемента.
//
Процедура ДеревоПравВывестиСтроку(Элемент, ОформлениеСтроки, ДанныеСтроки) Экспорт
	
	// Наименование.
	ОформлениеСтроки.Ячейки.Наименование.ОтображатьКартинку = Истина;
	Если ДанныеСтроки.ЭтоГруппа Тогда
		ОформлениеСтроки.Ячейки.Наименование.Картинка = БиблиотекаКартинок.ГруппаСправочника;
	Иначе
		ОформлениеСтроки.Ячейки.Наименование.Картинка = БиблиотекаКартинок.ЭлементСправочника;
	КонецЕсли;
	
	// Значение.
	Если ДанныеСтроки.ЭтоГруппа Тогда
		ОформлениеСтроки.Ячейки.Значение.ТолькоПросмотр = Истина;
		ОформлениеСтроки.Ячейки.Значение.ОтображатьТекст = Ложь;
	Иначе
		Если ДанныеСтроки.ТипЗначения.СодержитТип(Тип("Булево")) И (ДанныеСтроки.ТипЗначения.Типы().Количество() = 1) Тогда
			ОформлениеСтроки.Ячейки.Значение.ОтображатьТекст = Ложь;
			ОформлениеСтроки.Ячейки.Значение.ОтображатьФлажок = Истина;
			ОформлениеСтроки.Ячейки.Значение.ТолькоПросмотр = Истина;
			ОформлениеСтроки.Ячейки.Значение.Флажок = ДанныеСтроки.ТипЗначения.ПривестиЗначение(ДанныеСтроки.Значение);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ДеревоПравВывестиСтроку()

// Обработчик события ДеревоПрав.ПриИзмененииФлажка элемента.
//
Процедура ДеревоПравИзменитьФлажок(Элемент, Колонка) Экспорт
	
	СтрокаДерева = Элемент.ТекущаяСтрока;
	СтрокаДерева.Значение = Не СтрокаДерева.ТипЗначения.ПривестиЗначение(СтрокаДерева.Значение);
	
КонецПроцедуры // ДеревоПравИзменитьФлажок()

#КонецЕсли
