
//Костенюк Александр-Старт 08.06.2012
// Процедура выполняет выгрузку данных в файл.
//
// Параметры:
//  Нет
// 
Процедура ВыполнитьВыгрузку() Экспорт
	
	Если ВыгружатьЧеки Тогда
		ВыгрузкаЧеков();
	КонецЕсли;
	
	Если ВыгружатьСС Тогда
		ВыгрузкаСомнительныхСитуаций();
	КонецЕсли;
	
КонецПроцедуры
//Костенюк Александр-Финиш 08.06.2012

//Костенюк Александр-Старт 11.01.2013
// Процедура выполняет выгрузку чеков в файл.
//
// Параметры:
//  Нет
// 
Процедура ВыгрузкаЧеков() Экспорт
	
	ВремяНачала = ТекущаяДата();
	ОбработаноОбъектов = 0;
	
	Параметры = Новый Структура;
	Параметры.Вставить("СекцияЗначений"				, "Values");
	Параметры.Вставить("ИдентификаторКонфигурации"	, Метаданные.Версия);
	Параметры.Вставить("ИдентификаторЧекИнтерсвязь"	, 7);
	Параметры.Вставить("ИдентификаторЧек"			, 1);
	Параметры.Вставить("Пароль"						, "");
	Параметры.Вставить("КодРесторана"				, "" + Формат(Константы.КодРесторана.Получить(),"ЧЦ=4; ЧН=0000; ЧВН=; ЧГ="));
	Параметры.Вставить("СекторнаяВерсия"			, Константы.СекторнаяВерсияБухгалтерии.Получить());
	Параметры.Вставить("Скидка"						, 0);
	
	Запрос = Новый Запрос;
	Запрос.Текст =  "ВЫБРАТЬ РАЗЛИЧНЫЕ
	                |	Продажи.Регистратор
	                |ИЗ
	                |	РегистрНакопления.Продажи КАК Продажи
	                |ГДЕ
	                |	Продажи.Период МЕЖДУ &ДатаНач И &ДатаКон";
	Запрос.УстановитьПараметр("ДатаНач", НачалоДня(ДатаНач));
	Запрос.УстановитьПараметр("ДатаКон", КонецДня(ДатаКон));
	ТаблицаЗаказов = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаТаблицы Из ТаблицаЗаказов Цикл
		
		//подсчитаем общее количество обработанных объектов
		ОбработаноОбъектов = ОбработаноОбъектов + 1;

		Документ = СтрокаТаблицы.Регистратор;
		Сообщить("Обработка документа: " + Строка(Документ));
		ОбменДанными.ПередатьЗаказПоHTTP(Документ, Параметры);
		
	КонецЦикла;
	
	ВремяОкончания 	= ТекущаяДата();
	ВремяОбработки	= ВремяОкончания - ВремяНачала; //в секундах
	Длительность 	= ОбщегоНазначения.ЧЧММСС(ВремяОбработки);
	Сообщить("_________________________________________________________");
	Сообщить(                                                         
	"НачалоВыгрузкиЧеков	: " + ВремяНачала 			+ Символы.ПС +
	"ОбработаноЧеков		: " + ОбработаноОбъектов 	+ Символы.ПС +
	"ОкончаниеВыгрузкиЧеков	: " + ВремяОкончания 		+ Символы.ПС +
	"Длительность			: " + Длительность                
	, СтатусСообщения.Информация);	

КонецПроцедуры
//Костенюк Александр-Финиш 11.01.2013

//Костенюк Александр-Старт 11.01.2013
// Процедура выполняет выгрузку сомнительных ситуаций в файл.
//
// Параметры:
//  Нет
// 
Процедура ВыгрузкаСомнительныхСитуаций() Экспорт
	
	ВремяНачала 		= ТекущаяДата();
	ОбработаноОбъектов 	= 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	СомнительныеСитуации.Идентификатор,
	               |	СомнительныеСитуации.Дата,
	               |	СомнительныеСитуации.СомнительнаяСитуация,
	               |	СомнительныеСитуации.Подтвержена,
	               |	СомнительныеСитуации.Пользователь,
	               |	СомнительныеСитуации.Компьютер,
	               |	СомнительныеСитуации.Заказ,
	               |	СомнительныеСитуации.Значение
	               |ИЗ
	               |	РегистрСведений.СомнительныеСитуации КАК СомнительныеСитуации
	               |ГДЕ
	               |	СомнительныеСитуации.Дата МЕЖДУ &ДатаНач И &ДатаКон";
	Запрос.УстановитьПараметр("ДатаНач", НачалоДня(ДатаНач));
	Запрос.УстановитьПараметр("ДатаКон", КонецДня(ДатаКон));
	ТаблицаСС = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаТаблицы Из ТаблицаСС Цикл
		
		//подсчитаем общее количество обработанных объектов
		ОбработаноОбъектов = ОбработаноОбъектов + 1;
		ОбменДанными.ПередатьСомнительнуюСитуацию(СтрокаТаблицы);
		
	КонецЦикла;
	
	ВремяОкончания 	= ТекущаяДата();
	ВремяОбработки	= ВремяОкончания - ВремяНачала; //в секундах
	Длительность 	= ОбщегоНазначения.ЧЧММСС(ВремяОбработки);
	Сообщить("_________________________________________________________");
	Сообщить(                                                         
	"НачалоВыгрузкиСС		: " + ВремяНачала 			+ Символы.ПС +
	"ОбработаноСС			: " + ОбработаноОбъектов 	+ Символы.ПС +
	"ОкончаниеВыгрузкиСС	: " + ВремяОкончания 		+ Символы.ПС +
	"Длительность			: " + Длительность                
	, СтатусСообщения.Информация);	
	
КонецПроцедуры
//Костенюк Александр-Финиш 11.01.2013
