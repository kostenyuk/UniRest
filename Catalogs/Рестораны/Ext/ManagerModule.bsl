
Функция ОсновнойРесторан() Экспорт
	
	// СОВМЕСТИОСЬ: Выбирвать ресторан из настроек.
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	                      |	Рестораны.Ссылка КАК Ссылка
	                      |ИЗ
	                      |	Справочник.Рестораны КАК Рестораны
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	Рестораны.ПометкаУдаления,
	                      |	Рестораны.Актуальность УБЫВ
	                      |АВТОУПОРЯДОЧИВАНИЕ");
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Справочники.Рестораны.ПустаяСсылка();

КонецФункции // ОсновнойРесторан()

Функция КоличествоРесторановБолееОдного() Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Рестораны.Ссылка) КАК Количество
	                      |ИЗ
	                      |	Справочник.Рестораны КАК Рестораны");
						  
	Возврат (Запрос.Выполнить().Выгрузить()[0].Количество > 1);

КонецФункции // КоличествоРесторановБолееОдного()

Функция КоличествоАктуальныхРесторановБолееОдного() Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Рестораны.Ссылка) КАК Количество
	                      |ИЗ
	                      |	Справочник.Рестораны КАК Рестораны
	                      |ГДЕ
	                      |	Рестораны.Актуальность
	                      |	И НЕ Рестораны.ПометкаУдаления");
						  
	Возврат (Запрос.Выполнить().Выгрузить()[0].Количество > 1);

КонецФункции // КоличествоАктуальныхРесторановБолееОдного()


Функция ПолучитьАктуальныеРестораны() Экспорт

	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	Рестораны.Ссылка КАК Ресторан
	                      |ИЗ
	                      |	Справочник.Рестораны КАК Рестораны
	                      |ГДЕ
	                      |	Рестораны.Актуальность
	                      |	И НЕ Рестораны.ПометкаУдаления");
						  
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ресторан");						  

КонецФункции // ПолучитьАктуальныеРестораны()

//Костенюк Александр-Старт 21.05.2014
// Процедура формирует запрос по актуальным ресторанам.
//
// Параметры:
// Пользователь - СправочникСсылка.Пользователи.
//
// ВозвращаемоеЗначение:
// РезлуьтатЗапроса.
// 
Функция СформироватьЗапросПоАктуальнымРесторанам(Пользователь) Экспорт
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ЕСТЬNULL(РестораныПользователя.Ресторан, Рестораны.Ресторан) КАК Ресторан,
	               |	ЕСТЬNULL(РестораныПользователя.Чтение, ИСТИНА) КАК Чтение
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		Рестораны.Ссылка КАК Ресторан
	               |	ИЗ
	               |		Справочник.Рестораны КАК Рестораны
	               |	ГДЕ
	               |		Рестораны.Актуальность
	               |		И НЕ Рестораны.ПометкаУдаления) КАК Рестораны
	               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |			ПользователиРестораны.Ресторан КАК Ресторан,
	               |			ПользователиРестораны.Чтение КАК Чтение
	               |		ИЗ
	               |			Справочник.Пользователи.Рестораны КАК ПользователиРестораны
	               |		ГДЕ
	               |			ПользователиРестораны.Ссылка = &Пользователь) КАК РестораныПользователя
	               |		ПО Рестораны.Ресторан = РестораныПользователя.Ресторан
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Ресторан";
	
	Если ОбменДаннымиВызовСервера.ОбменДаннымиВключен("ОбменДанными2K_CRS") Тогда
		ЭтотУзел = ПланыОбмена.ОбменДанными2K_CRS.ЭтотУзел();
		ЭтоЦБ = ПроцедурыОбменаДаннымиFrontOffice_Restoran.ЭтоЦентральнаяБаза(ЭтотУзел);
		Если ЭтоЦБ Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ЕСТЬNULL(РестораныПользователя.Чтение, ИСТИНА) КАК Чтение", "ЕСТЬNULL(РестораныПользователя.Чтение, ЛОЖЬ) КАК Чтение");
		КонецЕсли;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	
	Возврат Запрос.Выполнить();
	
КонецФункции
//Костенюк Александр-Финиш 21.05.2014

//Костенюк Александр-Старт 21.05.2014
// Процедура формирует запрос по актуальным ресторанам.
//
// Параметры:
// Пользователь - СправочникСсылка.Пользователи.
//
// ВозвращаемоеЗначение:
// Массив - Массив ресторанов доступных пользователю.
// 
Функция ПолучитьМассивРесторановДоступныхПользователю(Пользователь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	РестораныПользователя.Ресторан
	               |ИЗ
	               |	Справочник.Пользователи.Рестораны КАК РестораныПользователя
	               |ГДЕ
	               |	РестораныПользователя.Чтение
	               |	И РестораныПользователя.Ссылка = &Пользователь";
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ресторан");
	
КонецФункции
//Костенюк Александр-Финиш 21.05.2014
