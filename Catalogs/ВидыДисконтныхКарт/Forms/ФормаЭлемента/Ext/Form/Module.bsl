
&НаСервере
Процедура УстановитьВидимостьНаСервере()
	
	//Костенюк Александр-Старт 17.06.2014
	Элементы.ГруппаСтраницаПараметры.Видимость = (Объект.ТипКарты = Перечисления.ТипыИнформационныхКарт.Дисконтная);
	Элементы.ГруппаСтраницаУсловияСкидок.Видимость = (Объект.ТипКарты = Перечисления.ТипыИнформационныхКарт.Дисконтная);
	//Костенюк Александр-Финиш 17.06.2014
	
	// Настройка формы.
	Если Объект.СкидкаПоУсловию Тогда
	
		Элементы.ГруппаСтраницы.ОтображениеСтраниц = ОтображениеСтраницФормы.ЗакладкиСверху;
		
		Элементы.ГруппаСтраницаУсловияСкидок.Видимость = Истина;
		Элементы.ДекорацияОтступ.Видимость = Ложь;

	Иначе
	
		Элементы.ГруппаСтраницы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
		
		Элементы.ГруппаСтраницаУсловияСкидок.Видимость = Ложь;
		Элементы.ДекорацияОтступ.Видимость = Истина;
		
	КонецЕсли;
	
	// Элементы.
	Элементы.Накопительная.Доступность = (Не Объект.ТипМонетарности = Перечисления.ТипыМонетарности.НеИспользуется);
	Элементы.Оплата.Доступность = Элементы.Накопительная.Доступность;
	
КонецПроцедуры // РасширенныеНастройкиНаСервере()


&НаКлиенте
Процедура OnLineПриИзменении(Элемент)

	Объект.Локальная = Не Объект.OnLine;
	
КонецПроцедуры // OnLineПриИзменении()

&НаКлиенте
Процедура СкидкаПоУсловиюПриИзменении(Элемент)
	
	// Настройка формы.
	УстановитьВидимостьНаСервере();
	
	Если Элементы.ГруппаСтраницаУсловияСкидок.Видимость Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаСтраницаУсловияСкидок;
	КонецЕсли; 

КонецПроцедуры // СкидкаПоУсловиюПриИзменении()

&НаКлиенте
Процедура ТипМонетарностиПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(Объект.ТипМонетарности) Тогда
		Объект.ТипМонетарности = ПредопределенноеЗначение("Перечисление.ТипыМонетарности.НеИспользуется");
	КонецЕсли; 
	
	// Настройка формы.
	УстановитьВидимостьНаСервере();

КонецПроцедуры // ТипМонетарностиПриИзменении()


&НаКлиенте
Процедура УсловияСкидокПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элементы.УсловияСкидок.ТекущиеДанные;
	
	Если НоваяСтрока И Копирование Тогда
		ТекущиеДанные.Ссылка = Неопределено;
	КонецЕсли; 
	
КонецПроцедуры // УсловияСкидокПриНачалеРедактирования()

&НаКлиенте
Процедура УсловияСкидокПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)

	ТекущиеДанные = Элементы.УсловияСкидок.ТекущиеДанные;

	Если (Не ТекущиеДанные = Неопределено) Тогда
		Если ПустаяСтрока(ТекущиеДанные.Наименование) Тогда
			
			Если ЗначениеЗаполнено(ТекущиеДанные.Ссылка) Тогда
				ТекущиеДанные.Наименование = __ОбщегоНазначенияСервер.ПолучитьЗначение(ТекущиеДанные.Ссылка, "Наименование", Истина);
			Иначе
				УсловияСкидок.Удалить(ТекущиеДанные);
			КонецЕсли; 
			
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры // УсловияСкидокПриОкончанииРедактирования()

&НаКлиенте
Процедура УсловияСкидокПередУдалением(Элемент, Отказ)

	ТекущиеДанные = Элементы.УсловияСкидок.ТекущиеДанные;

	Если ЗначениеЗаполнено(ТекущиеДанные.Ссылка) Тогда
		
		Отказ = Истина;
		
		ТекущиеДанные.ПометкаУдаления = Не ТекущиеДанные.ПометкаУдаления;
		ТекущиеДанные.ИндексКартинки = ТекущиеДанные.ПометкаУдаления;
	
	КонецЕсли; 
	
КонецПроцедуры // УсловияСкидокПередУдалением()


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Данные.
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриЧтенииНаСервере(Объект);
	КонецЕсли; 
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// Условия скидок.
	Справочники.УсловияСкидок.ПолучитьУсловияСкидокВидаДисконтнойКарты(ТекущийОбъект, УсловияСкидок);
	
	// Настройка формы.
	УстановитьВидимостьНаСервере();
	
КонецПроцедуры // ПриЧтенииНаСервере()

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	// Условия скидок.
	Справочники.УсловияСкидок.УстановитьУсловияСкидокВидаДисконтнойКарты(ТекущийОбъект, УсловияСкидок, Отказ);
	
КонецПроцедуры // ПриЗаписиНаСервере()

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Не Объект.СкидкаПоУсловию Тогда
		__ОбщегоНазначенияСервер.ИзменитьПроверяемыеРеквизиты(ПроверяемыеРеквизиты, , "УсловияСкидок");
	КонецЕсли; 
	
КонецПроцедуры // ОбработкаПроверкиЗаполненияНаСервере()

&НаКлиенте
//Костенюк Александр-Старт 17.06.2014
Процедура ТипКартыПриИзменении(Элемент)
	
	// Настройка формы.
	УстановитьВидимостьНаСервере();
	
КонецПроцедуры
//Костенюк Александр-Финиш 17.06.2014
