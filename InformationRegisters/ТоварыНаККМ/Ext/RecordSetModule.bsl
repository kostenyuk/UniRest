
// Обработчик события ПередЗаписью объекта.
//
Процедура ПередЗаписью(Отказ, Замещение)
	
	Перем Описание;
	
	// При обмене данными ничего не проверяем.
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Проверка заполнения реквизитов.
	Для Каждого Запись Из ЭтотОбъект Цикл
		Если Не ЗначениеЗаполнено(Запись.Номенклатура) Тогда
			ОбщегоНазначения.СообщитьОбНезаполненомРесурсеРегистра(ЭтотОбъект, "Номенклатура", Отказ);
		КонецЕсли;
		Если Запись.Номенклатура.Набор Тогда
			ОбщегоНазначения.СообщитьОбОшибкеЗапеисиРегистра("В регистре сведений «" + Метаданные.РегистрыСведений.ТоварыНаККМ.Синоним + "» не допускается использование наборов.", Отказ, ,, ЭтотОбъект);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Запись.ЕдиницаИзмерения) Тогда
			ОбщегоНазначения.СообщитьОбНезаполненомРесурсеРегистра(ЭтотОбъект, "ЕдиницаИзмерения", Отказ);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Запись.НаименованиеСокращенное) Тогда
			ОбщегоНазначения.СообщитьОбНезаполненомРеквизитеРегистра(ЭтотОбъект, "НаименованиеСокращенное", Отказ);
		КонецЕсли;
		
		Если Отказ Тогда
			 Возврат;
		КонецЕсли;
	КонецЦикла;

	// Проверка корректности данных.
	Для Каждого Запись Из ЭтотОбъект Цикл
		Запрос = Новый Запрос("ВЫБРАТЬ
		                      |	ВЫБОР
		                      |		КОГДА РегТовары.Код = &Код
		                      |			ТОГДА ИСТИНА
		                      |		ИНАЧЕ ЛОЖЬ
		                      |	КОНЕЦ КАК НеУникальныйКод,
		                      |	ПРЕДСТАВЛЕНИЕ(&КассаККМ) КАК КассаККМ,
		                      |	ПРЕДСТАВЛЕНИЕ(РегТовары.Код) КАК Код,
		                      |	ПРЕДСТАВЛЕНИЕ(РегТовары.Номенклатура) КАК Номенклатура,
		                      |	ПРЕДСТАВЛЕНИЕ(РегТовары.ЕдиницаИзмерения) КАК ЕдиницаИзмерения,
		                      |	РегТовары.НаименованиеСокращенное КАК НаименованиеСокращенное
		                      |ИЗ
		                      |	РегистрСведений.ТоварыНаККМ КАК РегТовары
		                      |ГДЕ
		                      |	РегТовары.КассаККМ = &КассаККМ
		                      |	И (РегТовары.Код = &Код
		                      |			ИЛИ РегТовары.Номенклатура = &Номенклатура
		                      |				И РегТовары.ЕдиницаИзмерения = &ЕдиницаИзмерения
		                      |				И РегТовары.НаименованиеСокращенное = &НаименованиеСокращенное)");
		Запрос.УстановитьПараметр("Код", Запись.Код);
		Запрос.УстановитьПараметр("КассаККМ", Запись.КассаККМ);
		Запрос.УстановитьПараметр("Номенклатура", Запись.Номенклатура);
		Запрос.УстановитьПараметр("ЕдиницаИзмерения", Запись.ЕдиницаИзмерения);
		Запрос.УстановитьПараметр("НаименованиеСокращенное", Запись.НаименованиеСокращенное);
		Выборка = Запрос.Выполнить();
		Если Выборка.Пустой() Тогда
			Продолжить;
		КонецЕсли;
		
		Выборка = Выборка.Выбрать();
		Пока Выборка.Следующий() Цикл
			Если Выборка.НеУникальныйКод Тогда
				Ошибка = "Товары с таким значением кода для данной кассы уже существуют:"
			Иначе
				Ошибка = "Товары с таким набором данных для данной кассы уже существуют:"
			КонецЕсли;
			Ошибка = Ошибка + Символы.ПС +
					 "       Касса: " + Выборка.КассаККМ + Символы.ПС +
					 "       Код: " + Выборка.Код + Символы.ПС +
					 "       Номенклатура: " + Выборка.Номенклатура + Символы.ПС +
					 "       Сокращенное наименование: " + Выборка.НаименованиеСокращенное + Символы.ПС +
					 "       Единица измерения: " + Выборка.ЕдиницаИзмерения;
			Если ПустаяСтрока(Описание) Тогда
				Описание = " - " + Ошибка;				 
			Иначе
				Описание = Описание + Символы.ПС + " - " + Ошибка;				 
			КонецЕсли;
		КонецЦикла;
		ОбщегоНазначения.СообщитьОбОшибкеЗапеисиРегистра(Описание, Отказ, ,, ЭтотОбъект);
		
		Если Отказ Тогда
			 Возврат;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ПередЗаписью()
