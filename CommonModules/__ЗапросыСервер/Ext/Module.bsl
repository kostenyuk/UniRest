
Процедура НаложитьОтборСтруктурой(Запрос, Отбор, Расположение, Псевдоним = "") Экспорт
	
	// Применение отбора.
	ТекстЗапроса = "&Отбор_0000"; Индекс = 1; ТипыВложений = Новый ОписаниеТипов("Структура,ФиксированнаяСтруктура,Соответствие,ФиксированноеСоответствие");
	МассивСтруктурОтбора = Новый Массив; МассивСтруктурОтбора.Добавить(Новый Структура("Расположение,Оператор,Отбор", ТекстЗапроса, "И", Отбор));
	
	Для Каждого СтруктураОтбора Из МассивСтруктурОтбора Цикл
		Оператор = СтруктураОтбора.Оператор; НеПервый = Ложь; Текст = "";
		
		Для Каждого ЭлементСтруктуры Из СтруктураОтбора.Отбор Цикл
			
			ЗначениеОтбора = ЭлементСтруктуры.Значение;
			Вложение = ТипыВложений.СодержитТип(ТипЗнч(ЗначениеОтбора));
			
			// Имена.
			ПозицияПробела = Найти(ЭлементСтруктуры.Ключ, " "); ПозицияПодчеркивания = Найти(ЭлементСтруктуры.Ключ, "_");
			Если ПозицияПробела И ПозицияПодчеркивания Тогда
				Позиция = Мин(ПозицияПробела, ПозицияПодчеркивания);
			Иначе
				Позиция = Макс(ПозицияПробела, ПозицияПодчеркивания);
			КонецЕсли; 
			Если Позиция Тогда
				ИмяЭлементаОтбора = Сред(ЭлементСтруктуры.Ключ, Позиция + 1);
				ИмяВидаСравнения = ВРег(Лев(ЭлементСтруктуры.Ключ, Позиция - 1));
			Иначе
				Если Вложение Тогда
					ИмяЭлементаОтбора = ВРег(ЭлементСтруктуры.Ключ);
					ИмяВидаСравнения = ИмяЭлементаОтбора;
				Иначе
					ИмяЭлементаОтбора = ЭлементСтруктуры.Ключ;
					ИмяВидаСравнения = "РАВНО";
				КонецЕсли; 
			КонецЕсли;
			ПостфиксПараметраОтбора = Формат(Индекс, "ЧЦ=4; ЧН=; ЧВН=; ЧГ=");
				
			// Параметры отбора.
			Если Вложение Тогда
				
				Если (Не ИмяВидаСравнения = "И") И (Не ИмяВидаСравнения = "ИЛИ") И (Не ИмяВидаСравнения = "НЕ") Тогда
					ИмяВидаСравнения = "И";
				КонецЕсли;
					
			Иначе
				
				Если (ИмяВидаСравнения = "БОЛЬШЕ") Тогда
					ВидСравненияОтбора = ВидСравнения.Больше;
				ИначеЕсли (ИмяВидаСравнения = "БОЛЬШЕИЛИРАВНО") Или (ИмяВидаСравнения = "БОЛЬШЕРАВНО") Тогда
					ВидСравненияОтбора = ВидСравнения.БольшеИлиРавно;
				ИначеЕсли (ИмяВидаСравнения = "ВИЕРАРХИИ") Тогда
					ВидСравненияОтбора = ВидСравнения.ВИерархии;
				ИначеЕсли (ИмяВидаСравнения = "ВСПИСКЕ") Тогда
					ВидСравненияОтбора = ВидСравнения.ВСписке;
				ИначеЕсли (ИмяВидаСравнения = "ВСПИСКЕПОИЕРАРХИИ") Тогда
					ВидСравненияОтбора = ВидСравнения.ВСпискеПоИерархии;
				ИначеЕсли (ИмяВидаСравнения = "ИНТЕРВАЛ") Тогда
					ВидСравненияОтбора = ВидСравнения.Интервал;
				ИначеЕсли (ИмяВидаСравнения = "ИНТЕРВАЛВКЛЮЧАЯГРАНИЦЫ") Или (ИмяВидаСравнения = "ИНТЕРВАЛГРАНИЦЫ") Или (ИмяВидаСравнения = "ГРАНИЦЫ") Тогда
					ВидСравненияОтбора = ВидСравнения.ИнтервалВключаяГраницы;
				ИначеЕсли (ИмяВидаСравнения = "ИНТЕРВАЛВКЛЮЧАЯНАЧАЛО") Или (ИмяВидаСравнения = "ИНТЕРВАЛНАЧАЛО") Или (ИмяВидаСравнения = "НАЧАЛО") Тогда
					ВидСравненияОтбора = ВидСравнения.ИнтервалВключаяНачало;
				ИначеЕсли (ИмяВидаСравнения = "ИНТЕРВАЛВКЛЮЧАЯОКОНЧАНИЕ") Или (ИмяВидаСравнения = "ИНТЕРВАЛОКОНЧАНИЕ") Или (ИмяВидаСравнения = "ОКОНЧАНИЕ") Тогда
					ВидСравненияОтбора = ВидСравнения.ИнтервалВключаяОкончание;
				ИначеЕсли (ИмяВидаСравнения = "МЕНЬШЕ") Тогда
					ВидСравненияОтбора = ВидСравнения.Меньше;
				ИначеЕсли (ИмяВидаСравнения = "МЕНЬШЕИЛИРАВНО") Или (ИмяВидаСравнения = "МЕНЬШЕРАВНО") Тогда
					ВидСравненияОтбора = ВидСравнения.МеньшеИлиРавно;
				ИначеЕсли (ИмяВидаСравнения = "НЕВИЕРАРХИИ") Тогда
					ВидСравненияОтбора = ВидСравнения.НеВИерархии;
				ИначеЕсли (ИмяВидаСравнения = "НЕВСПИСКЕ") Тогда
					ВидСравненияОтбора = ВидСравнения.НеВСписке;
				ИначеЕсли (ИмяВидаСравнения = "НЕВСПИСКЕПОИЕРАРХИИ") Тогда
					ВидСравненияОтбора = ВидСравнения.НеВСпискеПоИерархии;
				ИначеЕсли (ИмяВидаСравнения = "НЕРАВНО") Или (ИмяВидаСравнения = "НЕ") Тогда
					ВидСравненияОтбора = ВидСравнения.НеРавно;
				ИначеЕсли (ИмяВидаСравнения = "НЕСОДЕРЖИТ") Тогда
					ВидСравненияОтбора = ВидСравнения.НеСодержит;
				ИначеЕсли (ИмяВидаСравнения = "РАВНО") Тогда
					ВидСравненияОтбора = ВидСравнения.Равно;
				ИначеЕсли (ИмяВидаСравнения = "СОДЕРЖИТ") Тогда
					ВидСравненияОтбора = ВидСравнения.Содержит;
				Иначе
					// Возможно поле отбора содержит подчеркивание.
					ИмяЭлементаОтбора = ЭлементСтруктуры.Ключ;
					ВидСравненияОтбора = ВидСравнения.Равно;
				КонецЕсли;
				
			КонецЕсли;
			
			// Префикс.
			Если Не ПустаяСтрока(Псевдоним) Тогда
				ИмяЭлементаОтбора = Псевдоним + "." + ИмяЭлементаОтбора;
			КонецЕсли;

			// Текст запроса.
			Если НеПервый Тогда
				Текст = Текст + " " + Оператор + " ";
			КонецЕсли;
				
			Если Вложение Тогда
				
				Если (ИмяВидаСравнения = "НЕ") Тогда
					ИмяВидаСравнения = "И";
					Текст = Текст + "НЕ ";
				КонецЕсли; 
				Текст = Текст + "&Отбор_" + ПостфиксПараметраОтбора;
				
				МассивСтруктурОтбора.Добавить(Новый Структура("Расположение,Оператор,Отбор", "&Отбор_" + ПостфиксПараметраОтбора, ИмяВидаСравнения, ЗначениеОтбора));
				
			Иначе
				
				Если (ВидСравненияОтбора = ВидСравнения.Больше) Тогда
					Текст = Текст + ИмяЭлементаОтбора + " > &Отбор_" + ПостфиксПараметраОтбора;
				ИначеЕсли (ВидСравненияОтбора = ВидСравнения.БольшеИлиРавно) Тогда
					Текст = Текст + ИмяЭлементаОтбора + " >= &Отбор_" + ПостфиксПараметраОтбора;
				ИначеЕсли (ВидСравненияОтбора = ВидСравнения.ВИерархии) Тогда
					Текст = Текст + ИмяЭлементаОтбора + " В ИЕРАРХИИ(&Отбор_" + ПостфиксПараметраОтбора + ")";
				ИначеЕсли (ВидСравненияОтбора = ВидСравнения.ВСписке) Тогда
					Текст = Текст + ИмяЭлементаОтбора + " В (&Отбор_" + ПостфиксПараметраОтбора + ")";
				ИначеЕсли (ВидСравненияОтбора = ВидСравнения.ВСпискеПоИерархии) Тогда
					Текст = Текст + ИмяЭлементаОтбора + " В ИЕРАРХИИ(&Отбор_" + ПостфиксПараметраОтбора + ")";
				ИначеЕсли (ВидСравненияОтбора = ВидСравнения.Интервал) Тогда
					Текст = Текст + ИмяЭлементаОтбора + " > &ОтборС_" + ПостфиксПараметраОтбора + " И " + ИмяЭлементаОтбора + " < &ОтборПо_" + ПостфиксПараметраОтбора;
				ИначеЕсли (ВидСравненияОтбора = ВидСравнения.ИнтервалВключаяГраницы) Тогда
					Текст = Текст + ИмяЭлементаОтбора + " >= &ОтборС_" + ПостфиксПараметраОтбора + " И " + ИмяЭлементаОтбора + " <= &ОтборПо_" + ПостфиксПараметраОтбора;
				ИначеЕсли (ВидСравненияОтбора = ВидСравнения.ИнтервалВключаяНачало) Тогда
					Текст = Текст + ИмяЭлементаОтбора + " >= &ОтборС_" + ПостфиксПараметраОтбора + " И " + ПостфиксПараметраОтбора + " < &ОтборПо_" + ПостфиксПараметраОтбора;
				ИначеЕсли (ВидСравненияОтбора = ВидСравнения.ИнтервалВключаяОкончание) Тогда
					Текст = Текст + ИмяЭлементаОтбора + " > &ОтборС_" + ПостфиксПараметраОтбора + " И " + ПостфиксПараметраОтбора + " <= &ОтборПо_" + ПостфиксПараметраОтбора;
				ИначеЕсли (ВидСравненияОтбора = ВидСравнения.Меньше) Тогда
					Текст = Текст + ИмяЭлементаОтбора + " < &Отбор_" + ПостфиксПараметраОтбора;
				ИначеЕсли (ВидСравненияОтбора = ВидСравнения.МеньшеИлиРавно) Тогда
					Текст = Текст + ИмяЭлементаОтбора + " <= &Отбор_" + ПостфиксПараметраОтбора;
				ИначеЕсли (ВидСравненияОтбора = ВидСравнения.НеВИерархии) Тогда
					Текст = Текст + "(НЕ " + ИмяЭлементаОтбора + " В ИЕРАРХИИ(&Отбор_" + ПостфиксПараметраОтбора + "))";
				ИначеЕсли (ВидСравненияОтбора = ВидСравнения.НеВСписке) Тогда
					Текст = Текст + "(НЕ " + ИмяЭлементаОтбора + " В (&Отбор_" + ПостфиксПараметраОтбора + "))";
				ИначеЕсли (ВидСравненияОтбора = ВидСравнения.НеВСпискеПоИерархии) Тогда
					Текст = Текст + "(НЕ " + ИмяЭлементаОтбора + " В ИЕРАРХИИ(&Отбор_" + ПостфиксПараметраОтбора + "))";
				ИначеЕсли (ВидСравненияОтбора = ВидСравнения.НеРавно) Тогда
					Текст = Текст + "(НЕ " + ИмяЭлементаОтбора + " = &Отбор_" + ПостфиксПараметраОтбора + ")";
				ИначеЕсли (ВидСравненияОтбора = ВидСравнения.НеСодержит) Тогда
					Текст = Текст + "(НЕ " + ИмяЭлементаОтбора + " ПОДОБНО &Отбор_" + ПостфиксПараметраОтбора + " СПЕЦСИМВОЛ ""~"")";
				ИначеЕсли (ВидСравненияОтбора = ВидСравнения.Равно) Тогда
					Текст = Текст + ИмяЭлементаОтбора + " = &Отбор_" + ПостфиксПараметраОтбора;
				ИначеЕсли (ВидСравненияОтбора = ВидСравнения.Содержит) Тогда
					Текст = Текст + ИмяЭлементаОтбора + " ПОДОБНО &Отбор_" + ПостфиксПараметраОтбора + " СПЕЦСИМВОЛ ""~""";
				КонецЕсли;
				
				// Параметры запроса.
				Если (ВидСравненияОтбора = ВидСравнения.Интервал) Или (ВидСравненияОтбора = ВидСравнения.ИнтервалВключаяГраницы) Или (ВидСравненияОтбора = ВидСравнения.ИнтервалВключаяНачало) Или (ВидСравненияОтбора = ВидСравнения.ИнтервалВключаяОкончание) Тогда
					Попытка Запрос.УстановитьПараметр("ОтборС_"  + ПостфиксПараметраОтбора, ЗначениеОтбора.С);  Исключение Запрос.УстановитьПараметр("ОтборС_"  + ПостфиксПараметраОтбора, ЗначениеОтбора.ЗначениеС);  КонецПопытки;
					Попытка Запрос.УстановитьПараметр("ОтборПо_" + ПостфиксПараметраОтбора, ЗначениеОтбора.По); Исключение Запрос.УстановитьПараметр("ОтборПо_" + ПостфиксПараметраОтбора, ЗначениеОтбора.ЗначениеПо); КонецПопытки;
				Иначе
					Запрос.УстановитьПараметр("Отбор_" + ПостфиксПараметраОтбора, ЗначениеОтбора);
				КонецЕсли;
				
			КонецЕсли;
		
			НеПервый = Истина; Индекс = Индекс + 1;		
		КонецЦикла;
		
		// Внедрение в полный текст.
		Если СтруктураОтбора.Отбор.Количество() Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, СтруктураОтбора.Расположение, "(" + Текст + ")");
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, СтруктураОтбора.Расположение, "(ИСТИНА)");
		КонецЕсли;
	КонецЦикла;
	
	// Внедрение в текст запроса.
	Запрос.Текст = СтрЗаменить(Запрос.Текст, Расположение, ТекстЗапроса);
	
КонецПроцедуры // НаложитьОтборСтруктурой()
