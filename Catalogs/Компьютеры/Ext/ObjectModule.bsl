
Функция ПолучитьОчереднойКод()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	МАКСИМУМ(Компьютеры.Код) КАК Код
		|ИЗ
		|	Справочник.Компьютеры КАК Компьютеры
		|ГДЕ
		|	Компьютеры.ЭтоГруппа");
	Выборка = Запрос.Выполнить().Выбрать();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Выборка.Следующий() Тогда
		Если Не ПустаяСтрока(Выборка.Код) Тогда
			Возврат ОбщегоНазначения.ПолучитьСледующийНомер(СокрП(Выборка.Код));
		КонецЕсли;
	КонецЕсли;
	
	Возврат "0000000001";
	
КонецФункции // ПолучитьОчереднойКод()


Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	// При обмене данными ничего не проверяем.
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоГруппа Тогда
		// Код.
		Если ПустаяСтрока(Код) Тогда
			Код = ПолучитьОчереднойКод();
		КонецЕсли; 
	Иначе
		
		// Код.
		РазрещенныеСимволы = "0123456789-ABCDEFGHIJKLMNOPQRSTUVWXYZ_ЙЦУКЕНГШЩЗХЪФЫВАПРОЛДЖЭЯЧСМИТЬБЮЁ";
		НепроверенныйКод = ВРег(СокрП(Код)); ПроверенныйКод = Строка(Неопределено); ИзменятьНаименование = (ВРег(Наименование) = НепроверенныйКод);
		Для Индекс = 1 По СтрДлина(НепроверенныйКод) Цикл
			Если (Не Найти(РазрещенныеСимволы, Сред(НепроверенныйКод, Индекс, 1)) = 0) Тогда
				ПроверенныйКод = ПроверенныйКод + Сред(НепроверенныйКод, Индекс, 1);
			КонецЕсли;
		КонецЦикла; Код = ПроверенныйКод;
		__ОбщегоНазначенияСервер.ИзменитьПроверяемыеРеквизиты(ПроверяемыеРеквизиты, "Код");
		
		// Наименование.
		Если ИзменятьНаименование Или ПустаяСтрока(Наименование) Тогда
			Наименование = СокрП(Код);
		КонецЕсли;
		
		// Группа пользователей.
		Если ЗначениеЗаполнено(Пользователь) Тогда
			__ОбщегоНазначенияСервер.ИзменитьПроверяемыеРеквизиты(ПроверяемыеРеквизиты, "ГруппаПользователей");
		КонецЕсли; 
		
		// Интерактивный режим.
		Если ДополнительныеСвойства.Свойство("ИнтерактивныйРежим") Тогда
			ДополнительныеСвойства.Удалить("ИнтерактивныйРежим");
			__ОбщегоНазначенияСервер.ИзменитьПроверяемыеРеквизиты(ПроверяемыеРеквизиты, "РежимРаботы");
		КонецЕсли;
		
	КонецЕсли; 
	
КонецПроцедуры // ОбработкаПроверкиЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	
	Код = Неопределено;
	Если Не ЭтоГруппа Тогда
		Наименование = Неопределено;
		Пользователь = Неопределено;
		ГруппаПользователей = Неопределено;
	КонецЕсли; 
	
КонецПроцедуры // ПриКопировании()

Процедура ПриЗаписи(Отказ)
	
	// При обмене данными ничего не проверяем.
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	РегистрыСведений.ИсторияИспользованияКомпьютеров.ПриЗаписиКомпьютера(ЭтотОбъект, Отказ);
	
КонецПроцедуры // ПриЗаписи()
