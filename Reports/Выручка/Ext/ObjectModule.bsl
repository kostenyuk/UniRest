#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
// Для общей формы "Форма отчета выручка" подсистемы "Варианты отчетов".
Функция ПолучитьНастройкиОтчета() Экспорт
	НастройкиОтчета = ПолучитьНастройкиОтчетаПоУмолчанию();
	НастройкиОтчета.ПараметрыПечатиПоУмолчанию.ПолеСверху = 5;
	НастройкиОтчета.ПараметрыПечатиПоУмолчанию.ПолеСлева = 5;
	НастройкиОтчета.ПараметрыПечатиПоУмолчанию.ПолеСнизу = 5;
	НастройкиОтчета.ПараметрыПечатиПоУмолчанию.ПолеСправа = 5;
	Возврат НастройкиОтчета;
КонецФункции

// Формирует настройки отчета по умолчанию.
//
// Возвращаемое значение:
//   Настройки (Структура)
//       |- СоответствиеПериодичностиПараметров (Соответствие)
//           |- Ключ     (ПараметрКомпоновкиДанных)
//           |- Значение (ПеречислениеСсылка.ДоступныеПериодыОтчета)
//       |- ПараметрыПечатиПоУмолчанию (Структура)
//
Функция ПолучитьНастройкиОтчетаПоУмолчанию() Экспорт
	
	Настройки = Новый Структура;
	Настройки.Вставить("СоответствиеПериодичностиПараметров", Новый Соответствие);
	Настройки.Вставить("ПараметрыПечатиПоУмолчанию", Новый Структура);
	Настройки.ПараметрыПечатиПоУмолчанию.Вставить("ПолеСверху", 10);
	Настройки.ПараметрыПечатиПоУмолчанию.Вставить("ПолеСлева", 10);
	Настройки.ПараметрыПечатиПоУмолчанию.Вставить("ПолеСнизу", 10);
	Настройки.ПараметрыПечатиПоУмолчанию.Вставить("ПолеСправа", 10);
	Настройки.ПараметрыПечатиПоУмолчанию.Вставить("ОриентацияСтраницы", ОриентацияСтраницы.Портрет);
	Настройки.ПараметрыПечатиПоУмолчанию.Вставить("АвтоМасштаб", Истина);
	Настройки.ПараметрыПечатиПоУмолчанию.Вставить("МасштабПечати", Неопределено);
	
	Возврат Настройки;
	
КонецФункции

#КонецЕсли


Процедура СформироватьОтчет(ТабличныйДокумент, НачалоПериода, КонецПериода) Экспорт
	
	РезультатЗапросаПоРесторанам = ВыполнитьЗапросПродажПоРесторанам(НачалоПериода, КонецПериода);
	
	Если РезультатЗапросаПоРесторанам.Пустой() Тогда
		Возврат;	
	КонецЕсли;
	
	РезультатЗапросаПоРесторанамПрогноз = ВыполнитьЗапросПродажПоРесторанам(НачалоПериода, КонецПериода - 86400);
	РезультатЗапросаПоДням 				= ВыполнитьЗапросПродажПоДням(НачалоПериода, КонецПериода);
	
	// Области макета
	Макет 					= ПолучитьМакет("МакетВыручка");
	ОбластьШапка		 	= Макет.ПолучитьОбласть("Шапка|Основная");
	ОбластьСтрока		 	= Макет.ПолучитьОбласть("Строка|Основная");
	ОбластьШапкаДень 		= Макет.ПолучитьОбласть("Шапка|День");
	ОбластьСтрокаДень 		= Макет.ПолучитьОбласть("Строка|День");
	ОбластьИтогоОсновная 	= Макет.ПолучитьОбласть("Итого|Основная");
	ОбластьИтогоДень 		= Макет.ПолучитьОбласть("Итого|День");
	
	ТабличныйДокумент.Очистить();
	
	// Шапка
	ТабличныйДокумент.Вывести(ОбластьШапка);
	ВыборкаДень = РезультатЗапросаПоДням.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "День");
	
	Пока ВыборкаДень.Следующий() Цикл
		ОбластьШапкаДень.Параметры.Заполнить(ВыборкаДень);
		ТабличныйДокумент.Присоединить(ОбластьШапкаДень);
	КонецЦикла;
	
	ТаблицаПродаж = РезультатЗапросаПоРесторанам.Выгрузить();
	
	Если НачалоДня(НачалоПериода) = НачалоДня(КонецПериода) Тогда
		ТаблицаПродажПрогноз = Новый ТаблицаЗначений;
		ТаблицаПродажПрогноз.Колонки.Добавить("Ресторан");
		ТаблицаПродажПрогноз.Колонки.Добавить("СуммаПрогноз");
		ТаблицаПродажПрогноз.Колонки.Добавить("КоличествоКлиентовПрогноз");
		ТаблицаПродажПрогноз.Колонки.Добавить("СреднийЧекПрогноз");
		ТаблицаПродажПрогноз.Колонки.Добавить("СредняяТранзакцияПрогноз");
		ТаблицаПродажПрогноз.Колонки.Добавить("КЦППрогноз");
	Иначе
		ТаблицаПродажПрогноз = РезультатЗапросаПоРесторанамПрогноз.Выгрузить();
	КонецЕсли;
	
	ВыборкаРесторан = РезультатЗапросаПоДням.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Ресторан");
	
	// Строка
	Пока ВыборкаРесторан.Следующий() Цикл
		
		Ресторан = ВыборкаРесторан.Ресторан;
		
		НайденнаяСтрока 		= ТаблицаПродаж.Найти(Ресторан, "Ресторан");
		НайденнаяСтрокаПрогноз 	= ТаблицаПродажПрогноз.Найти(Ресторан, "Ресторан");
		
		Если НайденнаяСтрока <> Неопределено Тогда
			
			// Ресторан
			ОбластьСтрока.Параметры.Ресторан 					= НайденнаяСтрока.Ресторан;
			
			// "План"
			ОбластьСтрока.Параметры.СуммаПлан 					= НайденнаяСтрока.СуммаПлан;
			ОбластьСтрока.Параметры.КоличествоКлиентовПлан 		= НайденнаяСтрока.КоличествоКлиентовПлан;
			ОбластьСтрока.Параметры.СреднийЧекПлан 				= НайденнаяСтрока.СреднийЧекПлан;
			ОбластьСтрока.Параметры.СредняяТранзакцияПлан 		= НайденнаяСтрока.СредняяТранзакцияПлан;
			ОбластьСтрока.Параметры.КЦППлан 					= НайденнаяСтрока.КЦППлан;
			ОбластьСтрока.Параметры.КоличествоТранзакцийПлан 	= "";

			// "Факт"
			ОбластьСтрока.Параметры.СуммаФакт 					= НайденнаяСтрока.СуммаФакт;
			ОбластьСтрока.Параметры.КоличествоКлиентовФакт 		= НайденнаяСтрока.КоличествоКлиентовФакт;
			ОбластьСтрока.Параметры.СреднийЧекФакт 				= НайденнаяСтрока.СреднийЧекФакт;
			ОбластьСтрока.Параметры.СредняяТранзакцияФакт 		= НайденнаяСтрока.СредняяТранзакцияФакт;
			ОбластьСтрока.Параметры.КЦПФакт 					= НайденнаяСтрока.КЦПФакт;
			ОбластьСтрока.Параметры.КоличествоТранзакцийФакт 	= НайденнаяСтрока.КоличествоТранзакцийФакт;
			
			// "Прогноз"
			Если НайденнаяСтрокаПрогноз <> Неопределено Тогда
				
				ОбластьСтрока.Параметры.СуммаПрогноз 				= НайденнаяСтрокаПрогноз.СуммаПрогноз;
				ОбластьСтрока.Параметры.КоличествоКлиентовПрогноз 	= НайденнаяСтрокаПрогноз.КоличествоКлиентовПрогноз;
				ОбластьСтрока.Параметры.СреднийЧекПрогноз 			= НайденнаяСтрокаПрогноз.СреднийЧекПрогноз;
				ОбластьСтрока.Параметры.СредняяТранзакцияПрогноз 	= НайденнаяСтрокаПрогноз.СредняяТранзакцияПрогноз;
				ОбластьСтрока.Параметры.КЦППрогноз 					= НайденнаяСтрокаПрогноз.КЦППрогноз;
				ОбластьСтрока.Параметры.КоличествоТранзакцийПрогноз = "";
				
			КонецЕсли;
			
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
		КонецЕсли;
		
		ВыборкаДень = РезультатЗапросаПоДням.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "День");
		Выборка 	= РезультатЗапросаПоДням.Выбрать();
		
		Пока ВыборкаДень.Следующий() Цикл
			
			СтуктураПоиска = Новый Структура("Ресторан, День", Ресторан, ВыборкаДень.День);
			
			Если Выборка.НайтиСледующий(СтуктураПоиска) Тогда
				ОбластьСтрокаДень.Параметры.Заполнить(Выборка);
			Иначе
				ОбластьСтрокаДень.Параметры.Сумма 					= "";
				ОбластьСтрокаДень.Параметры.КоличествоКлиентов 		= "";
				ОбластьСтрокаДень.Параметры.СреднийЧек 				= "";
				ОбластьСтрокаДень.Параметры.СредняяТранзакция 		= "";
				ОбластьСтрокаДень.Параметры.КЦП 					= "";
				ОбластьСтрокаДень.Параметры.КоличествоТранзакций 	= "";
			КонецЕсли;
			
			ТабличныйДокумент.Присоединить(ОбластьСтрокаДень);
			
		КонецЦикла;

	КонецЦикла;
	
	// Итоги план
	ОбластьИтогоОсновная.Параметры.СуммаПланИтого 						= ТаблицаПродаж.Итог("СуммаПлан");
	ОбластьИтогоОсновная.Параметры.КоличествоКлиентовПланИтого 			= ТаблицаПродаж.Итог("КоличествоКлиентовПлан");
	ОбластьИтогоОсновная.Параметры.КоличествоТранзакцийПланИтого 		= 0;
	ОбластьИтогоОсновная.Параметры.СреднийЧекПланИтого 					= ?(ТаблицаПродаж.Итог("КоличествоКлиентовПлан") = 0, 0, ТаблицаПродаж.Итог("СуммаПлан") / ТаблицаПродаж.Итог("КоличествоКлиентовПлан"));
	ОбластьИтогоОсновная.Параметры.СредняяТранзакцияПланИтого 			= ?(ТаблицаПродаж.Итог("КоличествоКлиентовПлан") = 0, 0, ТаблицаПродаж.Итог("КоличествоКлиентовСреднееПлан") / ТаблицаПродаж.Количество() * ТаблицаПродаж.Итог("СуммаПлан") / ТаблицаПродаж.Итог("КоличествоКлиентовПлан"));
	ОбластьИтогоОсновная.Параметры.КЦППланИтого 						= ?(ТаблицаПродаж.Итог("КоличествоКлиентовПлан") = 0, 0, ТаблицаПродаж.Итог("КоличествоБлюдПлан") / ТаблицаПродаж.Итог("КоличествоКлиентовПлан"));
	
	// Итоги прогноз
	ОбластьИтогоОсновная.Параметры.СуммаПрогнозИтого 					= ТаблицаПродажПрогноз.Итог("СуммаПрогноз");
	ОбластьИтогоОсновная.Параметры.КоличествоКлиентовПрогнозИтого 		= ТаблицаПродажПрогноз.Итог("КоличествоКлиентовПрогноз");
	ОбластьИтогоОсновная.Параметры.КоличествоТранзакцийПрогнозИтого 	= 0;
	ОбластьИтогоОсновная.Параметры.СреднийЧекПрогнозИтого 				= ?(ТаблицаПродажПрогноз.Итог("КоличествоКлиентовПрогноз") = 0, 0, ТаблицаПродажПрогноз.Итог("СуммаПрогноз") / ТаблицаПродажПрогноз.Итог("КоличествоКлиентовПрогноз"));
	ОбластьИтогоОсновная.Параметры.СредняяТранзакцияПрогнозИтого 		= ?(ТаблицаПродажПрогноз.Итог("КоличествоКлиентовПрогноз") = 0, 0, ТаблицаПродажПрогноз.Итог("КоличествоКлиентовФакт") / ТаблицаПродажПрогноз.Итог("КоличествоТранзакцийФакт") * ТаблицаПродажПрогноз.Итог("СуммаПрогноз") / ТаблицаПродажПрогноз.Итог("КоличествоКлиентовПрогноз"));
	ОбластьИтогоОсновная.Параметры.КЦППрогнозИтого 						= ?(ТаблицаПродажПрогноз.Итог("КоличествоКлиентовПрогноз") = 0, 0, ТаблицаПродажПрогноз.Итог("КоличествоБлюдПрогноз") / ТаблицаПродажПрогноз.Итог("КоличествоКлиентовПрогноз"));
	
	// Итоги факт
	ОбластьИтогоОсновная.Параметры.СуммаФактИтого 						= ТаблицаПродаж.Итог("СуммаФакт");
	ОбластьИтогоОсновная.Параметры.КоличествоКлиентовФактИтого 			= ТаблицаПродаж.Итог("КоличествоКлиентовФакт");
	ОбластьИтогоОсновная.Параметры.КоличествоТранзакцийФактИтого 		= ТаблицаПродаж.Итог("КоличествоТранзакцийФакт");
	ОбластьИтогоОсновная.Параметры.СреднийЧекФактИтого 					= ?(ТаблицаПродаж.Итог("КоличествоКлиентовФакт") = 0, 0, ТаблицаПродаж.Итог("СуммаФакт") / ТаблицаПродаж.Итог("КоличествоКлиентовФакт"));
	ОбластьИтогоОсновная.Параметры.СредняяТранзакцияФактИтого 			= ?(ТаблицаПродаж.Итог("КоличествоТранзакцийФакт") = 0, 0, ТаблицаПродаж.Итог("СуммаФакт") / ТаблицаПродаж.Итог("КоличествоТранзакцийФакт"));
	ОбластьИтогоОсновная.Параметры.КЦПФактИтого 						= ?(ТаблицаПродаж.Итог("КоличествоКлиентовФакт") = 0, 0, ТаблицаПродаж.Итог("КоличествоБлюдФакт") / ТаблицаПродаж.Итог("КоличествоКлиентовФакт"));
	
	ТабличныйДокумент.Вывести(ОбластьИтогоОсновная);
	
	ВыборкаДень = РезультатЗапросаПоДням.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "День");
	
	Пока ВыборкаДень.Следующий() Цикл
		
		ОбластьИтогоДень.Параметры.СуммаИтого 						= ВыборкаДень.Сумма;
		ОбластьИтогоДень.Параметры.КоличествоКлиентовИтого 			= ВыборкаДень.КоличествоКлиентов;
		ОбластьИтогоДень.Параметры.КоличествоТранзакцийИтого 		= ВыборкаДень.КоличествоТранзакций;
		ОбластьИтогоДень.Параметры.СреднийЧекИтого 					= ?(ВыборкаДень.КоличествоКлиентов = 0, 0, ВыборкаДень.Сумма / ВыборкаДень.КоличествоКлиентов);
		ОбластьИтогоДень.Параметры.СредняяТранзакцияИтого 			= ?(ВыборкаДень.КоличествоТранзакций = 0, 0, ВыборкаДень.Сумма / ВыборкаДень.КоличествоТранзакций);
		ОбластьИтогоДень.Параметры.КЦПИтого 						= ?(ВыборкаДень.КоличествоКлиентов = 0, 0, ВыборкаДень.КоличествоБлюд / ВыборкаДень.КоличествоКлиентов);
		
		ТабличныйДокумент.Присоединить(ОбластьИтогоДень);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ВыполнитьЗапросПродажПоРесторанам(НачалоПериода, КонецПериода)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДЕНЬ(КОНЕЦПЕРИОДА(&КонецПериода, МЕСЯЦ)) КАК ПериодТекущийКоличествоДней,
	               |	РАЗНОСТЬДАТ(ДОБАВИТЬКДАТЕ(&НачалоПериода, СЕКУНДА, -86400), &КонецПериода, ДЕНЬ) КАК ПериодОтчетаКоличествоДней
	               |ПОМЕСТИТЬ Периоды
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ПродажиОбороты.Ресторан КАК Ресторан,
	               |	КОЛИЧЕСТВО(РеализацияТоваровУслуг.Ссылка) КАК КоличествоДокументов,
	               |	СУММА(ВЫБОР
	               |			КОГДА РеализацияТоваровУслуг.КоличествоКлиентов = 0
	               |				ТОГДА 0
	               |			ИНАЧЕ 1
	               |		КОНЕЦ) КАК КоличествоТранзакций,
	               |	СУММА(РеализацияТоваровУслуг.КоличествоКлиентов) КАК КоличествоКлиентов
	               |ПОМЕСТИТЬ ТранзакцииПосещения
	               |ИЗ
	               |	РегистрНакопления.Продажи.Обороты(&НачалоПериода, &КонецПериода, Авто, Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Условие)) КАК ПродажиОбороты
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	               |		ПО ПродажиОбороты.Ресторан = РеализацияТоваровУслуг.Ресторан
	               |			И ПродажиОбороты.Регистратор = РеализацияТоваровУслуг.Ссылка
	               |			И ПродажиОбороты.Оплата = РеализацияТоваровУслуг.Оплата
	               |ГДЕ
	               |	ПродажиОбороты.Оплата В(&Оплата)
	               |	И ВЫБОР
	               |			КОГДА ПродажиОбороты.КоличествоОборот <> 0
	               |				ТОГДА ВЫБОР
	               |						КОГДА &ВидСравнения = ЗНАЧЕНИЕ(Перечисление.ВидыСравнения.Равно)
	               |							ТОГДА ПродажиОбороты.СтоимостьОборот / ПродажиОбороты.КоличествоОборот = &ЗначениеСравнения
	               |						КОГДА &ВидСравнения = ЗНАЧЕНИЕ(Перечисление.ВидыСравнения.НеРавно)
	               |							ТОГДА ПродажиОбороты.СтоимостьОборот / ПродажиОбороты.КоличествоОборот <> &ЗначениеСравнения
	               |						КОГДА &ВидСравнения = ЗНАЧЕНИЕ(Перечисление.ВидыСравнения.Меньше)
	               |							ТОГДА ПродажиОбороты.СтоимостьОборот / ПродажиОбороты.КоличествоОборот < &ЗначениеСравнения
	               |						КОГДА &ВидСравнения = ЗНАЧЕНИЕ(Перечисление.ВидыСравнения.МеньшеИлиРавно)
	               |							ТОГДА ПродажиОбороты.СтоимостьОборот / ПродажиОбороты.КоличествоОборот <= &ЗначениеСравнения
	               |						КОГДА &ВидСравнения = ЗНАЧЕНИЕ(Перечисление.ВидыСравнения.Больше)
	               |							ТОГДА ПродажиОбороты.СтоимостьОборот / ПродажиОбороты.КоличествоОборот > &ЗначениеСравнения
	               |						КОГДА &ВидСравнения = ЗНАЧЕНИЕ(Перечисление.ВидыСравнения.БольшеИлиРавно)
	               |							ТОГДА ПродажиОбороты.СтоимостьОборот / ПродажиОбороты.КоличествоОборот >= &ЗначениеСравнения
	               |					КОНЕЦ
	               |		КОНЕЦ
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ПродажиОбороты.Ресторан
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ПродажиОбороты.Ресторан,
	               |	СУММА(ПродажиОбороты.КоличествоОборот) КАК КоличествоБлюд,
	               |	СУММА(ПродажиОбороты.АкцизныйНалогОборот) КАК АкцизныйНалог,
	               |	СУММА(ПродажиОбороты.СтоимостьБезСкидокОборот) КАК СуммаБезСкидок,
	               |	СУММА(ПродажиОбороты.СуммаНаценкиОборот) КАК СуммаНаценки,
	               |	СУММА(ПродажиОбороты.СуммаСкидкиОборот) КАК СуммаСкидки,
	               |	СУММА(ПродажиОбороты.СтоимостьОборот) КАК Сумма,
	               |	СУММА(ПродажиОбороты.КоличествоОборот * СебестоимостьНоменклатуры.Себестоимость) КАК Себестоимость
	               |ПОМЕСТИТЬ ПродажиСебестоимость
	               |ИЗ
	               |	РегистрНакопления.Продажи.Обороты(&НачалоПериода, &КонецПериода, Авто, Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Условие)) КАК ПродажиОбороты
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СебестоимостьНоменклатуры КАК СебестоимостьНоменклатуры
	               |		ПО ПродажиОбороты.Ресторан = СебестоимостьНоменклатуры.Ресторан
	               |			И ПродажиОбороты.Номенклатура = СебестоимостьНоменклатуры.Номенклатура
	               |			И (НАЧАЛОПЕРИОДА(ПродажиОбороты.ПериодМесяц, МЕСЯЦ) = СебестоимостьНоменклатуры.Период)
	               |ГДЕ
	               |	ПродажиОбороты.Оплата В(&Оплата)
	               |	И ВЫБОР
	               |			КОГДА ПродажиОбороты.КоличествоОборот <> 0
	               |				ТОГДА ВЫБОР
	               |						КОГДА &ВидСравнения = ЗНАЧЕНИЕ(Перечисление.ВидыСравнения.Равно)
	               |							ТОГДА ПродажиОбороты.СтоимостьОборот / ПродажиОбороты.КоличествоОборот = &ЗначениеСравнения
	               |						КОГДА &ВидСравнения = ЗНАЧЕНИЕ(Перечисление.ВидыСравнения.НеРавно)
	               |							ТОГДА ПродажиОбороты.СтоимостьОборот / ПродажиОбороты.КоличествоОборот <> &ЗначениеСравнения
	               |						КОГДА &ВидСравнения = ЗНАЧЕНИЕ(Перечисление.ВидыСравнения.Меньше)
	               |							ТОГДА ПродажиОбороты.СтоимостьОборот / ПродажиОбороты.КоличествоОборот < &ЗначениеСравнения
	               |						КОГДА &ВидСравнения = ЗНАЧЕНИЕ(Перечисление.ВидыСравнения.МеньшеИлиРавно)
	               |							ТОГДА ПродажиОбороты.СтоимостьОборот / ПродажиОбороты.КоличествоОборот <= &ЗначениеСравнения
	               |						КОГДА &ВидСравнения = ЗНАЧЕНИЕ(Перечисление.ВидыСравнения.Больше)
	               |							ТОГДА ПродажиОбороты.СтоимостьОборот / ПродажиОбороты.КоличествоОборот > &ЗначениеСравнения
	               |						КОГДА &ВидСравнения = ЗНАЧЕНИЕ(Перечисление.ВидыСравнения.БольшеИлиРавно)
	               |							ТОГДА ПродажиОбороты.СтоимостьОборот / ПродажиОбороты.КоличествоОборот >= &ЗначениеСравнения
	               |					КОНЕЦ
	               |		КОНЕЦ
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ПродажиОбороты.Ресторан
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПланыПродажОбороты.Ресторан,
	               |	МАКСИМУМ(ПланыПродажОбороты.Регистратор.КоличествоКлиентов) КАК КоличествоКлиентов,
	               |	МАКСИМУМ(ПланыПродажОбороты.Регистратор.КоличествоКлиентовСреднее) КАК КоличествоКлиентовСреднее,
	               |	СУММА(ПланыПродажОбороты.КоличествоОборот) КАК КоличествоБлюд,
	               |	СУММА(ПланыПродажОбороты.КЦПОборот) КАК КЦП,
	               |	СУММА(ПланыПродажОбороты.ЦенаОборот) КАК Цена,
	               |	СУММА(ПланыПродажОбороты.СреднийЧекОборот) КАК СреднийЧек,
	               |	СУММА(ПланыПродажОбороты.СредняяТранзакцияОборот) КАК СредняяТранзакция,
	               |	СУММА(ПланыПродажОбороты.СуммаБезСкидокОборот) КАК СуммаБезСкидок,
	               |	СУММА(ПланыПродажОбороты.СуммаСкидкиОборот) КАК СуммаСкидки,
	               |	СРЕДНЕЕ(ПланыПродажОбороты.СуммаСкидкиОборот / ПланыПродажОбороты.СуммаБезСкидокОборот * 100) КАК СкидкаПроцент,
	               |	СУММА(ПланыПродажОбороты.СуммаОборот) КАК Сумма,
	               |	СУММА(ПланыПродажОбороты.СебестоимостьОборот) КАК Себестоимость,
	               |	СРЕДНЕЕ(ПланыПродажОбороты.СебестоимостьОборот / ПланыПродажОбороты.СуммаОборот * 100) КАК СебестоимостьПроцент,
	               |	СУММА(ПланыПродажОбороты.ВаловыйДоходОборот) КАК ВаловыйДоход
	               |ПОМЕСТИТЬ ПродажиПлан
	               |ИЗ
	               |	РегистрНакопления.ПланыПродаж.Обороты(&НачалоПериода, &КонецПериода, Авто, ) КАК ПланыПродажОбороты
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ПланыПродажОбороты.Ресторан
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПродажиСебестоимость.Ресторан,
	               |	СУММА(ТранзакцииПосещения.КоличествоДокументов) КАК КоличествоДокументов,
	               |	СУММА(ТранзакцииПосещения.КоличествоТранзакций) КАК КоличествоТранзакций,
	               |	СУММА(ТранзакцииПосещения.КоличествоКлиентов) КАК КоличествоКлиентов,
	               |	СУММА(ВЫБОР
	               |			КОГДА ТранзакцииПосещения.КоличествоТранзакций = 0
	               |				ТОГДА 0
	               |			ИНАЧЕ ТранзакцииПосещения.КоличествоКлиентов / ТранзакцииПосещения.КоличествоТранзакций
	               |		КОНЕЦ) КАК КоличествоКлиентовСреднее,
	               |	СУММА(ПродажиСебестоимость.КоличествоБлюд) КАК КоличествоБлюд,
	               |	СУММА(ПродажиСебестоимость.АкцизныйНалог) КАК АкцизныйНалог,
	               |	СУММА(ПродажиСебестоимость.СуммаБезСкидок) КАК СуммаБезСкидок,
	               |	СУММА(ПродажиСебестоимость.СуммаНаценки) КАК СуммаНаценки,
	               |	СУММА(ПродажиСебестоимость.СуммаСкидки) КАК СуммаСкидки,
	               |	СУММА(ПродажиСебестоимость.Сумма) КАК Сумма,
	               |	СУММА(ПродажиСебестоимость.Себестоимость) КАК Себестоимость,
	               |	СУММА(ВЫБОР
	               |			КОГДА ТранзакцииПосещения.КоличествоКлиентов = 0
	               |				ТОГДА 0
	               |			ИНАЧЕ ПродажиСебестоимость.КоличествоБлюд / ТранзакцииПосещения.КоличествоКлиентов
	               |		КОНЕЦ) КАК КЦП,
	               |	СУММА(ВЫБОР
	               |			КОГДА ПродажиСебестоимость.КоличествоБлюд = 0
	               |				ТОГДА 0
	               |			ИНАЧЕ ПродажиСебестоимость.Сумма / ПродажиСебестоимость.КоличествоБлюд
	               |		КОНЕЦ) КАК Цена,
	               |	СУММА(ВЫБОР
	               |			КОГДА ТранзакцииПосещения.КоличествоКлиентов = 0
	               |				ТОГДА 0
	               |			ИНАЧЕ ПродажиСебестоимость.Сумма / ТранзакцииПосещения.КоличествоКлиентов
	               |		КОНЕЦ) КАК СреднийЧек,
	               |	СУММА(ВЫБОР
	               |			КОГДА ТранзакцииПосещения.КоличествоТранзакций = 0
	               |				ТОГДА 0
	               |			ИНАЧЕ ПродажиСебестоимость.Сумма / ТранзакцииПосещения.КоличествоТранзакций
	               |		КОНЕЦ) КАК СредняяТранзакция,
	               |	СУММА(ВЫБОР
	               |			КОГДА ПродажиСебестоимость.СуммаБезСкидок = 0
	               |				ТОГДА 0
	               |			ИНАЧЕ ПродажиСебестоимость.СуммаСкидки / ПродажиСебестоимость.СуммаБезСкидок * 100
	               |		КОНЕЦ) КАК СкидкаПроцент,
	               |	СУММА(ВЫБОР
	               |			КОГДА ПродажиСебестоимость.Сумма = 0
	               |				ТОГДА 0
	               |			ИНАЧЕ ПродажиСебестоимость.Себестоимость / ПродажиСебестоимость.Сумма * 100
	               |		КОНЕЦ) КАК СебестоимостьПроцент,
	               |	СУММА(ПродажиСебестоимость.Сумма - ПродажиСебестоимость.Себестоимость) КАК ВаловыйДоход
	               |ПОМЕСТИТЬ ПродажиФакт
	               |ИЗ
	               |	ПродажиСебестоимость КАК ПродажиСебестоимость
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ТранзакцииПосещения КАК ТранзакцииПосещения
	               |		ПО ПродажиСебестоимость.Ресторан = ТранзакцииПосещения.Ресторан
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ПродажиСебестоимость.Ресторан
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПродажиФакт.Ресторан,
	               |	СУММА(ПродажиФакт.КоличествоКлиентов / Периоды.ПериодОтчетаКоличествоДней * Периоды.ПериодТекущийКоличествоДней) КАК КоличествоКлиентов,
	               |	СУММА(ПродажиФакт.КоличествоКлиентовСреднее) КАК КоличествоКлиентовСреднее,
	               |	СУММА(ПродажиФакт.КоличествоКлиентов / Периоды.ПериодОтчетаКоличествоДней * Периоды.ПериодТекущийКоличествоДней * ПродажиФакт.КЦП) КАК КоличествоБлюд,
	               |	СУММА(ПродажиФакт.КЦП) КАК КЦП,
	               |	СУММА(ПродажиФакт.Цена) КАК Цена,
	               |	СУММА(ПродажиФакт.КЦП * ПродажиФакт.Цена) КАК СреднийЧек,
	               |	СУММА(ПродажиФакт.КоличествоКлиентовСреднее * ПродажиФакт.КЦП * ПродажиФакт.Цена) КАК СредняяТранзакция,
	               |	СУММА(ПродажиФакт.КоличествоКлиентов / Периоды.ПериодОтчетаКоличествоДней * Периоды.ПериодТекущийКоличествоДней * ПродажиФакт.КЦП * ПродажиФакт.Цена / ((100 - ПродажиФакт.СкидкаПроцент) / 100)) КАК СуммаБезСкидок,
	               |	СУММА(ПродажиФакт.КоличествоКлиентов / Периоды.ПериодОтчетаКоличествоДней * Периоды.ПериодТекущийКоличествоДней * ПродажиФакт.КЦП * ПродажиФакт.Цена / ((100 - ПродажиФакт.СкидкаПроцент) / 100) - ПродажиФакт.КоличествоКлиентов / Периоды.ПериодОтчетаКоличествоДней * Периоды.ПериодТекущийКоличествоДней * ПродажиФакт.КЦП * ПродажиФакт.Цена) КАК СуммаСкидки,
	               |	СУММА(ПродажиФакт.СкидкаПроцент) КАК СкидкаПроцент,
	               |	СУММА(ПродажиФакт.КоличествоКлиентов / Периоды.ПериодОтчетаКоличествоДней * Периоды.ПериодТекущийКоличествоДней * ПродажиФакт.КЦП * ПродажиФакт.Цена) КАК Сумма,
	               |	СУММА(ПродажиФакт.КоличествоКлиентов / Периоды.ПериодОтчетаКоличествоДней * Периоды.ПериодТекущийКоличествоДней * ПродажиФакт.КЦП * ПродажиФакт.Цена * ПродажиФакт.СебестоимостьПроцент / 100) КАК Себестоимость,
	               |	СУММА(ПродажиФакт.СебестоимостьПроцент) КАК СебестоимостьПроцент,
	               |	СУММА(ПродажиФакт.КоличествоКлиентов / Периоды.ПериодОтчетаКоличествоДней * Периоды.ПериодТекущийКоличествоДней * ПродажиФакт.КЦП * ПродажиФакт.Цена - ПродажиФакт.КоличествоКлиентов / Периоды.ПериодОтчетаКоличествоДней * Периоды.ПериодТекущийКоличествоДней * ПродажиФакт.КЦП * ПродажиФакт.Цена * ПродажиФакт.СебестоимостьПроцент / 100) КАК ВаловыйДоход
	               |ПОМЕСТИТЬ ПродажиПрогноз
	               |ИЗ
	               |	Периоды КАК Периоды,
	               |	ПродажиФакт КАК ПродажиФакт
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ПродажиФакт.Ресторан
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЕСТЬNULL(ПродажиФакт.Ресторан, ПродажиПлан.Ресторан) КАК Ресторан,
	               |	СУММА(ПродажиПлан.КоличествоКлиентов) КАК КоличествоКлиентовПлан,
	               |	СУММА(ПродажиПлан.КоличествоКлиентовСреднее) КАК КоличествоКлиентовСреднееПлан,
	               |	СУММА(ПродажиПлан.КоличествоБлюд) КАК КоличествоБлюдПлан,
	               |	СУММА(ПродажиПлан.КЦП) КАК КЦППлан,
	               |	СУММА(ПродажиПлан.Цена) КАК ЦенаПлан,
	               |	СУММА(ПродажиПлан.СреднийЧек) КАК СреднийЧекПлан,
	               |	СУММА(ПродажиПлан.СредняяТранзакция) КАК СредняяТранзакцияПлан,
	               |	СУММА(ПродажиПлан.СуммаБезСкидок) КАК СуммаБезСкидокПлан,
	               |	СУММА(ПродажиПлан.СуммаСкидки) КАК СкидкаПлан,
	               |	СУММА(ПродажиПлан.СкидкаПроцент) КАК СкидкаПроцентПлан,
	               |	СУММА(ПродажиПлан.Сумма) КАК СуммаПлан,
	               |	СУММА(ПродажиПлан.Себестоимость) КАК СебестоимостьПлан,
	               |	СУММА(ПродажиПлан.СебестоимостьПроцент) КАК СебестоимостьПроцентПлан,
	               |	СУММА(ПродажиПлан.ВаловыйДоход) КАК ВаловыйДоходПлан,
	               |	СУММА(ПродажиФакт.КоличествоДокументов) КАК КоличествоДокументовФакт,
	               |	СУММА(ПродажиФакт.КоличествоТранзакций) КАК КоличествоТранзакцийФакт,
	               |	СУММА(ПродажиФакт.КоличествоКлиентов) КАК КоличествоКлиентовФакт,
	               |	СУММА(ПродажиФакт.КоличествоБлюд) КАК КоличествоБлюдФакт,
	               |	СУММА(ПродажиФакт.АкцизныйНалог) КАК АкцизныйНалогФакт,
	               |	СУММА(ПродажиФакт.СуммаБезСкидок) КАК СуммаБезСкидокФакт,
	               |	СУММА(ПродажиФакт.СуммаНаценки) КАК НаценкаФакт,
	               |	СУММА(ПродажиФакт.СуммаСкидки) КАК СкидкаФакт,
	               |	СУММА(ПродажиФакт.Сумма) КАК СуммаФакт,
	               |	СУММА(ПродажиФакт.Себестоимость) КАК СебестоимостьФакт,
	               |	СУММА(ПродажиФакт.КоличествоКлиентовСреднее) КАК КоличествоКлиентовСреднееФакт,
	               |	СУММА(ПродажиФакт.КЦП) КАК КЦПФакт,
	               |	СУММА(ПродажиФакт.Цена) КАК ЦенаФакт,
	               |	СУММА(ПродажиФакт.СреднийЧек) КАК СреднийЧекФакт,
	               |	СУММА(ПродажиФакт.СредняяТранзакция) КАК СредняяТранзакцияФакт,
	               |	СУММА(ПродажиФакт.СкидкаПроцент) КАК СкидкаПроцентФакт,
	               |	СУММА(ПродажиФакт.СебестоимостьПроцент) КАК СебестоимостьПроцентФакт,
	               |	СУММА(ПродажиФакт.ВаловыйДоход) КАК ВаловыйДоходФакт,
	               |	СУММА(ПродажиПрогноз.КоличествоБлюд - ПродажиПлан.КоличествоБлюд) КАК КоличествоБлюдОтклонение,
	               |	СУММА(ПродажиПрогноз.КЦП - ПродажиПлан.КЦП) КАК КЦПОтклонение,
	               |	СУММА(ПродажиПрогноз.Цена - ПродажиПлан.Цена) КАК ЦенаОтклонение,
	               |	СУММА(ПродажиПрогноз.СреднийЧек - ПродажиПлан.СреднийЧек) КАК СреднийЧекОтклонение,
	               |	СУММА(ПродажиПрогноз.СредняяТранзакция - ПродажиПлан.СредняяТранзакция) КАК СредняяТранзакцияОтклонение,
	               |	СУММА(ПродажиПрогноз.СуммаБезСкидок - ПродажиПлан.СуммаБезСкидок) КАК СуммаБезСкидокОтклонение,
	               |	СУММА(ПродажиПрогноз.СуммаСкидки - ПродажиПлан.СуммаСкидки) КАК СкидкаОтклонение,
	               |	СУММА(ПродажиПрогноз.СкидкаПроцент - ПродажиПлан.СкидкаПроцент) КАК СкидкаПроцентОтклонение,
	               |	СУММА(ПродажиПрогноз.Сумма - ПродажиПлан.Сумма) КАК СуммаОтклонение,
	               |	СУММА(ПродажиПрогноз.Себестоимость - ПродажиПлан.Себестоимость) КАК СебестоимостьОтклонение,
	               |	СУММА(ПродажиПрогноз.СебестоимостьПроцент - ПродажиПлан.СебестоимостьПроцент) КАК СебестоимостьПроцентОтклонение,
	               |	СУММА(ПродажиПрогноз.ВаловыйДоход - ПродажиПлан.ВаловыйДоход) КАК ВаловыйДоходОтклонение,
	               |	СУММА(ПродажиПрогноз.КоличествоКлиентов) КАК КоличествоКлиентовПрогноз,
	               |	СУММА(ПродажиПрогноз.КоличествоКлиентовСреднее) КАК КоличествоКлиентовСреднееПрогноз,
	               |	СУММА(ПродажиПрогноз.КоличествоБлюд) КАК КоличествоБлюдПрогноз,
	               |	СУММА(ПродажиПрогноз.КЦП) КАК КЦППрогноз,
	               |	СУММА(ПродажиПрогноз.Цена) КАК ЦенаПрогноз,
	               |	СУММА(ПродажиПрогноз.СреднийЧек) КАК СреднийЧекПрогноз,
	               |	СУММА(ПродажиПрогноз.СредняяТранзакция) КАК СредняяТранзакцияПрогноз,
	               |	СУММА(ПродажиПрогноз.СуммаБезСкидок) КАК СуммаБезСкидокПрогноз,
	               |	СУММА(ПродажиПрогноз.СуммаСкидки) КАК СкидкаПрогноз,
	               |	СУММА(ПродажиПрогноз.СкидкаПроцент) КАК СкидкаПроцентПрогноз,
	               |	СУММА(ПродажиПрогноз.Сумма) КАК СуммаПрогноз,
	               |	СУММА(ПродажиПрогноз.Себестоимость) КАК СебестоимостьПрогноз,
	               |	СУММА(ПродажиПрогноз.СебестоимостьПроцент) КАК СебестоимостьПроцентПрогноз,
	               |	СУММА(ПродажиПрогноз.ВаловыйДоход) КАК ВаловыйДоходПрогноз
	               |ИЗ
	               |	ПродажиФакт КАК ПродажиФакт
	               |		ПОЛНОЕ СОЕДИНЕНИЕ ПродажиПлан КАК ПродажиПлан
	               |		ПО ПродажиФакт.Ресторан = ПродажиПлан.Ресторан
	               |		ПОЛНОЕ СОЕДИНЕНИЕ ПродажиПрогноз КАК ПродажиПрогноз
	               |		ПО ПродажиФакт.Ресторан = ПродажиПрогноз.Ресторан
	               |ГДЕ
	               |	ПродажиФакт.Ресторан В(&Ресторан)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ЕСТЬNULL(ПродажиФакт.Ресторан, ПродажиПлан.Ресторан)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Ресторан";
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	
	Если ЗначениеЗаполнено(ОтборРесторан) Тогда
		Запрос.УстановитьПараметр("Ресторан", ОтборРесторан);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПродажиФакт.Ресторан В(&Ресторан)", "ИСТИНА");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборОплата) Тогда
		Запрос.УстановитьПараметр("Оплата", ОтборОплата);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПродажиОбороты.Оплата В(&Оплата)", "ИСТИНА");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ВидСравнения", ВидСравнения);
	Запрос.УстановитьПараметр("ЗначениеСравнения", ЗначениеСравнения);
	
	Возврат Запрос.Выполнить();

КонецФункции

Функция ВыполнитьЗапросПродажПоДням(НачалоПериода, КонецПериода)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДЕНЬ(КОНЕЦПЕРИОДА(&КонецПериода, МЕСЯЦ)) КАК ПериодТекущийКоличествоДней,
	               |	РАЗНОСТЬДАТ(ДОБАВИТЬКДАТЕ(&НачалоПериода, СЕКУНДА, -86400), &КонецПериода, ДЕНЬ) КАК ПериодОтчетаКоличествоДней
	               |ПОМЕСТИТЬ Периоды
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ПродажиОбороты.Ресторан КАК Ресторан,
	               |	ПродажиОбороты.ПериодДень,
	               |	КОЛИЧЕСТВО(РеализацияТоваровУслуг.Ссылка) КАК КоличествоДокументов,
	               |	СУММА(ВЫБОР
	               |			КОГДА РеализацияТоваровУслуг.КоличествоКлиентов = 0
	               |				ТОГДА 0
	               |			ИНАЧЕ 1
	               |		КОНЕЦ) КАК КоличествоТранзакций,
	               |	СУММА(РеализацияТоваровУслуг.КоличествоКлиентов) КАК КоличествоКлиентов
	               |ПОМЕСТИТЬ ТранзакцииПосещения
	               |ИЗ
	               |	РегистрНакопления.Продажи.Обороты(&НачалоПериода, &КонецПериода, Авто, Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Условие)) КАК ПродажиОбороты
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	               |		ПО ПродажиОбороты.Ресторан = РеализацияТоваровУслуг.Ресторан
	               |			И ПродажиОбороты.Регистратор = РеализацияТоваровУслуг.Ссылка
	               |			И ПродажиОбороты.Оплата = РеализацияТоваровУслуг.Оплата
	               |ГДЕ
	               |	ПродажиОбороты.Оплата В(&Оплата)
	               |	И ВЫБОР
	               |			КОГДА ПродажиОбороты.КоличествоОборот <> 0
	               |				ТОГДА ВЫБОР
	               |						КОГДА &ВидСравнения = ЗНАЧЕНИЕ(Перечисление.ВидыСравнения.Равно)
	               |							ТОГДА ПродажиОбороты.СтоимостьОборот / ПродажиОбороты.КоличествоОборот = &ЗначениеСравнения
	               |						КОГДА &ВидСравнения = ЗНАЧЕНИЕ(Перечисление.ВидыСравнения.НеРавно)
	               |							ТОГДА ПродажиОбороты.СтоимостьОборот / ПродажиОбороты.КоличествоОборот <> &ЗначениеСравнения
	               |						КОГДА &ВидСравнения = ЗНАЧЕНИЕ(Перечисление.ВидыСравнения.Меньше)
	               |							ТОГДА ПродажиОбороты.СтоимостьОборот / ПродажиОбороты.КоличествоОборот < &ЗначениеСравнения
	               |						КОГДА &ВидСравнения = ЗНАЧЕНИЕ(Перечисление.ВидыСравнения.МеньшеИлиРавно)
	               |							ТОГДА ПродажиОбороты.СтоимостьОборот / ПродажиОбороты.КоличествоОборот <= &ЗначениеСравнения
	               |						КОГДА &ВидСравнения = ЗНАЧЕНИЕ(Перечисление.ВидыСравнения.Больше)
	               |							ТОГДА ПродажиОбороты.СтоимостьОборот / ПродажиОбороты.КоличествоОборот > &ЗначениеСравнения
	               |						КОГДА &ВидСравнения = ЗНАЧЕНИЕ(Перечисление.ВидыСравнения.БольшеИлиРавно)
	               |							ТОГДА ПродажиОбороты.СтоимостьОборот / ПродажиОбороты.КоличествоОборот >= &ЗначениеСравнения
	               |					КОНЕЦ
	               |		КОНЕЦ
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ПродажиОбороты.Ресторан,
	               |	ПродажиОбороты.ПериодДень
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ПродажиОбороты.Ресторан,
	               |	ПродажиОбороты.ПериодДень,
	               |	СУММА(ПродажиОбороты.КоличествоОборот) КАК КоличествоБлюд,
	               |	СУММА(ПродажиОбороты.АкцизныйНалогОборот) КАК АкцизныйНалог,
	               |	СУММА(ПродажиОбороты.СтоимостьБезСкидокОборот) КАК СуммаБезСкидок,
	               |	СУММА(ПродажиОбороты.СуммаНаценкиОборот) КАК СуммаНаценки,
	               |	СУММА(ПродажиОбороты.СуммаСкидкиОборот) КАК СуммаСкидки,
	               |	СУММА(ПродажиОбороты.СтоимостьОборот) КАК Сумма,
	               |	СУММА(ПродажиОбороты.КоличествоОборот * СебестоимостьНоменклатуры.Себестоимость) КАК Себестоимость
	               |ПОМЕСТИТЬ ПродажиСебестоимость
	               |ИЗ
	               |	РегистрНакопления.Продажи.Обороты(&НачалоПериода, &КонецПериода, Авто, Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Условие)) КАК ПродажиОбороты
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СебестоимостьНоменклатуры КАК СебестоимостьНоменклатуры
	               |		ПО ПродажиОбороты.Ресторан = СебестоимостьНоменклатуры.Ресторан
	               |			И ПродажиОбороты.Номенклатура = СебестоимостьНоменклатуры.Номенклатура
	               |			И ПродажиОбороты.ПериодМесяц = СебестоимостьНоменклатуры.Период
	               |ГДЕ
	               |	ПродажиОбороты.Оплата В(&Оплата)
	               |	И ВЫБОР
	               |			КОГДА ПродажиОбороты.КоличествоОборот <> 0
	               |				ТОГДА ВЫБОР
	               |						КОГДА &ВидСравнения = ЗНАЧЕНИЕ(Перечисление.ВидыСравнения.Равно)
	               |							ТОГДА ПродажиОбороты.СтоимостьОборот / ПродажиОбороты.КоличествоОборот = &ЗначениеСравнения
	               |						КОГДА &ВидСравнения = ЗНАЧЕНИЕ(Перечисление.ВидыСравнения.НеРавно)
	               |							ТОГДА ПродажиОбороты.СтоимостьОборот / ПродажиОбороты.КоличествоОборот <> &ЗначениеСравнения
	               |						КОГДА &ВидСравнения = ЗНАЧЕНИЕ(Перечисление.ВидыСравнения.Меньше)
	               |							ТОГДА ПродажиОбороты.СтоимостьОборот / ПродажиОбороты.КоличествоОборот < &ЗначениеСравнения
	               |						КОГДА &ВидСравнения = ЗНАЧЕНИЕ(Перечисление.ВидыСравнения.МеньшеИлиРавно)
	               |							ТОГДА ПродажиОбороты.СтоимостьОборот / ПродажиОбороты.КоличествоОборот <= &ЗначениеСравнения
	               |						КОГДА &ВидСравнения = ЗНАЧЕНИЕ(Перечисление.ВидыСравнения.Больше)
	               |							ТОГДА ПродажиОбороты.СтоимостьОборот / ПродажиОбороты.КоличествоОборот > &ЗначениеСравнения
	               |						КОГДА &ВидСравнения = ЗНАЧЕНИЕ(Перечисление.ВидыСравнения.БольшеИлиРавно)
	               |							ТОГДА ПродажиОбороты.СтоимостьОборот / ПродажиОбороты.КоличествоОборот >= &ЗначениеСравнения
	               |					КОНЕЦ
	               |		КОНЕЦ
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ПродажиОбороты.Ресторан,
	               |	ПродажиОбороты.ПериодДень
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПродажиСебестоимость.Ресторан КАК Ресторан,
	               |	ПродажиСебестоимость.ПериодДень КАК День,
	               |	СУММА(ТранзакцииПосещения.КоличествоДокументов) КАК КоличествоДокументов,
	               |	СУММА(ЕСТЬNULL(ТранзакцииПосещения.КоличествоТранзакций, 0)) КАК КоличествоТранзакций,
	               |	СУММА(ЕСТЬNULL(ТранзакцииПосещения.КоличествоКлиентов, 0)) КАК КоличествоКлиентов,
	               |	СУММА(ВЫБОР
	               |			КОГДА ТранзакцииПосещения.КоличествоТранзакций = 0
	               |				ТОГДА 0
	               |			ИНАЧЕ ТранзакцииПосещения.КоличествоКлиентов / ТранзакцииПосещения.КоличествоТранзакций
	               |		КОНЕЦ) КАК КоличествоКлиентовСреднее,
	               |	СУММА(ПродажиСебестоимость.КоличествоБлюд) КАК КоличествоБлюд,
	               |	СУММА(ПродажиСебестоимость.АкцизныйНалог) КАК АкцизныйНалог,
	               |	СУММА(ПродажиСебестоимость.СуммаБезСкидок) КАК СуммаБезСкидок,
	               |	СУММА(ПродажиСебестоимость.СуммаНаценки) КАК Наценка,
	               |	СУММА(ПродажиСебестоимость.СуммаСкидки) КАК Скидка,
	               |	СУММА(ПродажиСебестоимость.Сумма) КАК Сумма,
	               |	СУММА(ПродажиСебестоимость.Себестоимость) КАК Себестоимость,
	               |	СУММА(ВЫБОР
	               |			КОГДА ТранзакцииПосещения.КоличествоКлиентов = 0
	               |				ТОГДА 0
	               |			ИНАЧЕ ПродажиСебестоимость.КоличествоБлюд / ТранзакцииПосещения.КоличествоКлиентов
	               |		КОНЕЦ) КАК КЦП,
	               |	СУММА(ВЫБОР
	               |			КОГДА ПродажиСебестоимость.КоличествоБлюд = 0
	               |				ТОГДА 0
	               |			ИНАЧЕ ПродажиСебестоимость.Сумма / ПродажиСебестоимость.КоличествоБлюд
	               |		КОНЕЦ) КАК Цена,
	               |	СУММА(ВЫБОР
	               |			КОГДА ТранзакцииПосещения.КоличествоКлиентов = 0
	               |				ТОГДА 0
	               |			ИНАЧЕ ПродажиСебестоимость.Сумма / ТранзакцииПосещения.КоличествоКлиентов
	               |		КОНЕЦ) КАК СреднийЧек,
	               |	СУММА(ВЫБОР
	               |			КОГДА ТранзакцииПосещения.КоличествоТранзакций = 0
	               |				ТОГДА 0
	               |			ИНАЧЕ ПродажиСебестоимость.Сумма / ТранзакцииПосещения.КоличествоТранзакций
	               |		КОНЕЦ) КАК СредняяТранзакция,
	               |	СУММА(ВЫБОР
	               |			КОГДА ПродажиСебестоимость.СуммаБезСкидок = 0
	               |				ТОГДА 0
	               |			ИНАЧЕ ПродажиСебестоимость.СуммаСкидки / ПродажиСебестоимость.СуммаБезСкидок * 100
	               |		КОНЕЦ) КАК СкидкаПроцент,
	               |	СУММА(ВЫБОР
	               |			КОГДА ПродажиСебестоимость.Сумма = 0
	               |				ТОГДА 0
	               |			ИНАЧЕ ПродажиСебестоимость.Себестоимость / ПродажиСебестоимость.Сумма * 100
	               |		КОНЕЦ) КАК СебестоимостьПроцент,
	               |	СУММА(ПродажиСебестоимость.Сумма - ПродажиСебестоимость.Себестоимость) КАК ВаловыйДоход
	               |ИЗ
	               |	ПродажиСебестоимость КАК ПродажиСебестоимость
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ТранзакцииПосещения КАК ТранзакцииПосещения
	               |		ПО ПродажиСебестоимость.Ресторан = ТранзакцииПосещения.Ресторан
	               |			И ПродажиСебестоимость.ПериодДень = ТранзакцииПосещения.ПериодДень
	               |ГДЕ
	               |	ПродажиСебестоимость.Ресторан В(&Ресторан)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ПродажиСебестоимость.Ресторан,
	               |	ПродажиСебестоимость.ПериодДень
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Ресторан,
	               |	День
	               |ИТОГИ ПО
	               |	Ресторан,
	               |	День";
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	
	Если ЗначениеЗаполнено(ОтборРесторан) Тогда
		Запрос.УстановитьПараметр("Ресторан", ОтборРесторан);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПродажиСебестоимость.Ресторан В(&Ресторан)", "ИСТИНА");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборОплата) Тогда
		Запрос.УстановитьПараметр("Оплата", ОтборОплата);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПродажиОбороты.Оплата В(&Оплата)", "ИСТИНА");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ВидСравнения", ВидСравнения);
	Запрос.УстановитьПараметр("ЗначениеСравнения", ЗначениеСравнения);
	
	Возврат Запрос.Выполнить();
				   
КонецФункции
