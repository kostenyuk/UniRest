////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	КлючВарианта = Параметры.КлючТекущихНастроек;
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	ОтчетИнформация = ВариантыОтчетов.СформироватьИнформациюОбОтчетеПоПолномуИмени(Параметры.КлючОбъекта);
	Если ТипЗнч(ОтчетИнформация.ТекстОшибки) = Тип("Строка") Тогда
		ВызватьИсключение ОтчетИнформация.ТекстОшибки;
	КонецЕсли;
	ОтчетИнформация.Удалить("ОтчетМетаданные");
	ОтчетИнформация.Удалить("ТекстОшибки");
	ОтчетИнформация.Вставить("ОтчетПолноеИмя", Параметры.КлючОбъекта);
	ОтчетИнформация = Новый ФиксированнаяСтруктура(ОтчетИнформация);
	
	ПолныеПраваНаВарианты = ВариантыОтчетов.ПолныеПраваНаВарианты();
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВариантыОтчетов.Ссылка,
	|	ВариантыОтчетов.Наименование,
	|	ВариантыОтчетов.КлючВарианта КАК КлючВарианта,
	|	ВЫБОР
	|		КОГДА ВариантыОтчетов.Пользовательский = ЛОЖЬ
	|			ТОГДА 1
	|		КОГДА ВариантыОтчетов.Автор = &ТекущийПользователь
	|			ТОГДА 2
	|		ИНАЧЕ 3
	|	КОНЕЦ КАК Группа,
	|	ВариантыОтчетов.Автор,
	|	ВариантыОтчетов.Описание
	|ИЗ
	|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
	|ГДЕ
	|	ВариантыОтчетов.Отчет = &Отчет
	|	И ВариантыОтчетов.ПометкаУдаления = ЛОЖЬ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Группа
	|ИТОГИ ПО
	|	Группа";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Отчет",               ОтчетИнформация.Отчет);
	Запрос.УстановитьПараметр("ТекущийПользователь", ТекущийПользователь);
	Запрос.Текст = ТекстЗапроса;
	
	Группы = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока Группы.Следующий() Цикл
		ГруппаДерева = ДеревоВариантовОтчета.ПолучитьЭлементы().Добавить();
		ЗаполнитьЗначенияСвойств(ГруппаДерева, Группы);
		Если Группы.Группа = 1 Тогда
			ГруппаДерева.Наименование = НСтр("ru = 'Предопределенные'");
		ИначеЕсли Группы.Группа = 2 Тогда
			ГруппаДерева.Наименование = НСтр("ru = 'Мои'");
		Иначе
			ГруппаДерева.Наименование = НСтр("ru = 'Общие'");
		КонецЕсли;
		
		ГруппаДерева.ИндексКартинки = 0;
		
		Варианты = Группы.Выбрать();
		Пока Варианты.Следующий() Цикл
			
			Вариант = ГруппаДерева.ПолучитьЭлементы().Добавить();
			ЗаполнитьЗначенияСвойств(Вариант, Варианты);
			Вариант.ИндексКартинки = -1;
			Если Вариант.КлючВарианта = КлючВарианта Тогда
				Элементы.ДеревоВариантовОтчета.ТекущаяСтрока = Вариант.ПолучитьИдентификатор();
			КонецЕсли;
			Вариант.АвторТекущийПользователь = (Вариант.Автор = ТекущийПользователь);
			Если НЕ ЗначениеЗаполнено(Вариант.Описание) Тогда
				Вариант.Описание = Вариант.Наименование;
			КонецЕсли;
			
		КонецЦикла;
	КонецЦикла;
	
	ДобавитьПредопределенныеВариантыВнешнегоОтчета();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ ДеревоВариантовОтчета

&НаКлиенте
Процедура ДеревоВариантовОтчетаПриАктивизацииСтроки(Элемент)
	Вариант = Элементы.ДеревоВариантовОтчета.ТекущиеДанные;
	Если Вариант = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Вариант.КлючВарианта) Тогда
		ВариантАвтор    = Неопределено;
		ВариантОписание = "";
	Иначе
		ВариантАвтор    = Вариант.Автор;
		ВариантОписание = Вариант.Описание;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВариантовОтчетаПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
	ОткрытьВариантДляИзменения();
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВариантовОтчетаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВариантовОтчетаПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВариантовОтчетаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ВыбратьИЗакрыть();
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВариантовОтчетаВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ВыбратьИЗакрыть();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура ВыбратьИЗакрыть()
	Вариант = Элементы.ДеревоВариантовОтчета.ТекущиеДанные;
	Если Вариант = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если ЗначениеЗаполнено(Вариант.КлючВарианта) Тогда
		Закрыть(Новый ВыборНастроек(Вариант.КлючВарианта));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВариантДляИзменения()
	Вариант = Элементы.ДеревоВариантовОтчета.ТекущиеДанные;
	Если Вариант = Неопределено ИЛИ НЕ ЗначениеЗаполнено(Вариант.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	Если НЕ ПравоИзмененияВарианта(Вариант, ПолныеПраваНаВарианты) Тогда
		Предупреждение(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Недостаточно прав доступа для изменения варианта ""%1"".'"),
				Вариант.Наименование
			)
		);
		Возврат;
	КонецЕсли;
	ОткрытьЗначение(Вариант.Ссылка);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПравоИзмененияВарианта(Вариант, ПолныеПраваНаВарианты)
	Возврат ПолныеПраваНаВарианты ИЛИ Вариант.АвторТекущийПользователь;
КонецФункции

&НаСервере
Процедура ДобавитьПредопределенныеВариантыВнешнегоОтчета()
	
	Если ОтчетИнформация.ТипОтчета <> Перечисления.ТипыОтчетов.Внешний Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		ОтчетОбъект = ВнешниеОтчеты.Создать(ОтчетИнформация.ОтчетИмя);
	Исключение
		ВариантыОтчетов.ОшибкаПоВарианту(Неопределено,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось получить список предопределенных вариантов
				|внешнего отчета ""%1"":'"),
				ОтчетИнформация.ОтчетИмя
			) + Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())
		);
		Возврат;
	КонецПопытки;
	
	Если ОтчетОбъект.СхемаКомпоновкиДанных = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ГруппаДерева = ДеревоВариантовОтчета.ПолучитьЭлементы().Добавить();
	ГруппаДерева.Группа = 1;
	ГруппаДерева.Наименование = НСтр("ru = 'Предопределенные'");
	
	Для Каждого ВариантНастроекКД Из ОтчетОбъект.СхемаКомпоновкиДанных.ВариантыНастроек Цикл
		Вариант = ГруппаДерева.ПолучитьЭлементы().Добавить();
		Вариант.Группа         = 3;
		Вариант.КлючВарианта   = ВариантНастроекКД.Имя;
		Вариант.Наименование   = ВариантНастроекКД.Представление;
		Вариант.ИндексКартинки = -1;
		Вариант.АвторТекущийПользователь = Ложь
	КонецЦикла;
	
КонецПроцедуры

