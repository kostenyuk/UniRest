
&НаКлиенте
Процедура РодительПриИзменении(Элемент)
	
	// Наследуемые реквизиты.
	РодительПриИзмененииСервер();

КонецПроцедуры // РодительПриИзменении()

&НаСервере
Процедура РодительПриИзмененииСервер()
	
	__РаботаСДаннымиКлиентСервер.ПолучитьНаследуемыеРеквизиты(Объект);

КонецПроцедуры // РодительПриИзмененииСервер()

&НаКлиенте
Процедура НоменклатураПриИзменении(Элемент)
	
	// Наименование.
	Объект.Наименование = __ОбщегоНазначенияКлиентСервер.ПолучитьНеПустоеНаименование(Объект.Номенклатура);
	
	// Изображения.
	НоменклатураПриИзмененииСервер();

КонецПроцедуры // НоменклатураПриИзменении()

&НаСервере
Процедура НоменклатураПриИзмененииСервер()
	
	Если ЗначениеЗаполнено(Объект.Номенклатура) Тогда
		ЗначениеВРеквизитФормы(Объект.Номенклатура.ПолучитьОбъект(), "Номенклатура");
	Иначе
		ЗначениеВРеквизитФормы(Новый (Тип("СправочникОбъект.Номенклатура")), "Номенклатура");
	КонецЕсли;
	
	ОбработкаОповещенияСервер();

КонецПроцедуры // НоменклатураПриИзмененииСервер()

&НаКлиенте
Процедура НоменклатураОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры // НоменклатураОчистка()


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Изображения.
	НоменклатураПриИзмененииСервер();

КонецПроцедуры // ПриСозданииНаСервере()

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// Связанные данные.
	__РаботаСДаннымиКлиентСервер.УстановитьСвязанныеДанные(
		Номенклатура.Ссылка, 
		Новый Структура("Изображение,Миниатюра,Иконка, Комментарий",
			Номенклатура.Изображение,
			Номенклатура.Миниатюра,
			Номенклатура.Иконка,
			Номенклатура.Комментарий),
		Отказ);
		
КонецПроцедуры // ПриЗаписиНаСервере()

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если __УправлениеДаннымиКлиентСервер.ОповещениеИзмененияЧерногоБелогоСписка(ИмяСобытия) Тогда
		ОбработкаОповещенияСервер();
	КонецЕсли; 
	
КонецПроцедуры // ОбработкаОповещения()

&НаСервере
Процедура ОбработкаОповещенияСервер()

	Элементы.БелыйСписокНоменклатуры.Картинка = __РаботаСДиалогамиКлиентСервер.ПолучитьКартинкуПометки(РегистрыСведений.БелыйСписокНоменклатуры.ПолучитьВхождениеНоменклатуры(Объект.Номенклатура));
	Элементы.ЧерныйСписокНоменклатуры.Картинка = __РаботаСДиалогамиКлиентСервер.ПолучитьКартинкуПометки(РегистрыСведений.ЧерныйСписокНоменклатуры.ПолучитьВхождениеНоменклатуры(Объект.Номенклатура));
	
КонецПроцедуры // ОбработкаОповещенияСервер()
