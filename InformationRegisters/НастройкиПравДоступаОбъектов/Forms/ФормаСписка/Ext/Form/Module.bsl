
&НаСервере
Функция УстановитьВыбор(Тип, Метаданные, Префикс)
	
	Возврат "КОГДА РегистрСведенийНастройкиПравДоступаОбъектов.ОбъектДоступа = ЗНАЧЕНИЕ(" + Тип + "." + Метаданные.Имя + ".ПустаяСсылка) ТОГДА """ +  Префикс + " «" + СтрЗаменить(Метаданные.Синоним, """", """""") + "»""";
	
КонецФункции // УстановитьВыбор()


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТестВыбора = "ВЫБОР";
	Для Каждого ЭлементМетаданных Из Метаданные.Справочники Цикл
		ТестВыбора = ТестВыбора + Символы.ПС + УстановитьВыбор("Справочник", ЭлементМетаданных, НСтр("ru = 'Справочник'; uk = 'Довідник'"));
	КонецЦикла; 
	Для Каждого ЭлементМетаданных Из Метаданные.Документы Цикл
		ТестВыбора = ТестВыбора + Символы.ПС + УстановитьВыбор("Документ", ЭлементМетаданных, НСтр("ru = 'Документ'; uk = 'Документ'"));
	КонецЦикла; 
	ТестВыбора = ТестВыбора + Символы.ПС + "ИНАЧЕ """ + НСтр("ru = 'Неопределено'; uk = 'Невизначено'") + """ КОНЕЦ";
	
	Список.ТекстЗапроса = СтрЗаменить(Список.ТекстЗапроса, "&Объект", ТестВыбора);
	
	Список.Параметры.УстановитьЗначениеПараметра("Константы", НСтр("ru = 'Настройки'; uk = 'Налаштування'"));
	Список.Параметры.УстановитьЗначениеПараметра("Пользователи", Метаданные.Справочники.Пользователи.Синоним);
	Список.Параметры.УстановитьЗначениеПараметра("ГруппыПользователей", Метаданные.Справочники.ГруппыПользователей.Синоним);
	Список.Параметры.УстановитьЗначениеПараметра("Неопределено", НСтр("ru = 'Неопределено'; uk = 'Невизначено'"));
	
	// Настройка формы.
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементУсловногоОформления.Использование = Истина;
	ЗаполнитьЗначенияСвойств(ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных")), Новый Структура("Использование,ЛевоеЗначение,ВидСравнения,ПравоеЗначение", Истина, Новый ПолеКомпоновкиДанных("Список.ВладелецПравДоступа"), ВидСравненияКомпоновкиДанных.Равно, Неопределено));
	ЗаполнитьЗначенияСвойств(ЭлементУсловногоОформления.Поля.Элементы.Добавить(), Новый Структура("Использование,Поле", Истина, Новый ПолеКомпоновкиДанных("ВладелецПравДоступа")));
	ЗаполнитьЗначенияСвойств(ЭлементУсловногоОформления.Оформление.Элементы.Найти("Текст"), Новый Структура("Использование,Значение", Истина, НСтр("ru = 'Настройки'; uk = 'Налаштування'")));
	
КонецПроцедуры // ПриСозданииНаСервере()
