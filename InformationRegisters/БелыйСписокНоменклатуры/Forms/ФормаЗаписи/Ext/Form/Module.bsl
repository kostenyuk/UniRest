
&НаКлиенте
Процедура КоличествоПриИзменении(Элемент)
	
	Если (ИмяФормы = "РегистрСведений.БелыйСписокНоменклатуры.Форма.ФормаУпрощенная") Тогда
		Актуальность = Истина;
	Иначе
		Элементы["Рестораны"].ТекущиеДанные.Актуальность = Элементы["Рестораны"].ТекущиеДанные.Количество;	
	КонецЕсли; 
	
КонецПроцедуры // КоличествоПриИзменении


&НаКлиенте
Процедура ОК(Команда)
	
	ОКСервер();
	Закрыть();
	
	__УправлениеДаннымиКлиент.ОповеститьИзменениеЧерногоБелогоСписка();

КонецПроцедуры // ОК()

&НаСервере
Процедура ОКСервер()

	ТаблицаВхождений = РеквизитФормыВЗначение("Рестораны");
	
	Если (ТаблицаВхождений.Количество() = 1) Тогда
		Если УчетКоличества И (ИмяФормы = "РегистрСведений.БелыйСписокНоменклатуры.Форма.ФормаУпрощенная") Тогда
			ТаблицаВхождений[0].Актуальность = Актуальность;
			ТаблицаВхождений[0].Количество = Запись.Количество;
		КонецЕсли; 
	КонецЕсли; 
	
    РегистрыСведений.БелыйСписокНоменклатуры.УстановитьТаблицуВхожденийНоменклатуры(Запись.Номенклатура, ТаблицаВхождений);
	
КонецПроцедуры // ОКСервер()


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Запись.Номенклатура) Тогда
		Запись.Номенклатура = Параметры.Номенклатура;
	КонецЕсли; 
	
	Отказ = Запись.Номенклатура.ЭтоГруппа; 
	
	Если Не Отказ Тогда
		УчетКоличества = ПолучитьПараметрыУчета().БелыйСписокНоменклатурыУчетКоличества;
	КонецЕсли;
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Не ЗначениеЗаполнено(Запись.Номенклатура) Тогда
		
		ПараметрыФормы = Новый Структура("РежимВыбора,Отбор", Истина, Новый Структура("ИспользуетсяУсловиями", Ложь));
		Запись.Номенклатура = ОткрытьФормуМодально("Справочник.Номенклатура.ФормаВыбора", ПараметрыФормы, ЭтаФорма);
		
		Отказ = Не ЗначениеЗаполнено(Запись.Номенклатура);
	КонецЕсли; 
	
	Если Не Отказ Тогда
		ПриОткрытииСервер(Отказ);
		Если Отказ Тогда
			__УправлениеДаннымиКлиент.ОповеститьИзменениеЧерногоБелогоСписка();
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры // ПриОткрытии()

&НаСервере
Процедура ПриОткрытииСервер(Отказ)
	
	ТаблицаВхождений = РегистрыСведений.БелыйСписокНоменклатуры.ПолучитьТаблицуВхожденийНоменклатуры(Запись.Номенклатура);
	
	Если (ТаблицаВхождений.Количество() = 1) Тогда
		Если УчетКоличества И (ИмяФормы = "РегистрСведений.БелыйСписокНоменклатуры.Форма.ФормаУпрощенная") Тогда
			Актуальность = Не ТаблицаВхождений[0].Актуальность;
			Запись.Количество = ТаблицаВхождений[0].Количество;
		Иначе
			ТаблицаВхождений[0].Актуальность = Не ТаблицаВхождений[0].Актуальность;
		    РегистрыСведений.БелыйСписокНоменклатуры.УстановитьТаблицуВхожденийНоменклатуры(Запись.Номенклатура, ТаблицаВхождений);
			Отказ = Истина;
		КонецЕсли; 
	КонецЕсли; 
	
	Если Не Отказ Тогда
		ЗначениеВРеквизитФормы(ТаблицаВхождений, "Рестораны");
	КонецЕсли; 
	
КонецПроцедуры // ПриОткрытииСервер()
