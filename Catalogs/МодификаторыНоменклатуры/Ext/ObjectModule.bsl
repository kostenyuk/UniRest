
Перем мИзмененРодитель;

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	// При обмене данными ничего не проверяем.
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоГруппа Тогда
		__ОбщегоНазначенияСервер.ИзменитьПроверяемыеРеквизиты(ПроверяемыеРеквизиты, , "Номенклатура");
	Иначе
		__ОбщегоНазначенияСервер.ИзменитьПроверяемыеРеквизиты(ПроверяемыеРеквизиты, , "Наименование");
		Наименование = Номенклатура;
	КонецЕсли; 
	
КонецПроцедуры // ОбработкаПроверкиЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	
	Если Не ЭтоГруппа Тогда
		Наименование = Неопределено;
		Номенклатура = Неопределено;
		Актуальность = Истина;
	КонецЕсли;
	
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ)
	
	// При обмене данными ничего не проверяем.
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	мИзмененРодитель = НЕ ЭтоНовый() И Родитель<>Ссылка.Родитель;
	
	// Актуальность.
	Если мИзмененРодитель
		И Не Родитель.Пустая() Тогда
		Актуальность = Родитель.Актуальность;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	// При обмене данными ничего не проверяем.
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Номенклатура меню.
	Если Не Отказ Тогда
		Если Не ЭтоГруппа Тогда
			Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ ПЕРВЫЕ 1
			|	НоменклатураМеню.Номенклатура
			|ИЗ
			|	РегистрСведений.НоменклатураМеню КАК НоменклатураМеню
			|ГДЕ
			|	НоменклатураМеню.Номенклатура = &Номенклатура");
			Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
			Если Запрос.Выполнить().Пустой() Тогда
				Отказ = Не Обработки.ОбновлениеМеню.Создать().ДобавитьОбновитьНоменклатуруМеню(Номенклатура);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если мИзмененРодитель И НЕ Отказ Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	МодификаторыНоменклатуры.Номенклатура
		|ИЗ
		|	РегистрСведений.МодификаторыНоменклатуры КАК МодификаторыНоменклатуры
		|ГДЕ
		|	МодификаторыНоменклатуры.Модификатор В ИЕРАРХИИ(&Ссылка)"
		);
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		ВыборкаПоНоменклатуре = Запрос.Выполнить().Выбрать();
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	МодификаторыНоменклатуры.Модификатор
		|ИЗ
		|	РегистрСведений.МодификаторыНоменклатуры КАК МодификаторыНоменклатуры
		|ГДЕ
		|	МодификаторыНоменклатуры.Номенклатура = &Номенклатура
		|	И МодификаторыНоменклатуры.Модификатор В ИЕРАРХИИ(&Ссылка)"
		);
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		Пока ВыборкаПоНоменклатуре.Следующий() Цикл
			
			АктуальностьРодителяЭлемента = Справочники.Номенклатура.ИспользуетсяМодификатор(ВыборкаПоНоменклатуре.Номенклатура,Родитель);
			Если Не АктуальностьРодителяЭлемента Тогда
				Запрос.УстановитьПараметр("Номенклатура",ВыборкаПоНоменклатуре.Номенклатура);
				ВыборкаЗаписей = Запрос.Выполнить().Выбрать();
				Пока ВыборкаЗаписей.Следующий() Цикл
					НаборЗаписей = РегистрыСведений.МодификаторыНоменклатуры.СоздатьНаборЗаписей();
					НаборЗаписей.Отбор.Модификатор.Установить(Ссылка);
					НаборЗаписей.Записать();
				КонецЦикла;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли; 
		
КонецПроцедуры

