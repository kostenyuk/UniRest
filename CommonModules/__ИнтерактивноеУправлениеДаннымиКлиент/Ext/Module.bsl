

Процедура АктуальностьСправочникаПриАктивизацииЯчейки(Элемент) Экспорт

	Форма = __РаботаСДиалогамиКлиентСервер.ПолучитьРодительскуюФорму(Элемент); Элементы = Форма.Элементы;
	
	Если (Элемент.ТекущийЭлемент = Элементы.ИндексАктуальности) Тогда
		Если ЗначениеЗаполнено(Форма["ТекущийЭлементСписка"]) Тогда
			Элемент.ТекущийЭлемент = Элементы[Форма["ТекущийЭлементСписка"]];
		Иначе
			Элемент.ТекущийЭлемент = Элементы.Наименование;
		КонецЕсли;
		
		Если Не Форма["ПредотвратитьУстановкуАктуальности"] Тогда
			Если Не __РаботаСДиалогамиКлиентСервер.СтрокаГруппировкиДинамическогоСписка(Элемент) Тогда
			
				__РаботаСДаннымиКлиентСервер.УстановитьАктуальность(Элемент.ТекущаяСтрока);
				Элемент.Обновить();
				
			КонецЕсли; 
		КонецЕсли; 

		Форма["ПредотвратитьУстановкуАктуальности"] = Ложь;
	КонецЕсли; 
	
	Если (Элемент.ТекущийЭлемент = Неопределено) Тогда
		Форма["ТекущийЭлементСписка"] = Неопределено;
	Иначе
		Форма["ТекущийЭлементСписка"] = Элемент.ТекущийЭлемент.Имя;
	КонецЕсли;
	
КонецПроцедуры	// АктуальностьСправочникаПриАктивизацииЯчейки()


Процедура ПереносМенюВыборЗначения(Список, Значение, СтандартнаяОбработка, Справочник = "Меню", Данные = Неопределено) Экспорт

	ПереносМенюПеретаскивание(Список, Новый Структура("Значение", Значение), СтандартнаяОбработка, Неопределено, Неопределено, Справочник, Данные);
	
КонецПроцедуры // ПереносМенюВыборЗначения()

Процедура ПереносМенюПеретаскивание(Список, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле, Справочник = "Меню", Данные = Неопределено, Распределение = Ложь) Экспорт

	Если ЗначениеЗаполнено(ПараметрыПеретаскивания.Значение) И (ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("СправочникСсылка.Номенклатура")) И (Не __РаботаСДиалогамиКлиентСервер.СтрокаГруппировкиДинамическогоСписка(Строка)) Тогда
		СтандартнаяОбработка = Ложь;
		Если Не __РаботаСДиалогамиКлиентСервер.СтрокаГруппировкиДинамическогоСписка(Строка) Тогда
			Если (Справочник = "Меню") И (Не ЗначениеЗаполнено(Строка)) И (Не ЗначениеЗаполнено(__РаботаСДиалогамиКлиентСервер.ПолучитьВладелецаСписка(Данные))) Тогда
				
				__ИнтерактивноеУправлениеДаннымиКлиент.ПредупреждениеКомандаНеМожетБытьВыполнена();
				
			Иначе
				
				Если ЗначениеЗаполнено(Строка) Тогда
					Параметры = __ОбщегоНазначенияСервер.ПолучитьЗначение(Строка, "Владелец,Родитель,ЭтоГруппа");
				Иначе
					Если (Справочник = "Меню") Тогда 
						ТекущийВладелец = __РаботаСДиалогамиКлиентСервер.ПолучитьВладелецаСписка(Данные);
					КонецЕсли;	
					
					Параметры = Новый Структура("Владелец,Родитель,ЭтоГруппа", 
						ТекущийВладелец,
						Список.ТекущийРодитель,
						Ложь);
				КонецЕсли;
				
				Если Параметры.ЭтоГруппа Тогда
					Параметры.Родитель = Строка;
				КонецЕсли; 
				
				Если __ОбщегоНазначенияСервер.ПолучитьЗначение(ПараметрыПеретаскивания.Значение, "ЭтоГруппа") Тогда
					ОткрытьФормуМодально("Справочник.Номенклатура.Форма.ФормаПараметровПереносаГруппы", 
						Новый Структура("Справочник,Номенклатура, Владелец,Родитель, Распределение", 
							Справочник, 
							ПараметрыПеретаскивания.Значение, 
							Параметры.Владелец,
							Параметры.Родитель,
							Распределение), 
						Список);
				Иначе
					ОткрытьФормуМодально("Справочник.Номенклатура.Форма.ФормаПараметровПереносаЭлемента", 
						Новый Структура("Справочник,Номенклатура, Владелец,Родитель, Распределение", 
							Справочник, 
							ПараметрыПеретаскивания.Значение, 
							Параметры.Владелец,
							Параметры.Родитель,
							Распределение), 
						Список);
				КонецЕсли;
				
				Если (ТипЗнч(Список) = Тип("ТаблицаФормы")) Тогда
					Список.Обновить();
				КонецЕсли;
				
				__УправлениеДаннымиКлиент.ОповеститьИзменениеРаспределениеНоменклатуры(Справочник);
				
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры // ПереносМенюПеретаскивание()

Процедура ПереносМенюПроверкаПеретаскивания(Список, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле, Справочник = "Меню", Данные = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(ПараметрыПеретаскивания.Значение) И (ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("СправочникСсылка.Номенклатура")) Тогда
		СтандартнаяОбработка = Ложь;
		Если Не __РаботаСДиалогамиКлиентСервер.СтрокаГруппировкиДинамическогоСписка(Строка) Тогда
			Если (Не Справочник = "Меню") Или ЗначениеЗаполнено(Строка) Или ЗначениеЗаполнено(__РаботаСДиалогамиКлиентСервер.ПолучитьВладелецаСписка(Данные)) Тогда
				ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Копирование;
			Иначе
				ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
			КонецЕсли; 
		Иначе
			ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
		КонецЕсли; 
	КонецЕсли; 

КонецПроцедуры // ПереносМенюПроверкаПеретаскивания()


Процедура ПредупреждениеКомандаНеМожетБытьВыполнена() Экспорт

	Предупреждение(НСтр("ru = 'Команда не может быть выполнена для указанного объекта!'; uk = 'Команда не може бути виконана для зазначеного об''єкта!'"));
	
КонецПроцедуры	// ПредупреждениеКомандаНеМожетБытьВыполнена()
