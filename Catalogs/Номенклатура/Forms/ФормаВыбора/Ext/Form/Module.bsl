
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Список.Параметры.УстановитьЗначениеПараметра("Период", ТекущаяДата());
	
	Если ЗначениеЗаполнено(Параметры.СписокТипыНоменклатуры) тогда
		ПараметрКомпоновкиДанных = Новый ПараметрКомпоновкиДанных("СписокТипыНоменклатуры");
		ЗначениеПараметраКомпоновкиДанных = Список.Параметры.НайтиЗначениеПараметра(ПараметрКомпоновкиДанных);
		Если ЗначениеПараметраКомпоновкиДанных = Неопределено Тогда
			Возврат;
		КонецЕсли; 
		ЗначениеПараметраКомпоновкиДанных.Значение =  Параметры.СписокТипыНоменклатуры;
		ЗначениеПараметраКомпоновкиДанных.Использование = Истина;		
		//Список.Параметры.УстановитьЗначениеПараметра("СписокТипыНоменклатуры", Параметры.СписокТипыНоменклатуры);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ТекущаяСтрока) Тогда
		Элементы.Список.ТекущаяСтрока = Параметры.ТекущаяСтрока; 
	КонецЕсли;
	
КонецПроцедуры

//////////////////////// Панель////////////////////////
&НаКлиенте
Процедура УстановитьПометку(Команда) 
	
	Для Каждого Кнопка Из Элементы.ПанельСБуквами.ПодчиненныеЭлементы Цикл
		Кнопка.Пометка = (Команда.Имя = Кнопка.Имя);
	КонецЦикла;
	
КонецПроцедуры // УстановитьПометку()

&НаКлиенте
Процедура УстановитьФильтрПоПервойБукве(Команда)
	
	Буква = Команда.Имя;
	
	СписокБукв = Новый Массив;
	СписокБукв.Добавить(Буква);
	СписокБукв.Добавить(НРег(Буква));
	
	Если Буква = "Е" Тогда
		СписокБукв.Добавить("Ё");
		СписокБукв.Добавить("ё");
	КонецЕсли;
	
	Если Буква = "И" Тогда
		СписокБукв.Добавить("Й");
		СписокБукв.Добавить("й");
	КонецЕсли;
	
	Список.Параметры.УстановитьЗначениеПараметра("СписокБукв", СписокБукв);
	
	УстановитьПометку(Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьФильтрПоПервойБукве(Команда )
	
	ПараметрКомпоновкиДанных = Новый ПараметрКомпоновкиДанных("СписокБукв");
	ЗначениеПараметраКомпоновкиДанных = Список.Параметры.НайтиЗначениеПараметра(ПараметрКомпоновкиДанных);
	Если ЗначениеПараметраКомпоновкиДанных = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗначениеПараметраКомпоновкиДанных.Использование = Ложь;
	
	УстановитьПометку(Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПоБуквам(Команда)
	ПараметрКомпоновкиДанных = Новый ПараметрКомпоновкиДанных("СписокБукв");
	ЗначениеПараметраКомпоновкиДанных = Список.Параметры.НайтиЗначениеПараметра(ПараметрКомпоновкиДанных);
	Если ЗначениеПараметраКомпоновкиДанных = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.ПанельСБуквами <> Неопределено Тогда
		Если Элементы.ФормаОтборПоБуквам.Пометка Тогда
			ЗначениеПараметраКомпоновкиДанных.Использование = Ложь;
			Элементы.ФормаОтборПоБуквам.Пометка = Ложь;
			Элементы.ПанельСБуквами.Видимость = Ложь;
			Элементы.Список.Отображение = ОтображениеТаблицы.ИерархическийСписок;		
		Иначе 
			Элементы.ФормаОтборПоБуквам.Пометка = Истина;
			Элементы.ПанельСБуквами.Видимость = Истина;
			Элементы.Список.Отображение = ОтображениеТаблицы.Список;
			ЗначениеПараметраКомпоновкиДанных.Использование = Истина;
		КонецЕсли; 
	КонецЕсли;  
КонецПроцедуры

