
Функция ПолучитьНормальнуюСтроку(Строка) Экспорт

	Результат = СокрЛП(Строка);
	Пока Найти(Результат, "  ") Цикл
		Результат = СтрЗаменить(Результат, "  ", " ");
	КонецЦикла;
	
	Возврат Результат;

КонецФункции // ПолучитьНормальнуюСтроку()


Процедура НаименованиеПриИзменении(Наименование, АвтокорректировкаНаименованийКонтрагентов = Ложь) Экспорт

	// Двойные пробелы.
	Наименование = СокрЛП(Наименование);
	Пока Найти(Наименование, "  ") Цикл
		Наименование = СтрЗаменить(Наименование, "  ", " ");
	КонецЦикла;
	
	Если АвтокорректировкаНаименованийКонтрагентов Тогда
		
		// Титульный вид.
		Наименование = ТРег(Наименование);
		
	КонецЕсли;

КонецПроцедуры // НаименованиеПриИзменении()


// REFACTORING +
//	Перенести процеудры в модуль управления контактной информацией.

Функция ПолучитьПустуюСтруктуруТелефона() Экспорт
	
	Возврат Новый Структура("Тип,Представление,Наименование,Поиск,Поле1,Поле2,Поле3,Поле4,Поле5,Поле6,Поле7,Поле8,Поле9,Поле10,Поле11,Комментарий,Объект,Вид", ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Телефон"),"","","","","","","","","","","","","","");

КонецФункции // ПолучитьПустуюСтруктуруТелефона()


Функция ПроверкаПустойСтроки(ВыбСтрока, ПризнакЗапятой = Истина)
	
	Если ПустаяСтрока(ВыбСтрока) Тогда
		Возврат "";
	Иначе
		Если (ТипЗнч(ПризнакЗапятой) = Тип("Булево")) тогда
			Если ПризнакЗапятой Тогда
				Возврат ", ";
			Иначе
				Возврат " ";
			КонецЕсли;
		Иначе
			Возврат Строка(ПризнакЗапятой);
		КонецЕсли;
	КонецЕсли; 
	
КонецФункции // ПроверкаПустойСтроки()

Функция ПолучитьТолькоЦифры(ПроизвольнаяСтрока, КоличествоЦифр = Неопределено)
	
	ТолькоЦифры = "";
	Для Индекс = 1 По СтрДлина(ПроизвольнаяСтрока) Цикл
		Если СтрЧислоВхождений("1234567890", Сред(ПроизвольнаяСтрока, Индекс, 1)) Тогда
			ТолькоЦифры = ТолькоЦифры + Сред(ПроизвольнаяСтрока, Индекс, 1);
		КонецЕсли;
	КонецЦикла;
	
	КоличествоЦифр = СтрДлина(ТолькоЦифры);
	Возврат ТолькоЦифры;
	
КонецФункции // ПолучитьТолькоЦифры()

Функция ПолучитьПервоеВхождениеИзНабораСимволов(Строка, Символы)
	
	Для Индекс = 1 По СтрДлина(Строка) Цикл
		Если СтрЧислоВхождений(Символы, Сред(Строка, Индекс, 1)) Тогда
			Возврат Индекс;
		КонецЕсли;
	КонецЦикла;
	
	Возврат 0;
	
КонецФункции // ПолучитьПервоеВхождение()


Функция ПривестиКодСтраныНомераТелефонаКШаблону(КодСтраныНомерТЛФ)
	
	Результат = ПолучитьТолькоЦифры(КодСтраныНомерТЛФ);
	Пока (Лев(Результат, 1) = "0") Цикл
		Результат = Сред(Результат, 2);
	КонецЦикла;
	Результат = Лев(Результат, 3);

	Возврат ПроверкаПустойСтроки(Результат, "+") + Результат;
	
КонецФункции // ПривестиКодСтраныНомераТелефонаКШаблону()

Функция ПривестиКодГородаНомераТелефонаКШаблону(КодГородаНомерТЛФ)
	
	Результат = ПолучитьТолькоЦифры(КодГородаНомерТЛФ);
	Пока (Лев(Результат, 1) = "0") Цикл
		Результат = Сред(Результат, 2);
	КонецЦикла;
	Результат = Лев(Результат, 4);

	Возврат Результат;
	
КонецФункции // ПривестиКодГородаНомераТелефонаКШаблону()

Функция ПривестиНомерТелефонаКШаблону(НомерТЛФ)
	
	Перем КоличествоЦифрНомера;
	
	ТолькоЦифрыНомера = Лев(ПолучитьТолькоЦифры(НомерТЛФ, КоличествоЦифрНомера), 7); КоличествоЦифрНомера = Мин(КоличествоЦифрНомера, 7);
	
	Если Не Булево(КоличествоЦифрНомера) Тогда
		Возврат ТолькоЦифрыНомера;
	КонецЕсли;
	
	СтруктураШаблонов = УправлениеКонтактнойИнформациейПовторно.ПолучитьШаблоныТелефонныхНомеров();
	
	ПолученныйШаблон = СтруктураШаблонов.Получить(КоличествоЦифрНомера);
	Если (ПолученныйШаблон = Неопределено) Тогда
		Возврат ТолькоЦифрыНомера;
	КонецЕсли;
	
	ПриведенныйНомер = "";
	НомерЦифры = 0;
	
	Для Индекс = 1 По СтрДлина(ПолученныйШаблон) Цикл
		Если (Сред(ПолученныйШаблон, Индекс, 1) = "9") Тогда
			НомерЦифры = НомерЦифры + 1;
			ПриведенныйНомер = ПриведенныйНомер + Сред(ТолькоЦифрыНомера, НомерЦифры, 1);
		Иначе
			ПриведенныйНомер = ПриведенныйНомер + Сред(ПолученныйШаблон, Индекс, 1);
		КонецЕсли;
	КонецЦикла; 

	Возврат ПриведенныйНомер;
	
КонецФункции // ПривестиНомерТелефонаКШаблону()

Процедура СформироватьПредставлениеТелефона(Запись)

	Представление = ПолучитьПредставлениеТелефона(Запись);
	Поиск = ПолучитьПоискТелефона(Запись);
	
	ЗаполнитьЗначенияСвойств(Запись, Новый Структура(
		"Представление,Наименование,Поиск",
		Представление,
		Представление,
		Поиск));

КонецПроцедуры // СформироватьПредставлениеТелефона()


Функция РазложитьТелефонПоПолям(НомерТелефона, Запись = Неопределено) Экспорт

	Перем КоличествоЦифр;
	
	// Только допустимые символы, без двойных разделителей "- */=", с учетом мест и количеств вхождений символов "+()".
	ТолькоДопустимые = "";
	Для Индекс = 1 По СтрДлина(НомерТелефона) Цикл
		Символ = Сред(НомерТелефона, Индекс, 1);
		Если Не Булево(СтрЧислоВхождений("1234567890+()- */=", Символ)) Тогда
			Продолжить;
		КонецЕсли;
		Если (Символ = "+") И Булево(СтрДлина(ТолькоДопустимые)) Тогда
			Продолжить;
		КонецЕсли;
		Если (Символ = "(") И Булево(Найти(ТолькоДопустимые, "(")) Тогда
			Продолжить;
		КонецЕсли;
		Если (Символ = ")") И (Булево(Найти(ТолькоДопустимые, ")")) Или (Не Булево(Найти(ТолькоДопустимые, "(")))) Тогда
			Продолжить;
		КонецЕсли;
		Если Булево(СтрЧислоВхождений("- */=", Символ)) И Булево(СтрЧислоВхождений("+()- */=", Прав(ТолькоДопустимые, 1))) Тогда
			Продолжить;
		КонецЕсли;
		ТолькоДопустимые = ТолькоДопустимые + Символ;
	КонецЦикла;
	Если Булево(Найти(ТолькоДопустимые, "(")) И (Не Булево(Найти(ТолькоДопустимые, ")"))) Тогда 
		ТолькоДопустимые = СтрЗаменить(ТолькоДопустимые, "(", "");
	КонецЕсли;
	
	// Код страны. В случаях: +380..., +xxx?... , +...(...)... , ...(...)...
	БылКодСтраны = (Лев(ТолькоДопустимые, 1) = "+") Или Булево(Найти(ТолькоДопустимые, "("));
	Если БылКодСтраны Тогда
		Если Булево(Найти(ТолькоДопустимые, "(")) Тогда
			ДлинаКодаСтраны = Найти(ТолькоДопустимые, "(") - 1; // Для: +...(...)... , ...(...)...
		Иначе
			ДлинаКодаСтраны = ПолучитьПервоеВхождениеИзНабораСимволов(ТолькоДопустимые, "- */=");
			Если Булево(ДлинаКодаСтраны) Тогда
				Если (ДлинаКодаСтраны > 5) Тогда  // Для: +xxx?...
					ДлинаКодаСтраны = 0;
				КонецЕсли;
			Иначе
				Если (ТолькоДопустимые = "+380") Тогда  // Для: +380...
					ДлинаКодаСтраны = 4;
				Иначе
					ДлинаКодаСтраны = 0;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		КодСтраны = ПолучитьТолькоЦифры(ПривестиКодСтраныНомераТелефонаКШаблону(ПолучитьТолькоЦифры(Лев(ТолькоДопустимые, ДлинаКодаСтраны))));
		ТолькоДопустимые = Сред(ТолькоДопустимые, ДлинаКодаСтраны + 1);
		БылКодСтраны = Булево(ДлинаКодаСтраны);
	КонецЕсли;
	
	// Код города. В случаях: +380xx..., +xxx?xxxx?... , +...(...)... , ...(...)... , (...)...
	БылКодГорода = БылКодСтраны Или (Лев(ТолькоДопустимые, 1) = "(");
	Если БылКодГорода Тогда
		Если (Лев(ТолькоДопустимые, 1) = "(") Тогда
			ДлинаКодаГорода = Найти(ТолькоДопустимые, ")"); // Для: +...(...)... , ...(...)... , (...)...
		Иначе
			ДлинаКодаГорода = Макс(ПолучитьПервоеВхождениеИзНабораСимволов(ТолькоДопустимые, "- */=") - 1, 0);
			Если Булево(ДлинаКодаГорода) Тогда
				Если (ДлинаКодаГорода > 5) Тогда  // Для: +xxx?xxxx?...
					ДлинаКодаГорода = 0;
				КонецЕсли;
			Иначе
				ДлинаКодаГорода = 2; // Для: +380xx...
			КонецЕсли;
		КонецЕсли;
		КодГорода = ПривестиКодГородаНомераТелефонаКШаблону(ПолучитьТолькоЦифры(Лев(ТолькоДопустимые, ДлинаКодаГорода)));
		ТолькоДопустимые = Сред(ТолькоДопустимые, ДлинаКодаГорода + 1);
		ДлинаКодаГорода = Булево(ДлинаКодаСтраны);
	КонецЕсли;
	
	// Телефон. В случаях: в международном формате, в региональном формате
	Если БылКодГорода Тогда
		МаксимальнаяДлина = 7;
	Иначе
		Если БылКодСтраны Тогда
			МаксимальнаяДлина = (12 + 1) - Макс(СтрДлина(КодСтраны), 2); // +1 - для поправик на плюс перед кодом страны
		Иначе
			МаксимальнаяДлина = (12);
		КонецЕсли;
	КонецЕсли;
	ТолькоДопустимые = Лев(ПолучитьТолькоЦифры(ТолькоДопустимые), МаксимальнаяДлина);
	СамТелефон = ПривестиНомерТелефонаКШаблону(Прав(ТолькоДопустимые, 7));
	Если Не БылКодГорода Тогда
		ТолькоДопустимые = Лев(ТолькоДопустимые, СтрДлина(ТолькоДопустимые) - 7);
		Если БылКодСтраны Тогда
			КодГорода = ПривестиКодГородаНомераТелефонаКШаблону(ТолькоДопустимые);
		Иначе
			КодГорода = ПривестиКодГородаНомераТелефонаКШаблону(Прав(ТолькоДопустимые, 2));
			КодСтраны = ПолучитьТолькоЦифры(ПривестиКодСтраныНомераТелефонаКШаблону(Лев(ТолькоДопустимые, СтрДлина(ТолькоДопустимые) - СтрДлина(КодГорода))));
		КонецЕсли;
	КонецЕсли;
	
	КодСтраны = ПривестиКодСтраныНомераТелефонаКШаблону(КодСтраны);
	
	// Корректировка кода страны для Украины.
	Если (КодСтраны = "+80") Тогда
		КодСтраны = "+380";
	КонецЕсли;
			
	СтруктураПолейТелефона = Новый Структура("КодСтраны,КодГорода,СамТелефон", КодСтраны, КодГорода, СамТелефон);
	
	Если (Не Запись = Неопределено) Тогда
		Запись.Поле1 = СтруктураПолейТелефона.КодСтраны;
		Запись.Поле2 = СтруктураПолейТелефона.КодГорода;
		Запись.Поле3 = СтруктураПолейТелефона.СамТелефон;
		Запись.Поле4 = "";
		СформироватьПредставлениеТелефона(Запись);
	КонецЕсли;
	
	Возврат СтруктураПолейТелефона;

КонецФункции

Функция ПолучитьПредставлениеТелефона(Запись)

	Представление = Запись.Поле3;
	Если Не ПустаяСтрока(Запись.Поле3) Тогда
		Представление = ПроверкаПустойСтроки(Запись.Поле2, "(") + Запись.Поле2 + ПроверкаПустойСтроки(Запись.Поле2, ") ") + Представление;
		Если Не ПустаяСтрока(Запись.Поле2) Тогда
			Представление = Запись.Поле1 + ПроверкаПустойСтроки(Запись.Поле1, " ") + Представление;
		Иначе
			Представление = Запись.Поле1 + ПроверкаПустойСтроки(Запись.Поле1, Истина) + Представление;
		КонецЕсли;
		Представление = Представление + ПроверкаПустойСтроки(Запись.Поле4, ", доб. ") + Запись.Поле4;
	КонецЕсли;
	
	Возврат Представление;

КонецФункции // ПолучитьПредставлениеТелефона()

Функция ПолучитьПоискТелефона(Запись)

	Поиск = ПолучитьТолькоЦифры(Запись.Поле1) + ПолучитьТолькоЦифры(Запись.Поле2) + ПолучитьТолькоЦифры(Запись.Поле3);
	
	Возврат Поиск;

КонецФункции // ПолучитьПоискТелефона()

// REFACTORING -
