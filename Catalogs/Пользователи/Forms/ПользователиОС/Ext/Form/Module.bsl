
&НаКлиенте
Процедура ЗаполнитьСписокДоменов()
	
	СписокДоменов.Очистить();
	
	Для Каждого Запись Из ТаблицаДоменовИПользователей Цикл
		Домен = СписокДоменов.Добавить();
		Домен.ИмяДомена = Запись.ИмяДомена;
	КонецЦикла;
	
	СписокДоменов.Сортировать("ИмяДомена");
	
КонецПроцедуры // ЗаполнитьСписокДоменов()

&НаКлиенте
Процедура СкомпоноватьРезультатИЗакрытьФорму()
	
	ИмяДомена = Элементы.ТаблицаДоменов.ТекущиеДанные.ИмяДомена;
	ИмяПользователя = Элементы.ТаблицаПользователейДомена.ТекущиеДанные.ИмяПользователя;
	СтрокаПользователяWindows = "\\" + ИмяДомена + "\" + ИмяПользователя;
	Закрыть(СтрокаПользователяWindows);
	
КонецПроцедуры // СкомпоноватьРезультатИЗакрытьФорму()


&НаКлиенте
Процедура Выбрать(Команда)
	
	Если Элементы.ТаблицаДоменов.ТекущиеДанные = Неопределено Тогда
		Предупреждение(НСтр("ru = 'Выберите домен.'; uk = 'Оберіть домен.'"));
		Возврат;
	КонецЕсли;
	ИмяДомена = Элементы.ТаблицаДоменов.ТекущиеДанные.ИмяДомена;
	
	Если Элементы.ТаблицаПользователейДомена.ТекущиеДанные = Неопределено Тогда
		Предупреждение(НСтр("ru = 'Выберите пользователя домена.'; uk = 'Оберіть користувача домену.'"));
		Возврат;
	КонецЕсли;
	ИмяПользователя = Элементы.ТаблицаПользователейДомена.ТекущиеДанные.ИмяПользователя;
	
	СкомпоноватьРезультатИЗакрытьФорму();
	
КонецПроцедуры // Выбрать()


&НаКлиенте
Процедура ТаблицаДоменовПриАктивизацииСтроки(Элемент)
	
	СписокПользователейТекущегоДомена.Очистить();
	
	Если Не Элемент.ТекущиеДанные = Неопределено Тогда
		ИмяДомена = Элемент.ТекущиеДанные.ИмяДомена;
		Для Каждого Запись Из ТаблицаДоменовИПользователей Цикл
			Если Запись.ИмяДомена = ИмяДомена Тогда
				Для Каждого Пользователь Из Запись.Пользователи Цикл
					ПользовательДомена = СписокПользователейТекущегоДомена.Добавить();
					ПользовательДомена.ИмяПользователя = Пользователь;
				КонецЦикла;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		СписокПользователейТекущегоДомена.Сортировать("ИмяПользователя");
	КонецЕсли;
	
КонецПроцедуры // ТаблицаДоменовПриАктивизацииСтроки()

&НаКлиенте
Процедура ТаблицаПользователейДоменаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СкомпоноватьРезультатИЗакрытьФорму();
	
КонецПроцедуры // ТаблицаПользователейДоменаВыбор()


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	#Если ТолстыйКлиентОбычноеПриложение Или ТолстыйКлиентУправляемоеПриложение Тогда
	ТаблицаДоменовИПользователей = ПользователиОС();
	#ИначеЕсли ТонкийКлиент Тогда
	ТаблицаДоменовИПользователей = Новый ФиксированныйМассив(ПользователиОС());
	#КонецЕсли
	
	ЗаполнитьСписокДоменов();
	
КонецПроцедуры // ПриОткрытии()
