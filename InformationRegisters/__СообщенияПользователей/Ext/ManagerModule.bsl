
Функция ПолучитьПолучателей() Экспорт
	
	// Привилегированный режим.
	УстановитьПривилегированныйРежим(Истина);
	
	// Выборка данных.
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ВложенныйЗапрос.Тип КАК Тип,
	                      |	ВложенныйЗапрос.Получатель КАК Получатель,
	                      |	ВложенныйЗапрос.ПолучательДокумент,
	                      |	ВложенныйЗапрос.Представление,
	                      |	ВложенныйЗапрос.Комментарий
	                      |ИЗ
	                      |	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	                      |		&ТипПользователи КАК Тип,
	                      |		ГруппыПользователей.Пользователь КАК Получатель,
	                      |		ПРЕДСТАВЛЕНИЕССЫЛКИ(ГруппыПользователей.Пользователь) КАК Представление,
	                      |		&ПустаяСтрока КАК Комментарий,
	                      |		ЛОЖЬ КАК ПолучательДокумент
	                      |	ИЗ
	                      |		РегистрСведений.ГруппыПользователей КАК ГруппыПользователей
	                      |	ГДЕ
	                      |		(НЕ ГруппыПользователей.Пароль = &ПустаяСтрока)
	                      |	
	                      |	ОБЪЕДИНИТЬ ВСЕ
	                      |	
	                      |	ВЫБРАТЬ РАЗЛИЧНЫЕ
	                      |		&ТипГруппыПользователей,
	                      |		ГруппыПользователей.ГруппаПользователей,
	                      |		ПРЕДСТАВЛЕНИЕССЫЛКИ(ГруппыПользователей.ГруппаПользователей),
	                      |		&ПустаяСтрока,
	                      |		ЛОЖЬ
	                      |	ИЗ
	                      |		РегистрСведений.ГруппыПользователей КАК ГруппыПользователей
	                      |	ГДЕ
	                      |		(НЕ ГруппыПользователей.Пароль = &ПустаяСтрока)
	                      |	
	                      |	ОБЪЕДИНИТЬ ВСЕ
	                      |	
	                      |	ВЫБРАТЬ
	                      |		&ТипДокументы,
	                      |		СостояниеДокументов.Документ,
	                      |		СостояниеДокументов.ПолныйНомерСтола,
	                      |		СостояниеДокументов.Комментарий,
	                      |		ИСТИНА
	                      |	ИЗ
	                      |		РегистрСведений.СостояниеДокументов КАК СостояниеДокументов
	                      |	ГДЕ
	                      |		СостояниеДокументов.Документ ССЫЛКА Документ.РеализацияТоваровУслуг
	                      |		И СостояниеДокументов.Модуль = ЗНАЧЕНИЕ(Перечисление.МодулиИПодсистемы.Ресторан)
	                      |	
	                      |	ОБЪЕДИНИТЬ ВСЕ
	                      |	
	                      |	ВЫБРАТЬ
	                      |		&ТипРабочиеЦентры,
	                      |		РабочиеЦентры.Ссылка,
	                      |		ПРЕДСТАВЛЕНИЕССЫЛКИ(РабочиеЦентры.Ссылка),
	                      |		&ПустаяСтрока,
	                      |		ЛОЖЬ
	                      |	ИЗ
	                      |		Справочник.РабочиеЦентры КАК РабочиеЦентры
	                      |	ГДЕ
	                      |		РабочиеЦентры.Актуальность
	                      |		И (НЕ РабочиеЦентры.Оборудование = &ПустаяСтрока)) КАК ВложенныйЗапрос
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	Тип,
	                      |	ВложенныйЗапрос.Представление
	                      |ИТОГИ ПО
	                      |	Тип");
	Запрос.УстановитьПараметр("ТипПользователи", Метаданные.Справочники.Пользователи.Синоним);
	Запрос.УстановитьПараметр("ТипГруппыПользователей", Метаданные.Справочники.ГруппыПользователей.Синоним);
	Запрос.УстановитьПараметр("ТипДокументы", Метаданные.Справочники.Столы.Синоним);
	Запрос.УстановитьПараметр("ТипРабочиеЦентры", Метаданные.Справочники.РабочиеЦентры.Синоним);
	Запрос.УстановитьПараметр("ПустаяСтрока", Строка(Неопределено));
	
	Возврат Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
КонецФункции // ПолучитьПолучателей()

Функция ПолучитьСообщения() Экспорт
	
	// Выборка данных.
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	__СообщенияПользователей.Сообщение КАК Сообщение
	                      |ИЗ
	                      |	РегистрСведений.__СообщенияПользователей КАК __СообщенияПользователей
	                      |ГДЕ
	                      |	(НЕ __СообщенияПользователей.Сообщение = &ПустаяСтрока)
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	Сообщение");
	Запрос.УстановитьПараметр("ПустаяСтрока", Строка(Неопределено));
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Сообщение");
	
КонецФункции // ПолучитьСообщения()
