
Процедура ПолучитьНастройкиПользователей(Ссылка, Объект) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	// Выборка данных.
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	НастройкиПользователейСтруктура.Ссылка КАК Настройка,
	                      |	НастройкиПользователейСтруктура.ТипЗначения,
	                      |	ЛОЖЬ КАК ТипЗначенияБулево,
	                      |	НастройкиПользователейЗначения.Значение,
	                      |	НастройкиПользователейЗначения.Значение КАК Флажок,
	                      |	ВЫБОР
	                      |		КОГДА НастройкиПользователейЗначения.Настройка ЕСТЬ NULL 
	                      |			ТОГДА ЛОЖЬ
	                      |		ИНАЧЕ ИСТИНА
	                      |	КОНЕЦ КАК Актуальность,
	                      |	НастройкиПользователейСтруктура.ЭтоГруппа КАК ТолькоПросмотр,
	                      |	ВЫБОР
	                      |		КОГДА НастройкиПользователейСтруктура.ЭтоГруппа
	                      |			ТОГДА 2
	                      |		ИНАЧЕ 0
	                      |	КОНЕЦ + ВЫБОР
	                      |		КОГДА НастройкиПользователейСтруктура.ПометкаУдаления
	                      |			ТОГДА 1
	                      |		ИНАЧЕ 0
	                      |	КОНЕЦ КАК ИндексКартинки
	                      |ИЗ
	                      |	ПланВидовХарактеристик.НастройкиПользователей КАК НастройкиПользователейСтруктура
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиПользователей КАК НастройкиПользователейЗначения
	                      |		ПО (НастройкиПользователейЗначения.Настройка = НастройкиПользователейСтруктура.Ссылка)
	                      |			И (НастройкиПользователейЗначения.Пользователь = &Владелец)
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	НастройкиПользователейСтруктура.Ссылка ИЕРАРХИЯ
	                      |АВТОУПОРЯДОЧИВАНИЕ");
	Если (Ссылка = Неопределено) Тогда
		Запрос.УстановитьПараметр("Владелец", Ссылка);
	Иначе
		Запрос.УстановитьПараметр("Владелец", Ссылка.Ссылка);
	КонецЕсли;
	
	НастройкиПользователей = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	// Булевый тип.
	Для Каждого СтрокаНастройки Из НастройкиПользователей.Строки.НайтиСтроки(Новый Структура("ТолькоПросмотр,ТипЗначения", Ложь, Новый ОписаниеТипов("Булево")), Истина) Цикл
		СтрокаНастройки.ТипЗначенияБулево = Истина;
	КонецЦикла; 
	
	ЗначениеВДанныеФормы(НастройкиПользователей, Объект);	

	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры // ПолучитьНастройкиПользователей()

Процедура УстановитьНастройкиПользователей(Ссылка, Объект, Отказ) Экспорт

	Если Не Отказ Тогда

		УстановитьПривилегированныйРежим(Истина);
		
		ПраваПользователей = ДанныеФормыВЗначение(Объект, Тип("ДеревоЗначений"));
		
		НаборЗаписей = РегистрыСведений.НастройкиПользователей.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Пользователь.Установить(Ссылка.Ссылка);
		
		// Актуальные.
		Для Каждого СтрокаПравДоступа Из ПраваПользователей.Строки.НайтиСтроки(Новый Структура("Актуальность,ТолькоПросмотр", Истина, Ложь), Истина) Цикл 
			Запись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, СтрокаПравДоступа);
			Запись.Пользователь = Ссылка.Ссылка;
		КонецЦикла; 

		Попытка
			НаборЗаписей.Записать(Истина);
		Исключение
			// TODO: Регистрация ошибки.
			__ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке(НСтр("ru = 'Не удалось изменить настройки'; uk = 'Не вдалося змінити настройки'"), ,, "НастройкиПользователей", Отказ);
		КонецПопытки;

		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли; 
	
КонецПроцедуры // УстановитьНастройкиПользователей()


Функция ПолучитьПрисутсвиеНастроекПользователей(Ссылка) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	                      |	НастройкиПользователей.Настройка
	                      |ИЗ
	                      |	РегистрСведений.НастройкиПользователей КАК НастройкиПользователей
	                      |ГДЕ
	                      |	НастройкиПользователей.Пользователь = &Владелец");
	Запрос.УстановитьПараметр("Владелец", Ссылка.Ссылка);
	
	Результат = Не Запрос.Выполнить().Пустой();

	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Результат;
	
КонецФункции // ПолучитьПрисутсвиеНастроекПользователей()
