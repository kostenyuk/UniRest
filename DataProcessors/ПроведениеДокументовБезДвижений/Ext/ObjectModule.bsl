
Процедура ВыполнитьОбработку(Ресторан = Неопределено, НачалоПериода = Неопределено, КонецПериода = Неопределено) Экспорт
	
	ВремяНачала = ТекущаяДата();
	
	НачалоПериода = ?(ЗначениеЗаполнено(НачалоПериода), НачалоПериода, НачалоМесяца(ТекущаяДата()));
	КонецПериода = ?(ЗначениеЗаполнено(КонецПериода), КонецПериода, КонецДня(ТекущаяДата()));
	
	РезультатЗапроса = СформироватьЗапрос(Ресторан, НачалоПериода, КонецПериода, "РеализацияТоваровУслуг", "Продажи");
	ОбработатьДокументы(РезультатЗапроса); РезультатЗапроса = Неопределено;
	РезультатЗапроса = СформироватьЗапрос(Ресторан, НачалоПериода, КонецПериода, "ЧекККМ");
	ОбработатьДокументы(РезультатЗапроса); РезультатЗапроса = Неопределено;
	РезультатЗапроса = СформироватьЗапрос(Ресторан, НачалоПериода, КонецПериода, "СписаниеТоваров");
	ОбработатьДокументы(РезультатЗапроса); РезультатЗапроса = Неопределено;
	РезультатЗапроса = СформироватьЗапрос(Ресторан, НачалоПериода, КонецПериода, "ТабельУчетаРабочегоВремениОрганизации", "РабочееВремяРаботниковОрганизаций");
	ОбработатьДокументы(РезультатЗапроса); РезультатЗапроса = Неопределено;
	РезультатЗапроса = СформироватьЗапрос(Ресторан, НачалоПериода, КонецПериода, "ПланПродаж", "ПланыПродаж");
	ОбработатьДокументы(РезультатЗапроса); РезультатЗапроса = Неопределено;
	РезультатЗапроса = СформироватьЗапрос(Ресторан, НачалоПериода, КонецПериода, "СобытиеДисконтныхКарт", "СобытияДисконтныхКарт");
	ОбработатьДокументы(РезультатЗапроса); РезультатЗапроса = Неопределено;
	
	ВремяОкончания 	= ТекущаяДата();
	Длительность 	= ОбщегоНазначения.ЧЧММСС(ВремяОкончания - ВремяНачала); //в секундах
	Сообщить(
	"Время начала 		: " + ВремяНачала 		+ Символы.ПС +
	"Длительность 		: " + Длительность      + Символы.ПС +
	"Время окончания 	: " + ВремяОкончания 	+ Символы.ПС
	, СтатусСообщения.Информация);
	
КонецПроцедуры

Функция СформироватьЗапрос(Ресторан, НачалоПериода, КонецПериода, ИмяДокумента, ИмяРегистра = "")
	
	Запрос = Новый Запрос;
	
	Если ИмяДокумента = "ЧекККМ" Тогда
		Запрос.Текст = "ВЫБРАТЬ
		               |	Чек.Ссылка
		               |ИЗ
		               |	РегистрНакопления.РозничнаяВыручка КАК РозничнаяВыручка
		               |		ПОЛНОЕ СОЕДИНЕНИЕ Документ.ЧекККМ КАК Чек
		               |		ПО РозничнаяВыручка.Регистратор = Чек.Ссылка
		               |ГДЕ
		               |	Чек.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		               |	И Чек.Проведен
		               |	И Чек.Ресторан = &Ресторан
		               |	И РозничнаяВыручка.Регистратор ЕСТЬ NULL 
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	Чек.Дата";
		Если ЗначениеЗаполнено(Ресторан) Тогда
			Запрос.УстановитьПараметр("Ресторан", Ресторан);
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "Чек.Ресторан = &Ресторан", "ИСТИНА");
		КонецЕсли;
	ИначеЕсли ИмяДокумента = "СписаниеТоваров" Тогда
		Запрос.Текст = "ВЫБРАТЬ
		               |	СписаниеТоваровТовары.Ссылка
		               |ИЗ
		               |	РегистрНакопления.СписанияТоваров КАК СписанияТоваров
		               |		ПОЛНОЕ СОЕДИНЕНИЕ Документ.СписаниеТоваров.Товары КАК СписаниеТоваровТовары
		               |		ПО СписанияТоваров.Регистратор = СписаниеТоваровТовары.Ссылка
		               |ГДЕ
		               |	СписаниеТоваровТовары.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		               |	И СписаниеТоваровТовары.Ссылка.Проведен
		               |	И СписаниеТоваровТовары.Списание
		               |	И СписаниеТоваровТовары.Ссылка.Ресторан = &Ресторан
		               |	И СписанияТоваров.Регистратор ЕСТЬ NULL 
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	СписаниеТоваровТовары.Ссылка.Дата";
		Если ЗначениеЗаполнено(Ресторан) Тогда
			Запрос.УстановитьПараметр("Ресторан", Ресторан);
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "СписаниеТоваровТовары.Ссылка.Ресторан = &Ресторан", "ИСТИНА");
		КонецЕсли;
	Иначе
		Запрос.Текст = "ВЫБРАТЬ
		               |	РеализацияТоваровУслуг.Ссылка
		               |ИЗ
		               |	РегистрНакопления.Продажи КАК Продажи
		               |		ПОЛНОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		               |		ПО Продажи.Регистратор = РеализацияТоваровУслуг.Ссылка
		               |ГДЕ
		               |	РеализацияТоваровУслуг.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		               |	И РеализацияТоваровУслуг.Проведен
		               |	И РеализацияТоваровУслуг.Ресторан = &Ресторан
		               |	И Продажи.Регистратор ЕСТЬ NULL 
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	РеализацияТоваровУслуг.Дата";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "РеализацияТоваровУслуг", ИмяДокумента);			   
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Продажи", ИмяРегистра);
		Если ЗначениеЗаполнено(Ресторан) Тогда
			Запрос.УстановитьПараметр("Ресторан", Ресторан);
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст, ""+ИмяДокумента+".Ресторан = &Ресторан", "ИСТИНА");
		КонецЕсли;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Процедура ОбработатьДокументы(РезультатЗапроса)
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		Попытка
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
			Сообщить("Документ " + ДокументОбъект.Ссылка + " проведен");
		Исключение
			Сообщить(ОписаниеОшибки());			
		КонецПопытки;
	КонецЦикла;
КонецПроцедуры
