
Процедура ПередЗаписью(Отказ, Замещение)
	
	// При обмене данными ничего не проверяем.
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеТиповСправочников = Справочники.ТипВсеСсылки();
	Для Каждого Запись Из ЭтотОбъект Цикл
		
		// Нормализация.
		Если ОписаниеТиповСправочников.СодержитТип(ТипЗнч(Запись.ОбъектДоступа)) Тогда
		
			Запись.Печать = Ложь;
			Запись.Проведение = Ложь;
			Запись.РазделениеПоРесторанам = Неопределено;
			Запись.РазделениеПоСотрудникам = Неопределено;
			
		Иначе
			
			Запись.РазделениеПоРесторанам = __ОбщегоНазначенияКлиентСервер.ПолучитьНеПустоеЗначение(Запись.РазделениеПоРесторанам, Перечисления.ПраваДоступаПользователей.Запретить);
			Запись.РазделениеПоСотрудникам = __ОбщегоНазначенияКлиентСервер.ПолучитьНеПустоеЗначение(Запись.РазделениеПоСотрудникам, Перечисления.ПраваДоступаПользователей.Запретить);
			
		КонецЕсли; 
		
		// Дополнение.
		Запись.ИгнорироватьРазделениеПоРесторанам = (Запись.РазделениеПоРесторанам = Перечисления.ПраваДоступаПользователей.Разрешить) Или (Запись.РазделениеПоРесторанам = Перечисления.ПраваДоступаПользователей.Привилегированные);
		Запись.ИгнорироватьРазделениеПоСотрудникам = (Запись.РазделениеПоСотрудникам = Перечисления.ПраваДоступаПользователей.Разрешить) Или (Запись.РазделениеПоСотрудникам = Перечисления.ПраваДоступаПользователей.Привилегированные);
		
	КонецЦикла;
	
КонецПроцедуры // ПередЗаписью()
