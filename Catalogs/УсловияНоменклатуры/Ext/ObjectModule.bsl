
Перем мИзмененРодитель;

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	// При обмене данными ничего не проверяем.
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоГруппа Тогда
		__ОбщегоНазначенияСервер.ИзменитьПроверяемыеРеквизиты(ПроверяемыеРеквизиты, , "Номенклатура");
	Иначе
		__ОбщегоНазначенияСервер.ИзменитьПроверяемыеРеквизиты(ПроверяемыеРеквизиты, , "Наименование");
		Наименование = Номенклатура;
	КонецЕсли; 
	
КонецПроцедуры // ОбработкаПроверкиЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	
	Если Не ЭтоГруппа Тогда
		Наименование = Неопределено;
		Номенклатура = Неопределено;
		Актуальность = Истина;
	КонецЕсли;
	
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	мИзмененРодитель = НЕ ЭтоНовый()
		И Родитель<>Ссылка.Родитель;
	
	Если не ЭтотОбъект.ЭтоГруппа  тогда
		Если ЭтотОбъект.ЭтоНовый() тогда
			Если не ЭтотОбъект.ПометкаУдаления Тогда
				Если ЗначениеЗаполнено(ЭтотОбъект.Номенклатура) тогда
					Проверка =  УправлениеНоменклатуройСервер.НоменклатураПроверка(ЭтотОбъект, "УсловияНоменклатуры");
					Если Проверка Тогда
						ЭтотОбъект.Наименование = ЭтотОбъект.Номенклатура.Наименование;
					Иначе 
						Отказ = Истина;			
					КонецЕсли;
				Иначе
					Отказ = Истина;	
				КонецЕсли;
			КонецЕсли;
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	
	// При обмене данными ничего не проверяем.
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Номенклатура меню.
	Если Не Отказ Тогда
		Если Не ЭтоГруппа Тогда
			Запрос = Новый Запрос(
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ ПЕРВЫЕ 1
			|	НоменклатураМеню.Номенклатура
			|ИЗ
			|	РегистрСведений.НоменклатураМеню КАК НоменклатураМеню
			|ГДЕ
			|	НоменклатураМеню.Номенклатура = &Номенклатура");
			Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
			Если Запрос.Выполнить().Пустой() Тогда
				Отказ = Не Обработки.ОбновлениеМеню.Создать().ДобавитьОбновитьНоменклатуруМеню(Номенклатура);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если мИзмененРодитель И НЕ Отказ Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	МодификаторыНоменклатуры.Номенклатура
		|ИЗ
		|	РегистрСведений.УсловияНоменклатуры КАК МодификаторыНоменклатуры
		|ГДЕ
		|	МодификаторыНоменклатуры.Условия В ИЕРАРХИИ(&Ссылка)"
		);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		ВыборкаПоНоменклатуре = Запрос.Выполнить().Выбрать();
		
		Запрос = Новый Запрос(                                             
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	МодификаторыНоменклатуры.Условия
		|ИЗ
		|	РегистрСведений.УсловияНоменклатуры КАК МодификаторыНоменклатуры
		|ГДЕ
		|	МодификаторыНоменклатуры.Номенклатура = &Номенклатура
		|	И МодификаторыНоменклатуры.Условия В ИЕРАРХИИ(&Ссылка)"
		);
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		Пока ВыборкаПоНоменклатуре.Следующий() Цикл
			
			АктуальностьРодителяЭлемента = Справочники.Номенклатура.ИспользуетсяУсловие(ВыборкаПоНоменклатуре.Номенклатура,Родитель);
			Если Не АктуальностьРодителяЭлемента Тогда
				Запрос.УстановитьПараметр("Номенклатура",ВыборкаПоНоменклатуре.Номенклатура);
				ВыборкаЗаписей = Запрос.Выполнить().Выбрать();
				Пока ВыборкаЗаписей.Следующий() Цикл
					НаборЗаписей = РегистрыСведений.УсловияНоменклатуры.СоздатьНаборЗаписей();
					НаборЗаписей.Отбор.Условия.Установить(Ссылка);
					НаборЗаписей.Записать();
				КонецЦикла;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли; 
	
КонецПроцедуры


