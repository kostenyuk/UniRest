
Функция ПолучитьНаименование(Объект) Экспорт

	Если ЗначениеЗаполнено(Объект.ТипСтола) Тогда
		Возврат Шаблон("[ТипСтола] № [Номер]", 
			Новый Структура("ТипСтола,Номер", Объект.ТипСтола, Объект.Код));
	КонецЕсли; 
	
	Возврат "№ " + Объект.Код; 
		
КонецФункции // ПолучитьНаименование()

Функция ПолучитьОчереднойКод(Объект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	МАКСИМУМ(Столы.Код) КАК Код
		|ИЗ
		|	Справочник.Столы КАК Столы
		|ГДЕ
		|	НЕ Столы.Ссылка = &Ссылка");
	Если (Объект = Неопределено) Тогда
		Запрос.УстановитьПараметр("Ссылка", Неопределено);
	Иначе
		Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	КонецЕсли; 	
	Выборка = Запрос.Выполнить().Выбрать();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Выборка.Следующий() И ЗначениеЗаполнено(Выборка.Код) Тогда
		Возврат Выборка.Код + 1;
	КонецЕсли;
	
	Возврат 1;
	
КонецФункции // ПолучитьОчереднойКод()


Функция ПолучитьВремяРезервированияСекунд(Стол) Экспорт
	
	Возврат Справочники.ТипыСтолов.ПолучитьВремяРезервированияСекунд(Стол);

КонецФункции // ПолучитьВремяРезервированияСекунд()

Функция ПолучитьМаксимальноеВремяРезервированияСекунд(Столы = Неопределено) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ЕСТЬNULL(МАКСИМУМ(Столы.ТипСтола.ВремяРезервированияСекунд), 3600) КАК ВремяРезервированияСекунд
	                      |ИЗ
	                      |	Справочник.Столы КАК Столы
	                      |ГДЕ
	                      |	Столы.Ссылка В(&Столы)");
	Если (Столы = Неопределено) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Столы.Ссылка В (&Столы)", "ИСТИНА");
	Иначе
		Запрос.УстановитьПараметр("Столы", Столы);
	КонецЕсли;
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ВремяРезервированияСекунд;
	КонецЕсли;
	
	Возврат 3600;

КонецФункции // ПолучитьМаксимальноеВремяРезервированияСекунд()


// Рефакторинг: использовать реквизит "Номер" вместо реквизита "Код".