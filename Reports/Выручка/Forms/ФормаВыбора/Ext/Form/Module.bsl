
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Установка реквизитов формы
	ИмяСправочника 		= Параметры.ИмяСправочника;
	ИмяПараметра 		= Параметры.ИмяПараметра;
	ЗначениеПараметра 	= Параметры.ЗначениеПараметра;
	
	ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени("Справочник." + ИмяСправочника);
	ЭтаФорма.Заголовок = ОбъектМетаданных.Синоним;
	
	// Заполнение дерева
	ДеревоЗначений = СформироватьДеревоЗначений(ОбъектМетаданных);
	ЗначениеВРеквизитФормы(ДеревоЗначений, "Дерево");
	
КонецПроцедуры

&НаСервере
Функция СформироватьДеревоЗначений(ОбъектМетаданных)
	
	СхемаКомпоновкиДанных = Отчеты.ВыручкаПоСчетам.ПолучитьМакет("КомпоновкаДанных");
	
	// Корректировка запроса набора данных
	НаборДанных = СхемаКомпоновкиДанных.НаборыДанных.Справочник;
	НаборДанных.Запрос = СтрЗаменить(НаборДанных.Запрос, "Справочник.ВидыОплатЧекаККМ", "Справочник." + ИмяСправочника);
	Если НЕ ОбъектМетаданных.Иерархический Тогда
		НаборДанных.Запрос = СтрЗаменить(НаборДанных.Запрос, "НЕ ВидыОплатЧекаККМ.ЭтоГруппа", "Истина");
	КонецЕсли;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных;
	КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	
	ПараметрСхемыКомпоновкиДанных = СхемаКомпоновкиДанных.Параметры.Найти(ИмяПараметра);
	ПараметрСхемыКомпоновкиДанных.Значение = ЗначениеПараметра;
	
	МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, КомпоновщикНастроек.Настройки, , , Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных; 
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных);
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВывода.УстановитьОбъект(Новый ДеревоЗначений);
	ДеревоЗначений = ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
	// Изменение пометок родителей.
	ИзменитьПометкиРодителей(ДеревоЗначений);
		
	Возврат ДеревоЗначений;
	
КонецФункции

&НаСервере
Процедура ИзменитьПометкиРодителей(ДеревоЗначений)
	
	Для Каждого ПодчиненнаяСтрокаДерева Из ДеревоЗначений.Строки Цикл
		РодительДанные = ПодчиненнаяСтрокаДерева.Родитель;
		Пока (Не РодительДанные = Неопределено) Цикл
			ПодчиненныеЭлементыРодителя = РодительДанные.Строки;         
			Итог = 0; 
			Для Каждого ПодчиненныеДанные Из ПодчиненныеЭлементыРодителя Цикл
				Если НЕ ПодчиненныеДанные.Пометка = Неопределено Тогда
					Итог = Итог + ПодчиненныеДанные.Пометка; 
				КонецЕсли;
			КонецЦикла; 
			РодительДанные.Пометка = Итог;
			РодительДанные = РодительДанные.Родитель;
		КонецЦикла;
		ИзменитьПометкиРодителей(ПодчиненнаяСтрокаДерева);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция СформироватьСписокЗначений()
	
	ДеревоЗначений = РеквизитФормыВЗначение("Дерево");
	СписокЗначений = Новый СписокЗначений;
	
	Для Каждого НайденнаяСтрока Из ДеревоЗначений.Строки.НайтиСтроки(Новый Структура("Пометка", Истина), Истина) Цикл
		// Есть подчиненные элементы, значит это группа
		Если НайденнаяСтрока.Строки.Количество() Тогда
			Продолжить;
		КонецЕсли;
		СписокЗначений.Добавить(НайденнаяСтрока.Ссылка, , Истина);
	КонецЦикла;
		
	Возврат СписокЗначений;
	
КонецФункции

&НаКлиенте
Процедура ДеревоПометкаПриИзменении(Элемент)
	ПометкаПриИзменении();
КонецПроцедуры

&НаКлиенте
Процедура ПометкаПриИзменении()
	
	ТекущиеДанные = Элементы.Дерево.ТекущиеДанные;
	
	// Изменение родителей.
	РодительДанные = ТекущиеДанные.ПолучитьРодителя();
	Пока (НЕ РодительДанные = Неопределено) Цикл
		ПодчиненныеЭлементыРодителя = РодительДанные.ПолучитьЭлементы(); 
		Итог = 0; 
		Для Каждого ПодчиненныеДанные Из ПодчиненныеЭлементыРодителя Цикл 
			Итог = Итог + ПодчиненныеДанные.Пометка; 
		КонецЦикла; 
		РодительДанные.Пометка = Итог;
		РодительДанные = РодительДанные.ПолучитьРодителя();
	КонецЦикла;
	
	// Изменение подчиненных.
	МассивПодчиненных = Новый Массив; 
	МассивПодчиненных.Добавить(ТекущиеДанные.ПолучитьЭлементы());
	Для Каждого РодительЭлементы Из МассивПодчиненных Цикл
		Для Каждого ПодчиненныеДанные Из РодительЭлементы Цикл
			ЗаполнитьЗначенияСвойств(ПодчиненныеДанные, ТекущиеДанные, "Пометка");
			РодительЭлементы = ПодчиненныеДанные.ПолучитьЭлементы(); 
			Если РодительЭлементы.Количество() Тогда
				МассивПодчиненных.Добавить(РодительЭлементы);
			КонецЕсли; 
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	ПараметрыЗакрытия = Новый Структура;
	СписокОтмеченных = СформироватьСписокЗначений();
	ПараметрыЗакрытия.Вставить("Имя", ИмяСправочника);
	ПараметрыЗакрытия.Вставить("Значение", СписокОтмеченных);
	
	Оповестить(, ПараметрыЗакрытия, ЭтаФорма);
	
	Справочник 	= Неопределено;
	Параметр 	= Неопределено;
	Значение 	= Неопределено;
	
	Закрыть();
	
КонецПроцедуры
