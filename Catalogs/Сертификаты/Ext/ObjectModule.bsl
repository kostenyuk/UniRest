
Функция ПолучитьНаименование()

	Если ЗначениеЗаполнено(ВидСертификата) И (Не ПустаяСтрока(КодСертификата)) Тогда
		Возврат Шаблон("[ВидСертификата] № [КодСертификата]", 
			Новый Структура("ВидСертификата,КодСертификата", ВидСертификата, КодСертификата));
	КонецЕсли; 
	
	Возврат "№ " + КодСертификата; 
		
КонецФункции // ПолучитьНаименование()


Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	// Проверка.
	Если Не ЭтоГруппа Тогда
		
		Если ПустаяСтрока(Наименование) Тогда
			Наименование = ПолучитьНаименование();
		КонецЕсли; 
		
		Если Не __УправлениеДаннымиСервер.ПроверитьУникальностьРеквизитаСправочника(ЭтотОбъект, "КодСертификата") Тогда
			__ОбщегоНазначенияСервер.СообщитьОбНеуникальностиРеквизитаСправочника(ЭтотОбъект, "КодСертификата", , Отказ);
		КонецЕсли; 
		
		Если Не ВидСертификата.НеобходимаДисконтнаяКатрочка Тогда
			__ОбщегоНазначенияСервер.ИзменитьПроверяемыеРеквизиты(ПроверяемыеРеквизиты, , "ДисконтнаяКарта");
		КонецЕсли; 
		Если Не ВидСертификата.НеобходимПароль Тогда
			__ОбщегоНазначенияСервер.ИзменитьПроверяемыеРеквизиты(ПроверяемыеРеквизиты, , "Пароль");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ОбработкаПроверкиЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	
	Если Не ЭтоГруппа Тогда
		КодСертификата = Неопределено;
		ДисконтнаяКарта = Неопределено;
		Пароль = Неопределено;
		
		Если ПустаяСтрока(Наименование) Тогда
			Наименование = ПолучитьНаименование();
		КонецЕсли; 
		
	КонецЕсли;
	
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ)

 	// При обмене данными ничего не проверяем.
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;	
	
	// Нормализация.
	Если Не ЭтоГруппа Тогда
		
		Если ПустаяСтрока(Наименование) Тогда
			Наименование = ПолучитьНаименование();
		КонецЕсли; 
		
		Если Не ВидСертификата.НеобходимаДисконтнаяКатрочка Тогда
			ДисконтнаяКарта = Неопределено;
		КонецЕсли; 
		Если Не ВидСертификата.НеобходимПароль Тогда
			Пароль = Неопределено;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()


