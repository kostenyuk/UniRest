
Функция СоответствиеСтруктурой(Соответствие) Экспорт
	
	// Структура.
	Структура = Новый Структура;
	
	// Перенос данных.
	Для Каждого Элемент Из Соответствие Цикл Структура.Вставить(Элемент.Ключ, Элемент.Значение); КонецЦикла;
	
	Возврат Структура;
	
КонецФункции // СоответствиеСтруктурой()


Функция НаборСтруктурой(Набор, Колонки) Экспорт
	
	// Структура.
	Если (ТипЗнч(Колонки) = Тип("Строка")) Тогда
		Структура = Новый Структура(Колонки);
	Иначе
		Структура = Новый Структура; Для Каждого Колонка Из Колонки Цикл Структура.Вставить(Колонка.Имя); КонецЦикла;
	КонецЕсли;
	
	// Перенос данных.
	ЗаполнитьЗначенияСвойств(Структура, Набор);
	
	Возврат Структура;
	
КонецФункции // НаборСтруктурой()

Функция МассивНаборовМассивомСтруктур(Коллекция, Колонки) Экспорт
	
	// Массив.
	Массив = Новый Массив;
	
	Если Коллекция.Количество() Тогда
	
		// Структура.
		Если (ТипЗнч(Колонки) = Тип("Строка")) Тогда
			СтруктураКолонок = Колонки;
		Иначе
			Первый = Истина; Для Каждого Колонка Из Колонки Цикл Если Первый Тогда СтруктураКолонок = Колонка.Имя; Первый = Ложь; Иначе СтруктураКолонок = СтруктураКолонок + "," + Колонка.Имя; КонецЕсли; КонецЦикла;
		КонецЕсли;
	
		// Перенос данных.
		Для Каждого Набор Из Коллекция Цикл Структура = Новый Структура(СтруктураКолонок); ЗаполнитьЗначенияСвойств(Структура, Набор); Массив.Добавить(Структура); КонецЦикла; 
		
	КонецЕсли; 
	
	Возврат Массив;
	
КонецФункции // МассивНаборовМассивомСтруктур()

Функция ЗапросСтруктурой(Запрос) Экспорт
	
	// Пустое значение.
	Если (ТипЗнч(Запрос) = Тип("Запрос")) Тогда
		РезультатЗапроса = Запрос.Выполнить();
	Иначе
		РезультатЗапроса = Запрос;
	КонецЕсли;
	
	// Структура.
	Структура = Новый Структура; Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл Структура.Вставить(Колонка.Имя, Колонка.ТипЗначения.ПривестиЗначение(Неопределено)); КонецЦикла;
	
	// Перенос данных.
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Следующий() Тогда ЗаполнитьЗначенияСвойств(Структура, Выборка); КонецЕсли;
	
	Возврат Структура;
	
КонецФункции // ЗапросСтруктурой()

Функция ЗапросМассивом(Запрос, Значение = Неопределено) Экспорт
	
	// Пустое значение.
	Если (ТипЗнч(Запрос) = Тип("Запрос")) Тогда
		РезультатЗапроса = Запрос.Выполнить();
	Иначе
		РезультатЗапроса = Запрос;
	КонецЕсли;
	
	Если (Значение = Неопределено) Тогда
		
		// Перенос данных.
		Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку(РезультатЗапроса.Колонки[0].Имя);	
	
	Иначе	
	
		// Перенос данных.
		Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку(Значение);	
		
	КонецЕсли;
	
КонецФункции // ЗапросМассивом()

Функция ЗапросСоответсвием(Запрос, Ключ, Значение = Неопределено) Экспорт
	
	// Пустое значение.
	Если (ТипЗнч(Запрос) = Тип("Запрос")) Тогда
		РезультатЗапроса = Запрос.Выполнить();
	Иначе
		РезультатЗапроса = Запрос;
	КонецЕсли;
	
	Соответствие = Новый Соответствие;
	
	Выборка = РезультатЗапроса.Выбрать();
	Если Булево(РезультатЗапроса.Колонки.Количество() - 2) Тогда
		
		// Структура.
		Если (Значение = Неопределено) Тогда
			Первый = Истина; Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл Если Первый Тогда СтруктураКолонок = Колонка.Имя; Первый = Ложь; Иначе СтруктураКолонок = СтруктураКолонок + "," + Колонка.Имя; КонецЕсли; КонецЦикла;
		Иначе
			Если (ТипЗнч(Значение) = Тип("Строка")) Тогда
				Если Булево(Найти(Значение, ",")) Тогда
					СтруктураКолонок = Значение;
				Иначе
					
					// Перенос данных.
					Пока Выборка.Следующий() Цикл Соответствие[Выборка[Ключ]] = Выборка[Значение]; КонецЦикла;
					
					// Результат.
					Возврат Соответствие;	
					
				КонецЕсли;
			Иначе
				Первый = Истина; Для Каждого Колонка Из Значение Цикл Если Первый Тогда СтруктураКолонок = Колонка.Имя; Первый = Ложь; Иначе СтруктураКолонок = СтруктураКолонок + "," + Колонка.Имя; КонецЕсли; КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		// Перенос данных.
		Пока Выборка.Следующий() Цикл Структура = Новый Структура(СтруктураКолонок); ЗаполнитьЗначенияСвойств(Структура, Выборка); Соответствие[Выборка[Ключ]] = Структура; КонецЦикла;
	
		Возврат Соответствие;	
	
	Иначе	
		
		// Значение.
		Если (Значение = Неопределено) Тогда
			Если (РезультатЗапроса.Колонки[0].Имя = Ключ) Тогда
				Значение = РезультатЗапроса.Колонки[1].Имя;
			Иначе
				Значение = РезультатЗапроса.Колонки[0].Имя;
			КонецЕсли;
		КонецЕсли;
	
		// Перенос данных.
		Пока Выборка.Следующий() Цикл Соответствие[Выборка[Ключ]] = Выборка[Значение]; КонецЦикла;
		
		// Результат.
		Возврат Соответствие;	
		
	КонецЕсли;
	
КонецФункции // ЗапросМассивом()

Функция ЗапросСоответсвиемНаборов(Запрос, Ключ, Значение = Неопределено) Экспорт
	
	// Пустое значение.
	Если (ТипЗнч(Запрос) = Тип("Запрос")) Тогда
		РезультатЗапроса = Запрос.Выполнить();
	Иначе
		РезультатЗапроса = Запрос;
	КонецЕсли;
	
	Соответствие = Новый Соответствие;
	
	Выборка = РезультатЗапроса.Выбрать();
		
	// Структура.
	Если (Значение = Неопределено) Тогда
		Первый = Истина; Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл Если Первый Тогда СтруктураКолонок = Колонка.Имя; Первый = Ложь; Иначе СтруктураКолонок = СтруктураКолонок + "," + Колонка.Имя; КонецЕсли; КонецЦикла;
	Иначе
		Если (ТипЗнч(Значение) = Тип("Строка")) Тогда
			СтруктураКолонок = Значение;
		Иначе
			Первый = Истина; Для Каждого Колонка Из Значение Цикл Если Первый Тогда СтруктураКолонок = Колонка.Имя; Первый = Ложь; Иначе СтруктураКолонок = СтруктураКолонок + "," + Колонка.Имя; КонецЕсли; КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	// Перенос данных.
	Пока Выборка.Следующий() Цикл Структура = Новый Структура(СтруктураКолонок); ЗаполнитьЗначенияСвойств(Структура, Выборка); ЗначениеКлюча = Выборка[Ключ]; Массив = Соответствие[ЗначениеКлюча]; Если (Массив = Неопределено) Тогда Массив = Новый Массив; Соответствие[ЗначениеКлюча] = Массив; КонецЕсли; Массив.Добавить(Структура); КонецЦикла;

	Возврат Соответствие;	
	
КонецФункции // ЗапросМассивом()




Функция ПолучитьПредставлениеДаты(Дата) Экспорт
	
	// Формирование сокращенного представления даты.
	Разница = Дата - ТекущаяДата();
	Если (-43200 < Разница) И (Разница < 43200) Тогда	// Плюс/минус 12 часов.
		ФорматнаяСтрока = "ДФ=HH:mm";
	Иначе
		ФорматнаяСтрока = "ДФ='dd.MM.yy HH:mm'";
	КонецЕсли;
	
	Возврат Формат(Дата, ФорматнаяСтрока);
	
КонецФункции // ПолучитьПредставлениеДаты()

Функция ПолучитьПредставлениеДокумента(Документ) Экспорт
	
	Если ЗначениеЗаполнено(Документ.Ссылка) Тогда
		Если (Документ.Модуль = Перечисления.МодулиИПодсистемы.Ресторан) Тогда
			Возврат Документ.ПолныйНомерСтола;
		Иначе
			Возврат Документ.Номер;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Строка(Неопределено);
	
КонецФункции // ПолучитьПредставлениеДокумента()
