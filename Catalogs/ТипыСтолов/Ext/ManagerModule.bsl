
Функция ОсновнойТипСтола() Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	                      |	ТипыСтолов.Ссылка
	                      |ИЗ
	                      |	(ВЫБРАТЬ ПЕРВЫЕ 1
	                      |		ТипыСтолов.Ссылка КАК Ссылка,
	                      |		ТипыСтолов.Ранг КАК Ранг
	                      |	ИЗ
	                      |		(ВЫБРАТЬ
	                      |			Столы.ТипСтола КАК Ссылка,
	                      |			КОЛИЧЕСТВО(Столы.Ссылка) КАК Ранг
	                      |		ИЗ
	                      |			Справочник.Столы КАК Столы
	                      |		ГДЕ
	                      |			НЕ Столы.ЭтоГруппа
	                      |		
	                      |		СГРУППИРОВАТЬ ПО
	                      |			Столы.ТипСтола) КАК ТипыСтолов
	                      |	
	                      |	ОБЪЕДИНИТЬ ВСЕ
	                      |	
	                      |	ВЫБРАТЬ ПЕРВЫЕ 1
	                      |		ТипыСтолов.Ссылка,
	                      |		-1
	                      |	ИЗ
	                      |		Справочник.ТипыСтолов КАК ТипыСтолов
	                      |	ГДЕ
	                      |		НЕ ТипыСтолов.ЭтоГруппа
	                      |	
	                      |	УПОРЯДОЧИТЬ ПО
	                      |		Ранг УБЫВ,
	                      |		Ссылка) КАК ТипыСтолов
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	ТипыСтолов.Ранг УБЫВ,
	                      |	ТипыСтолов.Ссылка.ПометкаУдаления,
	                      |	ТипыСтолов.Ссылка.Наименование");
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Справочники.ТипыСтолов.ПустаяСсылка();

КонецФункции // ОсновнойТипСтола()


Функция ПолучитьВремяРезервированияСекунд(ТипСтола) Экспорт
	
	Резултат = ТипСтола.ВремяРезервированияСекунд;
	
	Если Не Резултат Тогда
		Резултат = 3600;
	КонецЕсли;
	
	Возврат Резултат;

КонецФункции // ПолучитьВремяРезервированияСекунд()

Функция ПолучитьМаксимальноеВремяРезервированияСекунд(ТипыСтолов = Неопределено) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ЕСТЬNULL(МАКСИМУМ(ТипыСтолов.ВремяРезервированияСекунд), 3600) КАК ВремяРезервированияСекунд
	                      |ИЗ
	                      |	Справочник.ТипыСтолов КАК ТипыСтолов
	                      |ГДЕ
	                      |	ТипыСтолов.Ссылка В (&ТипыСтолов)");
	Если (ТипыСтолов = Неопределено) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ТипыСтолов.Ссылка В (&ТипыСтолов)", "ИСТИНА");
	Иначе
		Запрос.УстановитьПараметр("ТипыСтолов", ТипыСтолов);
	КонецЕсли;
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ВремяРезервированияСекунд;
	КонецЕсли;
	
	Возврат 3600;

КонецФункции // ПолучитьМаксимальноеВремяРезервированияСекунд()
