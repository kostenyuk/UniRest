
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	// Подавление проверки.
	Для Каждого Запись Из ЭтотОбъект Цикл
		
		Если Не Запись.ВебСервисИспользовать Тогда
			ПроверяемыеРеквизиты.Очистить();	
		КонецЕсли;
		Прервать;
		
	КонецЦикла;

КонецПроцедуры // ОбработкаПроверкиЗаполнения()

Процедура ПередЗаписью(Отказ, Замещение)
	
	// При обмене данными ничего не проверяем.
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		
		// Веб-сервис.
		Запись.ВебСервисПериодНеактивностиСеансов = Мин(Макс(Запись.ВебСервисПериодНеактивностиСеансов, '0001.01.01 00:00:05'), '0001.01.01 01:00:00');
		Запись.ВебСервисПериодХраненияНеактивнныхСеансов = Макс(Запись.ВебСервисПериодХраненияНеактивнныхСеансов, Запись.ВебСервисПериодНеактивностиСеансов + 15, '0001.01.01 00:15:00');
		
		// Сервер печати.
		Запись.СерверПечатиПериодИнтервалаПечати = Мин(Макс(Запись.СерверПечатиПериодИнтервалаПечати, '0001.01.01 00:00:05'), '0001.01.01 00:01:00');
		Запись.СерверПечатиПериодХраненияЗаданий = Макс(Запись.СерверПечатиПериодХраненияЗаданий, Запись.СерверПечатиПериодИнтервалаПечати + 15, '0001.01.01 00:15:00');
		
		// Веб-сервер.
		Если Запись.ВебСервисИспользовать Тогда
			Если Не ЗначениеЗаполнено(Запись.ВебСерверИдентификаторПубликации) Тогда
				Запись.ВебСерверИдентификаторПубликации = Новый УникальныйИдентификатор;
			КонецЕсли;
			Запись.ВебСерверКаталогПублицкации = РегистрыСведений.__НастройкиВебСервиса.ПолучитьПутьСтандартаПлатформы(Запись.ВебСерверКаталог, ТипПлатформы.Linux_x86) + "/2k/" + Запись.ВебСерверИдентификаторПубликации;
			Запись.ВебСерверКаталогИзображений = Запись.ВебСерверКаталогПублицкации + "/pic/";
			Запись.ВебСерверURLИзображений = "/../2k/" + Запись.ВебСерверИдентификаторПубликации + "/pic/";
		КонецЕсли;
		
		Прервать;
		
	КонецЦикла;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ, Замещение)
	
	// При обмене данными ничего не проверяем.
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		
		// Удаление связанных данных.
		Если Не Запись.ВебСервисИспользовать Тогда
			РегистрыСведений.__СостояниеСеансовВебСервиса.СоздатьНаборЗаписей().Записать();
			РегистрыСведений.__СостояниеЗаданийСервераПечати.СоздатьНаборЗаписей().Записать();
			РегистрыСведений.__ЗначенияСеансовВебСервиса.СоздатьНаборЗаписей().Записать();
		КонецЕсли;
		
		Прервать;
		
	КонецЦикла;
	
КонецПроцедуры // ПриЗаписи()
