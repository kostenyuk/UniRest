
&НаКлиенте
Процедура УстановитьВидимость()
	
	// Командная панель.
	Элементы.ПодробныйРежим.Пометка = ПодробныйРежим;
	
КонецПроцедуры // УстановитьВидимость()

&НаКлиенте
Процедура ПодробныйРежим(Команда)
	
	ПодробныйРежим = Не ПодробныйРежим;
	
	// Настройка формы.
	УстановитьВидимость();
	
КонецПроцедуры // ПодробныйРежим()


&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если (Не Копирование) И (Не Группа) Тогда
		
		ПолеКомпоновкиДанных = Новый ПолеКомпоновкиДанных("ТипКарты");
		Для Каждого ЭлементОтбора Из Список.Отбор.Элементы Цикл
			Если ЭлементОтбора.Использование И (ЭлементОтбора.ЛевоеЗначение = ПолеКомпоновкиДанных) И (ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно) И ЗначениеЗаполнено(ЭлементОтбора.ПравоеЗначение) Тогда
				Возврат;
			КонецЕсли; 
		КонецЦикла;
		
		Отказ = Истина;
		
		СписокЗначений = Новый СписокЗначений;
		СписокЗначений.Добавить(ПредопределенноеЗначение("Перечисление.ТипыИнформационныхКарт.Дисконтная"));
		СписокЗначений.Добавить(ПредопределенноеЗначение("Перечисление.ТипыИнформационныхКарт.Регистрационная"));
		ТипКарты = СписокЗначений.ВыбратьЭлемент(НСтр("ru = 'Тип карты'; uk = 'Тип карти'"));
		
		Если (ТипКарты = Неопределено) Тогда
			Возврат;
		КонецЕсли; 
		ТипКарты = ТипКарты.Значение;
		
		ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", Новый Структура("ТипКарты", ТипКарты));
		ОткрытьФорму("Справочник.ИнформационныеКарты.ФормаОбъекта", ПараметрыФормы, Элемент);
		
	КонецЕсли; 
	
КонецПроцедуры // СписокПередНачаломДобавления()


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// Режим выбора.
	РежимВыбора = __РаботаСДиалогамиКлиентСервер.ФормаРежимВыбора(ЭтаФорма, Элементы.Список,
						"ПодробныйРежим, ВидДисконтнойКарты,ГруппаПользователей");
		
	// Настройка формы.
	Элементы.ПроцентУточняемый.Видимость = Ложь;
	Элементы.ВидДисконтнойКарты.Видимость = Ложь;
	Элементы.ГруппаПользователей.Видимость = Ложь;
	
	Если Параметры.Свойство("Отбор") Тогда
		Если (__ПолучитьСвойство(Параметры.Отбор, "ТипКарты") = Перечисления.ТипыИнформационныхКарт.Дисконтная) Тогда
			
			АвтоЗаголовок = Ложь;
			Заголовок = НСтр("ru = 'Дисконтные карты'; uk = 'Дисконтні картки'");
			Элементы.ТипКарты.Видимость = Ложь;
			Элементы.ПроцентУточняемый.Видимость = Истина;
			Элементы.ВидДисконтнойКарты.Видимость = Не РежимВыбора;
			
		КонецЕсли; 
		Если (__ПолучитьСвойство(Параметры.Отбор, "ТипКарты") = Перечисления.ТипыИнформационныхКарт.Регистрационная) Тогда
			
			АвтоЗаголовок = Ложь;
			Заголовок = НСтр("ru = 'Регистрационные карты'; uk = 'Реєстраційні картки'");
			Элементы.ТипКарты.Видимость = Ложь;
			Элементы.ГруппаПользователей.Видимость = Не РежимВыбора;
			Элементы.ВладелецКарты.Заголовок = НСтр("ru = 'Пользователь'; uk = 'Користувач'");
			Элементы.ВладелецКарты.КартинкаШапки = БиблиотекаКартинок.Пользователь;
			
		КонецЕсли; 
	КонецЕсли; 
	Элементы.ПодробныйРежим.Видимость = Элементы.ПодробныйРежим.Видимость И (Не Элементы.ТипКарты.Видимость);
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// Настройка формы.
	УстановитьВидимость();
	
КонецПроцедуры // ПриОткрытии()
