////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Родитель") Тогда
		Объект.Родитель = Параметры.Родитель;
	КонецЕсли;
	
	РабочийКаталог = РассылкаОтчетовВызовСервера.ПолучитьРабочийКаталог(Объект.Ссылка);
	
	ОбновитьПолныйПуть();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	РабочийКаталог = РассылкаОтчетовВызовСервера.ПолучитьРабочийКаталог(Объект.Ссылка);
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура РодительПриИзменении(Элемент)
	
	ОбновитьПолныйПуть();
	
КонецПроцедуры

&НаКлиенте
Процедура РабочийКаталогВладельцаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Объект.Ссылка.Пустая() Тогда
		Если Записать() = Ложь Тогда
			Возврат;
		КонецЕсли;	
	КонецЕсли;	
	
	ОчиститьСообщения();
	
	Каталог = "";
	Режим = РежимДиалогаВыбораФайла.ВыборКаталога;
	
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(Режим);
	ДиалогОткрытияФайла.Каталог = РабочийКаталог;
	ДиалогОткрытияФайла.ПолноеИмяФайла = "";
	Фильтр = НСтр("ru = 'Все файлы(*.*)|*.*'");
	ДиалогОткрытияФайла.Фильтр = Фильтр;
	ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
	ДиалогОткрытияФайла.Заголовок = НСтр("ru = 'Выберите каталог'");
	Если ДиалогОткрытияФайла.Выбрать() Тогда
		
		ИмяКаталога = ДиалогОткрытияФайла.Каталог;
		//ИмяКаталога = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(ИмяКаталога, ОбщегоНазначенияКлиентПовтИсп.ТипПлатформыКлиента());
		ИмяКаталога = __ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(ИмяКаталога, РассылкаОтчетовКлиент.ТипПлатформыКлиента());
		
		// Создать каталог для файлов
		Попытка
			СоздатьКаталог(ИмяКаталога);
			ИмяКаталогаТестовое = ИмяКаталога + "ПроверкаДоступа\";
			СоздатьКаталог(ИмяКаталогаТестовое);
			УдалитьФайлы(ИмяКаталогаТестовое);
		Исключение
			// нет прав на создание каталога, или такой путь отсутствует
			
			ТекстОшибки 
				= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Неверный путь или отсутствуют права на запись в каталог ""%1""'"),
				ИмяКаталога);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, , "РабочийКаталог");
			Возврат;
		КонецПопытки;
		
		РабочийКаталог = ИмяКаталога;
		//РаботаСФайламиВызовСервера.СохранитьРабочийКаталог(Объект.Ссылка, РабочийКаталог);
		РассылкаОтчетовВызовСервера.СохранитьРабочийКаталог(Объект.Ссылка, РабочийКаталог);
		
	КонецЕсли;
	
	ОбновитьОтображениеДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура РабочийКаталогВладельцаОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	РодительСсылка = __ОбщегоНазначенияСервер.ПолучитьЗначениеРеквизита(Объект.Ссылка, "Родитель");
	//РабочийКаталогРодителя = РаботаСФайламиВызовСервера.ПолучитьРабочийКаталог(РодительСсылка);	
	РабочийКаталогРодителя = РассылкаОтчетовВызовСервера.ПолучитьРабочийКаталог(РодительСсылка);
	
	//РабочийКаталогЭтойПапки = РаботаСФайламиВызовСервера.ПолучитьРабочийКаталог(Объект.Ссылка);
	//РабочийКаталогУнаследованный = РабочийКаталогРодителя + Объект.Наименование + ФайловыеФункцииСлужебныйКлиентСервер.ПолучитьСлеш(ОбщегоНазначенияКлиентПовтИсп.ТипПлатформыКлиента());
	
	РабочийКаталогЭтойПапки = РассылкаОтчетовВызовСервера.ПолучитьРабочийКаталог(Объект.Ссылка);
	РабочийКаталогУнаследованный = РабочийКаталогРодителя + Объект.Наименование + РассылкаОтчетовКлиентСервер.ПолучитьСлеш(РассылкаОтчетовКлиент.ТипПлатформыКлиента());
	
	Если ПустаяСтрока(РабочийКаталогРодителя) Тогда
		РабочийКаталог = "";
		//РаботаСФайламиВызовСервера.ОчиститьРабочийКаталог(Объект.Ссылка);
		РассылкаОтчетовВызовСервера.ОчиститьРабочийКаталог(Объект.Ссылка);
	ИначеЕсли РабочийКаталогУнаследованный <> РабочийКаталогЭтойПапки Тогда
		РабочийКаталог = РабочийКаталогУнаследованный;
		//РаботаСФайламиВызовСервера.СохранитьРабочийКаталог(Объект.Ссылка, РабочийКаталог);
		РассылкаОтчетовВызовСервера.СохранитьРабочийКаталог(Объект.Ссылка, РабочийКаталог);
	КонецЕсли;
	
	ОбновитьОтображениеДанных();
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ОткрытьСписокПапокИФайлов(Команда)
	
	ПараметрыФормы = Новый Структура("Папка", Объект.Ссылка);
	
	ОткрытьФорму("Справочник.Файлы.Форма.ХранилищеФайлов", ПараметрыФормы, ,Объект.Ссылка);
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ОбновитьПолныйПуть()
	
	ПапкаРодитель = __ОбщегоНазначенияСервер.ПолучитьЗначениеРеквизита(Объект.Ссылка, "Родитель");
	
	Если ЗначениеЗаполнено(ПапкаРодитель) Тогда
	
		ПолныйПуть = "";
		Пока ЗначениеЗаполнено(ПапкаРодитель) Цикл
			
			ПолныйПуть = Строка(ПапкаРодитель) + "\" + ПолныйПуть;
			ПапкаРодитель = __ОбщегоНазначенияСервер.ПолучитьЗначениеРеквизита(ПапкаРодитель, "Родитель");
			Если Не ЗначениеЗаполнено(ПапкаРодитель) Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
		ПолныйПуть = ПолныйПуть + Строка(Объект.Ссылка);
		
		Если Не ПустаяСтрока(ПолныйПуть) Тогда
			ПолныйПуть = """" + ПолныйПуть + """";
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры	

