
Функция ПолучитьОчереднойКод(Объект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Объект.ЭтоГруппа Тогда
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	МАКСИМУМ(СотрудникиОрганизаций.Код) КАК Код
			|ИЗ
			|	Справочник.СотрудникиОрганизаций КАК СотрудникиОрганизаций
			|ГДЕ
			|	СотрудникиОрганизаций.ЭтоГруппа
			|	И (НЕ СотрудникиОрганизаций.Ссылка = &Ссылка)");
		
	Иначе
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	МАКСИМУМ(СотрудникиОрганизаций.Код) КАК Код
			|ИЗ
			|	Справочник.СотрудникиОрганизаций КАК СотрудникиОрганизаций
			|ГДЕ
			|	СотрудникиОрганизаций.Организация = &Организация
			|	И (НЕ СотрудникиОрганизаций.Ссылка = &Ссылка)");
		Запрос.УстановитьПараметр("Организация", Объект.Организация);
	
	КонецЕсли;
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Выборка.Следующий() И ЗначениеЗаполнено(Выборка.Код) Тогда
		Возврат ОбщегоНазначения.ПолучитьСледующийНомер(СокрП(Выборка.Код));
	КонецЕсли;
	
	Возврат "0000000001";
	
КонецФункции // ПолучитьОчереднойКод()


Функция ДобавитьСотрудника(ФИО, Код, Организация) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ПустаяСтрока(Код) Тогда
		СотрудникСсылка = Справочники.СотрудникиОрганизаций.НайтиПоКоду(Код);
	КонецЕсли; 
	Если Не ЗначениеЗаполнено(СотрудникСсылка) Тогда
		
		СотрудникОбъект = Справочники.СотрудникиОрганизаций.СоздатьЭлемент();
		СотрудникОбъект.Код = Код;
		СотрудникОбъект.Наименование = ФИО;
		СотрудникОбъект.Актуальность = Истина;
		СотрудникОбъект.Организация = __ОбщегоНазначенияКлиентСервер.ПолучитьНеПустоеЗначение(Организация, Справочники.Организации.ОсновнаяОрганизация());
		
		Если ПустаяСтрока(СотрудникОбъект.Код) Тогда
			СотрудникОбъект.Код = ПолучитьОчереднойКод(СотрудникОбъект);
		КонецЕсли;
		
		СотрудникОбъект.Записать();
		
		СотрудникСсылка = СотрудникОбъект.Ссылка;
	КонецЕсли; 
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат СотрудникСсылка;
	
КонецФункции // ДобавитьСотрудника()
