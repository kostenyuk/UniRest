
&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)

	ОК(Неопределено);
	
КонецПроцедуры // СписокВыборЗначения()


&НаКлиенте
Процедура ОК(Команда)

	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если (ТекущиеДанные = Неопределено) Или ПустаяСтрока(ТекущиеДанные.Идентификатор) Тогда
		Закрыть(Неопределено);
	Иначе
		ОповеститьОВыборе(Новый Структура("Идентификатор,Представление", 
			ТекущиеДанные.Идентификатор,
			ТекущиеДанные.Представление));
	КонецЕсли; 
	
КонецПроцедуры // ОК()


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Выборка данных.
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ТорговоеОборудование.Идентификатор КАК Идентификатор,
	                      |	ТорговоеОборудование.Компьютер КАК Компьютер,
	                      |	ТорговоеОборудование.Модель КАК Представление,
	                      |	0 КАК ИндексКартинки
	                      |ИЗ
	                      |	РегистрСведений.ТорговоеОборудование КАК ТорговоеОборудование
	                      |ГДЕ
	                      |	ТорговоеОборудование.Модель.ОбработкаОбслуживания.Вид = &Вид
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	Компьютер,
	                      |	Представление
	                      |ИТОГИ
	                      |	ПРЕДСТАВЛЕНИЕССЫЛКИ(Компьютер) КАК Представление,
	                      |	2 КАК ИндексКартинки
	                      |ПО
	                      |	Компьютер
	                      |АВТОУПОРЯДОЧИВАНИЕ");
	Запрос.УстановитьПараметр("Вид", Параметры.Вид);
						  
	СтруктураОборудования = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);					  
						  
	ЗначениеВДанныеФормы(СтруктураОборудования, Список);
	
	// Текущая строка.
	
	// Настройка формы.
	ЗакрыватьПриВыборе = Истина;
	
КонецПроцедуры // ПриСозданииНаСервере()
