Процедура Зарегистрировать(СомнительнаяСитуация,Заказ,Значение=Неопределено,Пользователь=Неопределено) Экспорт

	НоваяСтрока					= СоздатьМенеджерЗаписи();
	НоваяСтрока.Дата			= ТекущаяДата();
	НоваяСтрока.СомнительнаяСитуация = СомнительнаяСитуация;
	Если Заказ<>Неопределено Тогда
		НоваяСтрока.Заказ		= Заказ.Ссылка;
	КонецЕсли;
	Если Пользователь<>Неопределено Тогда
		НоваяСтрока.Пользователь = Пользователь;
	Иначе
		НоваяСтрока.Пользователь = ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли; 
	НоваяСтрока.Компьютер		= ПараметрыСеанса.ТекущийКомпьютер; //ПолучитьСерверТО().ПолучитьКомпьютерТО();
	НоваяСтрока.Значение		= Значение;
	НоваяСтрока.Идентификатор	= Новый УникальныйИдентификатор;
	НоваяСтрока.Записать(Ложь);
	
	ОбменДанными.ПередатьСомнительнуюСитуацию(НоваяСтрока);
	
КонецПроцедуры

//Костенюк Александр-Старт 12.11.2012
// Функция формирует табличный документ с печатной формой 
//
// Параметры:
// ДатаНачала - Тип: Дата. Дата начала периода выборки данных.
// ДатаОкончания - Тип: Дата. Дата окончания периода выборки данных.
// Возвращаемое значение:
// ТабДокумент - Тип: ТабличныйДокумент. Сформированная печатная форма.
//
Функция Печать(ДатаНачала, ДатаОкончания, РежимПечати) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	СомнительныеСитуации.Идентификатор,
	               |	СомнительныеСитуации.Дата КАК Дата,
	               |	СомнительныеСитуации.СомнительнаяСитуация,
	               |	СомнительныеСитуации.Подтвержена,
	               |	СомнительныеСитуации.Пользователь,
	               |	СомнительныеСитуации.Компьютер,
	               |	СомнительныеСитуации.Заказ,
	               |	СомнительныеСитуации.Значение
	               |ИЗ
	               |	РегистрСведений.СомнительныеСитуации КАК СомнительныеСитуации
	               |ГДЕ
	               |	СомнительныеСитуации.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	               |	И СомнительныеСитуации.Подтвержена = &Подтвержена
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Дата";
	
	Если РежимПечати = "Неподтвержденные" Тогда
		Запрос.УстановитьПараметр("Подтвержена", Ложь);
	ИначеЕсли РежимПечати = "Подтвержденные" Тогда
		Запрос.УстановитьПараметр("Подтвержена", Истина);
	ИначеЕсли РежимПечати = "Все" Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "СомнительныеСитуации.Подтвержена = &Подтвержена", "ИСТИНА");
	КонецЕсли;

	Запрос.УстановитьПараметр("ДатаНачала"		, ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания"	, ДатаОкончания);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СомнительныеСитуации";
	
	Макет = ПолучитьМакет("Макет");
	
	// печать производится на языке, указанном в настройках пользователя
	КодЯзыкаПечать = Локализация.ПолучитьЯзыкФормированияПечатныхФорм(УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "РежимФормированияПечатныхФорм"));
	Макет.КодЯзыкаМакета = КодЯзыкаПечать;
	
	ОбластьМакетаЗаголовок 		= Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакетаШапкаТаблицы 	= Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьМакетаСтрока 		= Макет.ПолучитьОбласть("Строка");
	ОбластьМакетаПодвал 		= Макет.ПолучитьОбласть("Подвал");
	
	ОбластьМакетаЗаголовок.Параметры.ДатаНачала 	= ДатаНачала;
	ОбластьМакетаЗаголовок.Параметры.ДатаОкончания 	= ДатаОкончания;
	ТабДокумент.Вывести(ОбластьМакетаЗаголовок);
	ТабДокумент.Вывести(ОбластьМакетаШапкаТаблицы);
	
	НомерСтроки = 0;
	
	Пока Выборка.Следующий() Цикл
		
		НомерСтроки = НомерСтроки + 1;
		
		ОбластьМакетаСтрока.Параметры.НомерСтроки = НомерСтроки;
		ОбластьМакетаСтрока.Параметры.Заполнить(Выборка);
		ОбластьМакетаСтрока.Параметры.Пользователь = СокрЛП(Строка(Выборка.Пользователь));
		ТабДокумент.Вывести(ОбластьМакетаСтрока);
		
	КонецЦикла;
	
	ТабДокумент.Вывести(ОбластьМакетаПодвал);
	
	Возврат ТабДокумент;
	
КонецФункции
//Костенюк Александр-Финиш 12.11.2012
