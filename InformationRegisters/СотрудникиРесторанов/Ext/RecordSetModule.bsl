
// Обработчик события ПередЗаписью объекта.
//
Процедура ПередЗаписью(Отказ, Замещение)
	
	// При обмене данными ничего не проверяем.
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Нормализация.
	Для Каждого Запись Из ЭтотОбъект Цикл
		Запись.Экспедитор = Истина; // Пока так, если понадобится указвать не только экспедиторов, нужно удалить эту строку.
	КонецЦикла;
	
	//Костенюк Александр-Старт 12.05.2014
	// Регистрация сотруднков по ресторанам
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ОбменДанными2K_CRS.Ссылка КАК Узел,
	               |	СотрудникиРесторанов.Ресторан,
	               |	СотрудникиОрганизаций.Ссылка КАК Сотрудник
	               |ИЗ
	               |	РегистрСведений.СотрудникиРесторанов КАК СотрудникиРесторанов
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СотрудникиОрганизаций КАК СотрудникиОрганизаций
	               |		ПО СотрудникиРесторанов.Сотрудник = СотрудникиОрганизаций.Ссылка
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ПланОбмена.ОбменДанными2K_CRS КАК ОбменДанными2K_CRS
	               |		ПО СотрудникиРесторанов.Ресторан = ОбменДанными2K_CRS.Ресторан
	               |ГДЕ
	               |	ОбменДанными2K_CRS.Ссылка <> &ЭтотУзел"; 
	Запрос.УстановитьПараметр("ЭтотУзел", ПланыОбмена.ОбменДанными2K_CRS.ЭтотУзел());
	ДополнительныеСвойства.Вставить("СотрудникиРесторана", Запрос.Выполнить().Выгрузить());
	//Костенюк Александр-Финиш 12.05.2014
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ, Замещение)
	
	// При обмене данными ничего не проверяем.
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	//Костенюк Александр-Старт 12.05.2014
	// Регистрация сотруднков по ресторанам
	СотрудникиРесторана = Неопределено;
	ДополнительныеСвойства.Свойство("СотрудникиРесторана", СотрудникиРесторана);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СотрудникиРесторана.Сотрудник
	               |ПОМЕСТИТЬ СотрудникиРесторана
	               |ИЗ
	               |	&СотрудникиРесторана КАК СотрудникиРесторана
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ОбменДанными2K_CRS.Ссылка КАК Узел,
	               |	СотрудникиРесторанов.Ресторан,
	               |	СотрудникиОрганизаций.Ссылка КАК Сотрудник
	               |ИЗ
	               |	РегистрСведений.СотрудникиРесторанов КАК СотрудникиРесторанов
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СотрудникиОрганизаций КАК СотрудникиОрганизаций
	               |		ПО СотрудникиРесторанов.Сотрудник = СотрудникиОрганизаций.Ссылка
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ПланОбмена.ОбменДанными2K_CRS КАК ОбменДанными2K_CRS
	               |		ПО СотрудникиРесторанов.Ресторан = ОбменДанными2K_CRS.Ресторан
	               |		ПОЛНОЕ СОЕДИНЕНИЕ СотрудникиРесторана КАК СотрудникиРесторана
	               |		ПО СотрудникиРесторанов.Сотрудник = СотрудникиРесторана.Сотрудник
	               |ГДЕ
	               |	ОбменДанными2K_CRS.Ссылка <> &ЭтотУзел
	               |	И СотрудникиРесторана.Сотрудник ЕСТЬ NULL ";
	Запрос.УстановитьПараметр("СотрудникиРесторана", СотрудникиРесторана);
	Запрос.УстановитьПараметр("ЭтотУзел", ПланыОбмена.ОбменДанными2K_CRS.ЭтотУзел());
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НаборЗаписей = РегистрыСведений.СотрудникиРесторанов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Ресторан.Установить(Выборка.Ресторан);
		НаборЗаписей.Отбор.Сотрудник.Установить(Выборка.Сотрудник);
		ПланыОбмена.ЗарегистрироватьИзменения(Выборка.Узел, НаборЗаписей);
	КонецЦикла;
	//Костенюк Александр-Финиш 12.05.2014

КонецПроцедуры
