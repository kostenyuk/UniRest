
&НаКлиенте
Процедура ВидМенюПриИзменении(Элемент)
	
	ВидМенюПриИзмененииСервер();
	
КонецПроцедуры // ВидМенюПриИзменении()

&НаСервере
Процедура ВидМенюПриИзмененииСервер()
	
	// Принудительный сброс строки и родителя (иначе плотформа может повести себя неадекватно).
	Элементы.Меню.ТекущаяСтрока = Неопределено;
	Элементы.Меню.ТекущийРодитель = Неопределено;
	
	Меню.Параметры.УстановитьЗначениеПараметра("ВидМеню", ВидМеню);
	
КонецПроцедуры // ВидМенюПриИзменении()

&НаКлиенте
Процедура РесторанПриИзменении(Элемент)
	
	РесторанПриИзмененииСервер();
	
КонецПроцедуры // ВидМенюПриИзменении()

&НаСервере
Процедура РесторанПриИзмененииСервер()
	
	// Принудительный сброс строки и родителя (иначе плотформа может повести себя неадекватно).
	Элементы.РабочиеЦентрыНоменклатура.ТекущаяСтрока = Неопределено;
	Элементы.РабочиеЦентрыНоменклатура.ТекущийРодитель = Неопределено;
	
	// Рабочие центры.
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	КОЛИЧЕСТВО(РабочиеЦентры.Ссылка) КАК Количество
	                      |ИЗ
	                      |	Справочник.РабочиеЦентры КАК РабочиеЦентры
	                      |ГДЕ
	                      |	РабочиеЦентры.Актуальность
	                      |	И РабочиеЦентры.Владелец = &Ресторан");
	Запрос.УстановитьПараметр("Ресторан", Ресторан);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		КоличествоРабочихЦентров = Выборка.Количество;
	Иначе
		КоличествоРабочихЦентров = 0;
	КонецЕсли; 
	
	// Номенклатура.
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	                      |	СправочникНоменклатура.Ссылка КАК Номенклатура
	                      |ИЗ
	                      |	Справочник.Номенклатура КАК СправочникНоменклатура
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	                      |			ПравилаИспользованияНоменклатуры.ОбъектИспользования КАК Номенклатура
	                      |		ИЗ
	                      |			РегистрСведений.ПравилаИспользования КАК ПравилаИспользованияНоменклатуры
	                      |		ГДЕ
	                      |			ПравилаИспользованияНоменклатуры.ОбъектИспользования ССЫЛКА Справочник.Номенклатура
	                      |			И ПравилаИспользованияНоменклатуры.ВладелецПравилИспользования ССЫЛКА Справочник.РабочиеЦентры
	                      |			И ПравилаИспользованияНоменклатуры.ВладелецПравилИспользования.Актуальность
	                      |			И ПравилаИспользованияНоменклатуры.ВладелецПравилИспользования.Владелец = &Ресторан
	                      |			И НЕ ПравилаИспользованияНоменклатуры.Актуальность
	                      |		
	                      |		СГРУППИРОВАТЬ ПО
	                      |			ПравилаИспользованияНоменклатуры.ОбъектИспользования
	                      |		
	                      |		ИМЕЮЩИЕ
	                      |			КОЛИЧЕСТВО(ПравилаИспользованияНоменклатуры.ВладелецПравилИспользования) = &КоличествоРабочихЦентров) КАК ВыборкаНоменклатуры
	                      |		ПО СправочникНоменклатура.Ссылка = ВыборкаНоменклатуры.Номенклатура
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	                      |			ПравилаИспользованияНоменклатурныхГрупп.ОбъектИспользования КАК НоменклатурнаяГруппа
	                      |		ИЗ
	                      |			РегистрСведений.ПравилаИспользования КАК ПравилаИспользованияНоменклатурныхГрупп
	                      |		ГДЕ
	                      |			ПравилаИспользованияНоменклатурныхГрупп.ОбъектИспользования ССЫЛКА Справочник.НоменклатурныеГруппы
	                      |			И ПравилаИспользованияНоменклатурныхГрупп.ВладелецПравилИспользования ССЫЛКА Справочник.РабочиеЦентры
	                      |			И ПравилаИспользованияНоменклатурныхГрупп.ВладелецПравилИспользования.Актуальность
	                      |			И ПравилаИспользованияНоменклатурныхГрупп.ВладелецПравилИспользования.Владелец = &Ресторан
	                      |			И НЕ ПравилаИспользованияНоменклатурныхГрупп.Актуальность
	                      |		
	                      |		СГРУППИРОВАТЬ ПО
	                      |			ПравилаИспользованияНоменклатурныхГрупп.ОбъектИспользования
	                      |		
	                      |		ИМЕЮЩИЕ
	                      |			КОЛИЧЕСТВО(ПравилаИспользованияНоменклатурныхГрупп.ВладелецПравилИспользования) = &КоличествоРабочихЦентров) КАК ВыборкаНоменклатурныеГруппы
	                      |		ПО СправочникНоменклатура.НоменклатурнаяГруппа = ВыборкаНоменклатурныеГруппы.НоменклатурнаяГруппа
	                      |ГДЕ
	                      |	НЕ &Ресторан = ЗНАЧЕНИЕ(Справочник.Рестораны.ПустаяСсылка)
	                      |	И (&КоличествоРабочихЦентров = 0
	                      |			ИЛИ НЕ ВыборкаНоменклатуры.Номенклатура ЕСТЬ NULL 
	                      |			ИЛИ НЕ ВыборкаНоменклатурныеГруппы.НоменклатурнаяГруппа ЕСТЬ NULL )
	                      |	И НЕ СправочникНоменклатура.ЭтоГруппа");
	Запрос.УстановитьПараметр("Ресторан", Ресторан);
	Запрос.УстановитьПараметр("КоличествоРабочихЦентров", КоличествоРабочихЦентров);
	
	// Параметры.
	РабочиеЦентрыНоменклатура.Параметры.УстановитьЗначениеПараметра("Номенклатура", Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Номенклатура"));
	РабочиеЦентрыНоменклатурныеГруппы.Параметры.УстановитьЗначениеПараметра("Ресторан", Ресторан);
	РабочиеЦентрыНоменклатурныеГруппы.Параметры.УстановитьЗначениеПараметра("КоличествоРабочихЦентров", КоличествоРабочихЦентров);
	
КонецПроцедуры // ВидМенюПриИзменении()


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Владельцы.
	ВидМеню = Справочники.ВидыМеню.ОсновнойВидМеню();
	Ресторан = Справочники.Рестораны.ОсновнойРесторан();
	
	// Параметры.
	ВидМенюПриИзмененииСервер();
	РесторанПриИзмененииСервер();
	
	// Настройка формы.
	Элементы.РабочиеЦентрыНоменклатурныеГруппыГруппа.Видимость = ПолучитьФункциональнуюОпцию("РежимНастройкиРабочихЦентровНоменклатурныеГруппы"); 
	Элементы.РабочиеЦентрыНоменклатураГруппа.Видимость = ПолучитьФункциональнуюОпцию("РежимНастройкиРабочихЦентровНоменклатура"); 
	Если Элементы.РабочиеЦентрыНоменклатурныеГруппыГруппа.Видимость Тогда
		Если Не Элементы.РабочиеЦентрыНоменклатураГруппа.Видимость Тогда
			Элементы.РабочиеЦентрыНоменклатурныеГруппы.РастягиватьПоВертикали = Истина;
		КонецЕсли; 
	Иначе
		Если Не Элементы.РабочиеЦентрыНоменклатураГруппа.Видимость Тогда
			Элементы.РабочиеЦентрыНоменклатурныеГруппыГруппа.Видимость = Истина;
			Элементы.РабочиеЦентрыНоменклатураГруппа.Видимость = Истина;
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если __УправлениеДаннымиКлиентСервер.ОповещениеИзмененияРаспределениеНоменклатуры(ИмяСобытия) Тогда
		Элементы[Параметр].Обновить();
	КонецЕсли;
	Если __УправлениеДаннымиКлиентСервер.ОповещениеИзмененияНастроекРабочихЦентров(ИмяСобытия) Тогда
		РесторанПриИзмененииСервер();
	КонецЕсли;
	
КонецПроцедуры // ОбработкаОповещения()
